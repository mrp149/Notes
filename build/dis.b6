шифр 419999 зс5^
лен 50(1400-ЗП)^
лен 41(2048)^
лен 66(1990-ЗП)^
лен 67(1999-ЗП)^
лен 40(9)^
eeв1а3
*name DispDubna-2017
*     no list
*call putflag*
5
*perso:66000
*MADLEN,L=FBG.
 LDSHIFT:,NAME,
 ,XTA,61B
 ,XTA,=0010 3700 0040 0000
 ,AAX,=7777 0077 7777 7777
 ,AOX,=:0000 03
 ,ATX,C
 ,ARX,=1
 ,ATX,C+1
 ,DRUM,C
 ,XTA,BSHIFT
 ,ATX,7776B
 ,XTA,CSHIFT
 ,ATX,7777B
 ,DRUM,=0000 0300 0066 0036
 ,DRUM,C+1
 ,DRUM,=0000 0300 0066 0037
 ,XTA,=0010 3700 0066 0036
 ,ATX,61B
 13,UJ,
 BSHIFT:,LOG,64000
 CSHIFT:,LOG,70000
 C:,BSS,2
 ,END,
*tcatalog
*CALL FICMEMOR:75550
*CALL LDSHIFT
*perso:50000
*MADLEN,L=BRGF,MODEL=SIMH,STEXT=FREQ300.
 *DRUMBAS:,NAME,
C      PP 12-7 YKAЗATEЛЬ MБ
C      PP 6-1 HAЧAЛЬHЫЙ N TPAKTA
C         ДЛЯ ЗAПИCИ ДИCПETЧEPA
C
C      BAPИAHT ДЛЯ ЗAГPYЗKИ HA ЛEHTY:
C      ,LOG, S DD00 TTZZZ
C      ЗДECЬ TTZZZ - YKAЗATEЛЬ MЛ
C            DD - БAЗOBЫЙ БAPAБAH
C            S=1 - BMECTE C CИCTEMOЙ
C            S=0 - TOЛЬKO ДИCПETЧEP
C
C ,LOG,2500
 ,LOG,12000 67000
 ,END,
 EXCHNRS:,NAME,
C     PP 13-18 YKAЗATEЛЬ MБ
C     PP 1-5 HAЧAЛЬHЫЙ N TPAKTA
C     ДЛЯ HEPEЗИДEHTA MYЛЬTИTAЙПA
C        ,014,100B
C        ,025,37B
C     B CЛYЧAE ЗAГPYЗKИ HA ЛEHTY
C     39-Й PAЗPЯД = 0,  1-24 HE CYЩECTBEHHЫ
C
  ,010,100B
  ,,
 ,END,
 *ПДИCK:,NAME,.
C
C---  B 1-4 БAЙTAX ЭTOГO CЛOBA
C     ( CЧИTAЯ CЛEBA HAПPABO )
C   8-Й БИT COOTBETCTBYET 1-MY HAПPABЛ.
C   7-Й       --""--      2-MY  --""--
C     . . . . . . . . . . . . .
C   3-Й БИT   --""--      6-MY HAПPABЛ.
C
C---  HAЗHAЧEHИE БAЙTOB :
C   1 - ШKAЛA HAПPABЛEHИЙ KBY MЛ
C   2 - ШKAЛA HAПPABЛEHИЙ ДИCKOB
C   3 - ДИCKИ 29 MEГAБAЙT
C   4 - ДYБHEHCKИЙ KMД-29
C   5 - ПYCTOЙ БAЙT (OБЯЗATEЛЬHO)
C   6 - KOЛИЧECTBO ЭKCTEHTOB ДЛЯ
C       MOДEЛИPOBAHИЯ MБ.
C
C   ECЛИ 3-E HAПPABЛEHИE MД = OБЩEE,
C   ДOБABЬTE 34 БИT;
C   ECЛИ 4-E HAПPABЛEHИE MД = OБШEE,
C   TO ДOБABЬTE 33 БИT.
C
C  ,ISO,6H'054''040''000''000''''37'
  ,ISO,6H'000''060''000''000''''37'
C
C... B ДYБHE HET 4 HAПP.KBY MЛ
C    A ДИCKИ B 3 И 4 HAПPABЛ.
 ,END,.
 *DISCS:,NAME,.
  DNAM:,BSS,16.ИMEHA ДП
  :,BSS,16.PEЗEPB
  ШMOД:,OCT,7777 74.ШK.BOЗM.MOДEЛEЙ MЛ
  ШФЛД:,OCT,.    ШK.PEAЛЬHЫX MOДEЛEЙ
 ,END,.
  TYPE MT:,NAME,
C*
C***  ПOДKЛЮЧEHИE MAГHИTOФOHOB
C*
C*  1. 4 CЛOBA COOTBETCTBEHHO ЧETЫPEM
C*    KBY-HAПPABЛEHИЯM MЛ  ( 4 ГPYППЫ
C*    KOMAHД ДBИЖEHИЯ ) :
C*     1-6  : AДPECA CЛYЖ.CЛOB OБMEHA
C*      44  : ЗHAЧEHИE 11 PAЗPЯДA KYC-A
C*    47-48 : TИП MAГHИTOФOHOB :
C*        0 : HMЛ БЭCM-6
C*        1 : EC5012 C ДYБHEHCKИM KMЛ
C*        2 : EC5017 C ДYБHEHCKИM KMЛ
C*        3 : EC5017 C ЗABOДCKИM  KMЛ
C*
  KBY:,LOG,00000 00000 000050.3-5012
     :,LOG,60.4-CDC405,608
     :,LOG,00000 00000 000050.5-5012
     :,LOG,60.6-БЭCM
C*
C*  2. ПPИЗHAK HAЛИЧИЯ (БИT2) ИЛИ OTCYTCTBИЯ
C*     (0) CDC-405.  ФAKTИЧECKИ - ПPИЗHAK
C*     POДHOЙ ДYБHEHCKOЙ ФИPMЫ :
C*
   FL405:,LOG,0.ECTЬ
C*
C*  3. ПPИЗHAK ФИKCИPOBAHHOЙ (БИT48)
C*     ПЛOTHOCTИ ПPИ PAБOTE C MЛ EC
C*     И EE ЗHAЧEHИE (11-10 БИTЫ) :
C*
   FIXDENS:,LOG,.HE ФИKCИPOBAHO
C*
C*  4. AДPEC B KOMAHДE OПPOCA БAYC-5 (1-15)
C*     И ЧИCЛO CДBИГOB BПPABO (48-42)  :
C*
   БAYC:,OCT,4600 0000 0000 4011.CД=12
C       ДЛЯ ЗABOДCKOГO KBY EC :
C      ,OCT,4000 0000 0000 4012.5017
C*
C*  5. ШKAЛA БЛOKИPOBOK OПOЗHABAHИЯ ЛEHT
C*     48:16  - MФ 30:67
C*
C  NORECTP:,LOG, 7077 7777 7770 0000
  NORECTP:,LOG, 0
  ,END,
C
C  TERMINALS
C
 *TLSIZE:,NAME,
C
C*****   HACTPOЙKA ПO TEPMИHAЛAM
C           (PAЗMEPЫ БYФEPOB)
C
C-----   ДЛИHA KAPTЫ HAПPЯЖEHИЙ
  ,LOG,74
C-----   KOЛИЧECTBO ФИЗ.KAHAЛOB
  ,LOG,31
C-----   MAKCИMAЛЬHOE KOЛ-BO KOHCOЛEЙ
 ,LOG,2
C-----   KOЛ-BO TEPMИHAЛЬHЫX ПOДCИCTEM
 ,LOG,4
 ,END,
 *TLSYS:,NAME,
C
C*****   HACTPOЙKA ПO TEPMИHAЛAM
C         (OПИCAHИЯ ПOДCИCTEM)
C
C-----   COCTOЯHИE
C  PP.48/43 - Д H З  ПOДCИCTEMЫ
C  PP.42/19 - ШKAЛA ПPИBЯЗ. ФИЗ.KAHAЛOB
C  PP.18/17 - P E З E P B
C  PP.   16 - ИДET ЗAПYCK П/C
C  PP.15/01 - AДPEC CTPOЧHИKA П/C
C             ( 0 - CTAHДAPT )
 ,BSS,4
C-----   ПPИBЯЗKA
C  PP.48/37 - ШKAЛA ДOПYCTИMЫX ЛOГ.KAHAЛOB
C  PP.36/25 - ШKAЛA ПPИBЯЗ. ЛOГ.KAHAЛOB
C  PP.24/01 - ШKAЛA CTYЧAЩ. ФИЗ.KAHAЛOB
 ,OCT,6000.*=* CONS
 ,OCT,1774.*=* MULTI
 ,OCT,7774.*=* SCREEN
 ,OCT,7774.*=* MONITO
C-----   METKA BPEMEHИ + OTBЯЗKA
C  PP.48/25 - METKA BPEMEHИ ПO ФИЗ.KAHAЛAM
C  PP.24/01 - ШKAЛA OTBЯЗKИ ФИЗ.KAHAЛOB
 ,BSS,4
 ,END,
  *TLCONN:,NAME,
C
C*****   KOHCTAHTЫ ДЛЯ ПPИBЯЗKИ
C
C-----   KOДЫ ПPИBЯЗKИ K ПOДCИCTEMAM
C  PP.48/33 - 2 CИMBOЛA ДЛЯ ПPИBЯЗKИ,
C             B OБPATHOM ПOPЯДKE
C  PP.15/01 - HOMEP ПOДCИCTEMЫ
  ,ISO,6H '''''''''1'
  ,ISO,6H  '''''''1'
  ,ISO,6HTM'''''''1'
  ,ISO,6HPO'''''''0'
C
C
  MAIN*C:,ENTRY,
C
C*****   PAЗPEШEHИE ПPИBЯЗKИ K ПOДCИCTEMAM
C
C  PP.48/25 - ШKAЛA ФИЗ.KAHAЛOB, ДЛЯ KOTO-
C             PЫX ПPИBЯЗKA PAЗPEШEHA
C
C-----   OПEPATOPCKИE KOHCOЛИ
C  PP.15/01 - HOMEP ПEPBOЙ OПEPATOPCKOЙ
C             KOHCOЛИ ПO YMOЛЧAHИЮ
  ,OCT,40004000 000 00001
C
C-----   ДPYГИE ПOДCИCTEMЫ
C  PP.15/01 - HOMEPA ЗAДAЧ ДЛЯ STARTJOB
  ,OCT,00000000 00000005.  MT
  ,OCT,00000000 00000067.  ЭP
  ,OCT,00004000 00000002.
C
  ,END,
 *TLPRMT:,NAME,
C
C*****   HACTPOЙKA ПO TEPMИHAЛAM
C         ПAPAMETPЫ ФИЗ.KAHAЛOB
C
C        PAБOЧAЯ ЯЧEЙKA:
C
C    B MOMEHT ЗAГPYЗKИ - ЧACTOTA BPEMEHHЫX
C    ПPEPЫBAHИЙ, ДEЛEHHAЯ HA 150:
C    1 - 150 ГЦ,  2 - 300 ГЦ,  4 - 600 ГЦ,
C                10 - 1200 ГЦ.
  ,LOG,2
C    BO BPEMЯ PAБOTЫ - HOMEP KAHAЛA TИПA
C    TEKTRONIX (ФOPMИPYETCЯ П/П STARTDD).
C
C
C        MACKИ KAHAЛOB BBOДA И BЫBOДA:
C
C    48/25 P. MIN И MOT ДOЛЖHЫ COBПAДATЬ.
C    OHИ COOTBETCTBYЮT TИПAM KAHAЛOB:
C    48 P. - 1-Й KOHCYЛЬCKИЙ KAHAЛ,
C    47 P. - 2-Й KOHCYЛЬCKИЙ KAHAЛ,
C    46 - 31 P. - KAHAЛЫ MYЛЬTИПЛEKCOPA,
C    30 - 26 P. - ПOKA HE ЗAДEЙCTBOBAHЫ,
C    25 P. - KAHAЛ TИПA "TEKTRONIX".
C    "1" B 24/1 P. MIN COOTBETCTBYЮT
C    TEM PAЗPЯДAM TEЛEГPAФHOГO PEГИCTPA,
C    ПO KOTOPЫM ИДET BBOД.
C    "1" B 24/1 P. MOT COOTBETCTBYЮT
C    TEM PAЗPЯДAM TEЛEГPAФHOГO PEГИCTPA,
C    ПO KOTOPЫM ИДET BЫBOД.
C
C       MAKCИMAЛЬHOE ЧИCЛO KAHAЛOB - 24.
C
C  ,LOG,77770000 7777 0000. MIN
C  ,LOG,77770000 0000 7777. MOT
  ,LOG,60000000 7777 0000. MIN
  ,LOG,60000000 0000 7777. MOT
C
C     ДAЛEE  E,P,U-XAPAKTEPИCTИKИ TPEX ГPYПП
C     ПOCЛEДOBATEЛЬHO ПOДKЛЮЧEH. TEPMИHAЛOB.
C
C
C        XAPAKTEPИCTИKA E :
C        42/28 - ФAЗA OT CTAPTA KOHЦA CИMB.
C        24/1  - ШKAЛA KAHAЛOB ДAHHOГO TИПA
C                ( ПO BBOДY И BЫBOДY )
C
 ,LOG,777670 1700 1700
  ,LOG,777550 6077 6077
 ,LOG,
C
C        XAPAKTEPИCTИKA P :
C        45/31 - HAЧAЛO CБOPKИ CИMBOЛA
C        30/16 - ШAГ ПO KAPTE HAПPЯЖEHИЙ
C        15/1  - KOЛИЧECTBO БИT CИMBOЛA
C
 ,LOG,77706 77705 77772
 ,LOG,77710 77706 77772
 ,LOG,
C
C        XAPAKTEPИCTИKA U :
C        45/31 - HAЧAЛO PAЗБOPKИ CИMBOЛA
C        30/16 - ШAГ ПO KAPTE HAПPЯЖEHИЙ
C        15/1  - OTHOCИT.ФAЗA KOHЦA CИMBOЛA
C
 ,LOG,77777 77777 77766
 ,LOG,77777 77776 77754
 ,LOG,
C
C     OПИCATEЛЬ MYЛЬTИПЛEKCOPA TEPMИHAЛOB
C
C      I.  0 - HET HИKAKИX MYЛЬTИПЛEKCOPOB
C
C     II.  MYЛЬTИПЛEKCOP ИBTAH.
C          ПOДKЛЮЧEHИE ПO 19 P. ПPП.
C          1-4  : MACKИ BBOДA/BЫBOДA
C          5-8  : ДOП.PEГИCTP ПPEPЫBAHИЙ
C
C    III.  MYЛЬTИПЛEKCOP  Г O И
C          ПOДKЛЮЧEHИE HA 12-11 P. ПPП.
C          HACTPAИBAETCЯ "1" B 48-47 PAЗPЯДAX.
C          (48P. COOTBETCTBYET 12 P.ПPП,
C           47P. COOTBETCTBYET 11 P.ПPП)
C          MACKA 12-11 P.ПPП OTKPЫBAETCЯ TOЛЬKO
C          ПPИ ЗAЖATOM 12-11 P. HA 6 TYMБЛ.PEГ.
C
C  ,LOG,4000 0000 0000 0000
   , log, 0
C
 ,END,
  *TLBUFI:,NAME,.БЫBШИЙ  C I N
C
C*****   HACTPOЙKA ПO TEPMИHAЛAM
C         OПИCAHИE ФИЗ.KAHAЛOB
C
C.....  ЯЧEЙKA 0 :
C       48:25 - ШKAЛA ФИЗ.KAHAЛOB, ДЛЯ
C         KOTOPЫX PAЗPEШEHA ПPИBЯЗKA K
C         "OPERTERM".
C       5:1   - HOMEP ГЛABHOЙ OПEPATOPCKOЙ
C         KOHCOЛИ ПPИ OTCYTCTBИИ HAБOPA
C         B 5:1 P. 2 TYMБЛEPHOГO PEГИCTPA.
C
    0,LOG,40004000 000000 01
C
C
C.....  ЯЧEЙKИ 1 - 24 : OПИCATEЛИ TEPM.KAHAЛOB
C        ( HACTPOEЧHЫE PAЗPЯДЫ  -  31:19  )
C
C PP.48/41 - BBEДEHHЫЙ CИMBOЛ
C    40/33 - ПPEДЫД.CИMBOЛ (HA CBOБ.KAHAЛE)
C    40/36 - PEЗEPB (HA ПPИBЯЗ.KAHAЛE)
C    40/37 - KOД CЛYЖ.ПOЛYБAЙTA ДЛЯ MPX
C    35/33 - HOM.ПOДCИCT.(HA ПPИBЯЗ.KAHAЛE)
C            0 = OPERTERM
C            1 = MULTITYP
C            2 = ЭKPAHHЫЙ PEДAKTOP
C            3 = MOHИTOP ПOДCИCTEM
C       32 - ECTЬ CИMBOЛ (ДЛЯ BINARY-INPUT)
C       31 - ECTЬ "ЗBOHOK"
C       30 - HYЖHA ЗAДEPЖKA ДЛЯ ROLL
C       29 - БЛOKИPOBKA PEГEHEPAЦИИ
C       28 - CИMBOЛ "LF" ДOПOЛHЯTЬ "CR"
C       27 - HET ПEPEKOДИPOBKИ BBOДA
C       26 - HET ПEPEKOДИPOBKИ BЫBOДA
C       25 - ПPИЗHAK CИHXPOHИЗAЦИИ KAHAЛA
C    24/22 - TИП TEPMИHAЛA
C            0 = VT-340
C            1 = CETEBOЙ (KOД ASCII)
C            2 = KOHCYЛ
C            3 = VDT-52130
C            4 = 15-ИЭ (ЭЛEKTPOHИKA)
C    21/19 - TИП ФИЗИЧECKOГO KAHAЛA
C            0 = ПOCЛEДOBATEЛЬHЫЙ KAHAЛ
C            1 = KOHCYЛ
C            2 = BИДEOTOH/KOHCYЛ
C            4 = KAHAЛ ПЭM-2
C       18 - PEЗEPB
C       17 - BЫB.PEГИCTP 15-ИЭ
C       16 - BB.PEГИCTP 15-ИЭ
C            БЫЛ "ESCAPE" (VDT-52130)
C     15/1 - AДPEC OПИCATEЛЯ BИPT.KAHAЛA
C
C*****   CTPYKTYPA OПИCATEЛЯ BИPTYAЛЬHOГO
C                    KAHAЛA
C
C    0 = LJOB - ЗAKAЗ HA PAБOTY
C               И COCTOЯHИE ЗAKAЗA.
C    P.48 - HAЧAЛO PAБOTЫ ПO JOB
C      47 - "LF" ПO HAЧAЛY CTPOKИ
C      46 - "CUP" ПO HAЧAЛY CTPOKИ
C      45 - "EOT" (ПEPEXOД HA BBOД)
C      44 - OБPAБOTKA INSERT CHAR.
C      43 - OБPAБOTKA DELETE CHAR.
C      42 - BBOД/BЫBOД CTPOKИ
C      41 - ЗAKAЗ HA OTBЯЗKY
C      40 - ГEHEPAЦИЯ COБЫTИЯ ПOДCИCTEME
C      39 - PEЗEPB
C      38 - CTOПOP BЫBOДA (БЫЛ CNTR/S)
C      37 - ПPИЗHAK BEДEHИЯ TAЙMEPA
C      36 - ПPИЗHAK BBOДA
C      35 - ПPИЗHAK ЗACEKPEЧИBAHИЯ "*"
C      34 - ЗAKOHЧEHA PAБOTA ПO CTPOKE
C      33 - ПPEPBAH BЫBOД,
C           ЛИБO CPAБOTAЛ TAЙMEP HA BBOДE
C      32 - ЛOПHYЛA ДЛИHA CTPOKИ
C      31 - PEЗEPB
C      30 - PEЗEPB
C      29 - БЛOKИPOBKA PEГEHEPAЦИИ ПPИ BBOДE
C      28 - ПPИЗHAK "ПPOЗPAЧHOГO" BBOДA
C      27 - PEЗEPB
C      26 - PEЗEPB
C      25 - ПPИЗHAK HEПYCTOГO BBOДA
C      24 - ПPИЗHAK ПEPEПOЛHEHИЯ CЧETЧИKA
C      23/16 - TEK.CЧETЧИK CИMBOЛOB
C      15/1  - TEK.AДPEC ПO БYФEPY BB/BЫB.
C
C    1 = MSK - MACKA ПOЗИЦИИ CИMBOЛA B TEK.
C              CЛOBE БYФEPA BBOДA/BЫBOДA
C
C    2 = XNT - CCЫЛKA HA ФИЗ. KAHAЛ
C       48/41 - TEK.TAЙMEP ПPИ BBOДE
C       40/33 - HAЧ.TAЙMEP HA BBOД CИMBOЛA
C       32/21 - PEЗEPB
C       20/16 - HOMEP ЛOГИЧECKOГO KAHAЛA
C       15/01 - HOMEP ФИЗИЧECKOГO KAHAЛA
C
C    3 = OWNER - BЛAДEЛEЦ KAHAЛA
C       48/43 - ДHЗ, ПOДABШEЙ ЭKCTPAKOД
C       10/01 - HOMEP PASS ПOЛЬЗOBATEЛЯ
C
C    4 = WSC - PAБOЧAЯ ЯЧEЙKA ДЛЯ PEAЛИЗAЦИИ
C              "IC" И "DC"
C       ПOCЛE OTPAБOTKИ ЗAKAЗA:
C       8/1 - CИMBOЛ OKOHЧAHИЯ BBOДA, ЛИБO
C             CИMBOЛ ПEPEБИBKИ BЫBOДA.
C
C    5 = ... - "-1" ЯЧEЙKA CTPOKИ
C
C    6 = STRI(14) - БYФEP BBOДA.
C        OПEPAЦИИ "IC" И "DC" PEAЛИЗYЮTCЯ
C        TOЛЬKO B ПPEДEЛAX ЭTOГO БYФEPA.
C
C        БYФEP BЫBOДA MOЖET PACПOЛAГATЬCЯ
C        B ДPYГOM MECTE.
C
C
C=====   HACTPOЙKA ПO TИПAM TEPMИHAЛOB
C
C---  KOHCYЛЫ
  1,LOG,0001 000000.VT-CONS 1
  2,LOG,0001 000000.VT-CONS 2
C---  MYЛЬTИПЛEKCOP ИBTAH
  3,LOG,0000 000000.CETЬ-0
  4,LOG,0000 000000.CETЬ-1
  5,LOG,0000 000000.CETЬ-2
  6,LOG,0000 000000.CETЬ-3
  7,LOG,0000 000000.CETЬ-4
  8,LOG,0000 000000.CETЬ-5
  9,LOG,0000 000000.CETЬ-6
 10,LOG,0000 000000.CETЬ-7
 11,LOG,0000 000000.CETЬ-8
 12,LOG,0000 000000.CETЬ-9
C---  TEЛEГPAФHЫЙ PEГИCTP
 13,LOG,0000 000000.VT-340
 14,LOG,0000 000000.VT-340
 15,LOG,0000 000000. V D T
 16,LOG,0000 000000.VT-340
 17,LOG,0000 000000.---"---
 18,LOG,0000 000000.---"---
 19,LOG,0000 000000.---"---
 20,LOG,0000 000000.---"---
 21,LOG,0000 000000.---"---
 22,LOG,0000 000000.---"---
 23,LOG,0000 000000.---"---
 24,LOG,0000 000000.---"---
 ,END,


C
C FIXES FOR TERMS
C
 BIO*TL:,NAME,.20.02.78
C
C*****   OБCЛYЖИBAHИE TEЛEГPAФHOГO PEГИCTPA
C*****      ( БИTOBOE , MACCOBOE )
C
 *TLSIZE:LC,BLOCK,LMTR
 M:,WEQ,LMTR.ДЛИHA MATPИЦЫ
 *TLMTR:,LC,M+1
 *TLMTR:I,BLOCK,XF,MTR
 *TLPRMT:LC,BLOCK,(1),MIN,MOT,X*E(3)
 *TLSTAT:LC,BLOCK,BB,B/FREE,N/FREE,LAST
 ,CONT,OUT,REQOUT(2),JIN,JOUT
 *YCЫ:LC,BLOCK,(2),COMMIR
 WSD:LC,BLOCK,X1
 RETURN:,SUBP,
  ITIMEB10:,SUBP,
  IFJOB*TL:,SUBP,
  B24/1:,LC,1
 FULWOR:,LC,1
 *701:B,BLOCK,B47,B46(6),B40(9),B31(16),B15(14),B1
C-----   ИCПOЛЬЗYEMЫE ИHДEKC-PEГИCTPЫ
 F:,EQU,13
 E:,EQU,14
 R:,EQU,15	
C
C-----   KOPPEKЦИЯ ФAЗЫ
 ,XTA,XF
 ,A-X,B1
 ,UZA,PHA P
 ,A+X,LMTR
 PHA P:,ATI,F
 ,ATX,XF.
C-----   TL - BЫДAЧA (ДИФФEP.KOД)
 F,XTA,MTR
 ,AAX,MOT
 ,AEX,OUT
 ,EXT,140B. O U T P U T
 ,ATX,OUT
C-----
 F,XTA,MTR
 ,ASN,64+24.- ECTЬ KOHЦЫ
 ,UZA,INPUT. = HET
C=====   KOHЦЫ CИMBOЛOB (BBOД/BЫBOД)
 ,ATX,BB
 R,VJM,FREE
 ,XTA,BB
 ,APX,MIN
 ,AOX,J IN
 ,ATX,J IN.= BBEДEHO
 INPUT:,BSS,
C-----   BBOД + HAЧAЛA CИMBOЛOB
 ,EXT,4100B. I N P U T
CS ,EXT,146B.CИHXPOHИЗAЦИЯ
 ,AAX,MIN
 F,ATX,MTR.- KOHЦЫ И BЫBOД
 ,AAX,LAST
 ,AAX,B/FREE
 ,U1A,BEG CHR.= ECTЬ CTAPTЫ
  *ЧACTOTA:,IFGE,FREQ,600
  *TL/ERR:LC,BLOCK,T31,N1*ER
 ,XTA,T31.KOHTPOЛЬ 31P
 ,UZA,F LAST. -> HET
 ,XTA,B/FREE
 ,AEX,FULWOR
  ,AAX,B24/1
 ,U1A,F LAST. -> HEЛЬЗЯ
 ,ATX,T31
  ,XTA,B*START
 ,AOX,B40
 ,AEX,FULWOR
 ,MOD,37B
 ,AEX,FULWOR
 ,AEX,COMMIR
 ,ATX,COMMIR
 ,MOD,36B
  *ЧACTOTA*:,ENDIF,
 F LAST:,BSS,
C-----   ЛOBЛЯ HYЛЯ B TL-PEГИCTPE
 F,XTA,MTR
 ,AEX,MIN.CTPOБ ДЛЯ
 ,ATX,LAST.CTAPTOBЫX
C-----   ПPOБA ГOTOBHOCTИ BЫBOДA
 ,XTA,REQ OUT.
 ,AAX,N/FREE.
 ,UZA,IF JOB. = HET
C=====   ЗAПYCK BЫBOДA CИMBOЛOB
 ,AOX,J OUT
 ,ATX,J OUT.ЗAKAЗ BЫBOДA
 ,AUX,MOT
 R,VJM,BUSY.
  IF JOB:,BSS,
  *ЧACTOTA:1,IFGE,FREQ,600
  14,VTM,RETURN
  *ЧACTOTA:1,IFLT,FREQ,600
  14,VTM,ITIMEB10
  ,UJ,IFJOB*TL
C
C=====   HAЧAЛA CИMBOЛOB (BBOД)
 BEG CHR:,BSS,
 ,ATX,BB.BEGIN-BIT
 R,VJM,BUSY. PUT '0'
C-----   ЗAПИCЬ KOHЦOB B COOTBETCTBИE
C        C TИПOM TEPMИHAЛA
 F,VTM,-2
 R,VTM,EL BEG
 L BEG:,XTA,BB
 F,AAX,X*E+2.HAШ TИП
 ,ATX,X1. (B БPЗ)
 ,U1A,P BEG. = ДA
 EL BEG:F,VLM,L BEG
 ,WTC,XF
 F,VTM,.BOCCTAH.ФAЗЫ
  *ЧACTOTA:2,IFGE,FREQ,600
 ,XTA,
 ,ATX,T31
 ,UJ,F LAST
 P BEG:F,XTA,X*E+2
 ,ASN,64+27
 ,ATI,E.- AДPEC KOHЦA
 ,XTA,X1
C
C=====   ЗAПИCЬ KOHЦOB CИMBOЛOB
C
 WRE*TL:,ENTRY,
 ,ASN,64-24
 ,ATX,X1. - KOHЦЫ
 ,WTC,XF
 E,UTM,.ФAЗA KOHЦA
 ,ITA,E
 ,AAX,B15
 ,UZA,WR 1
 E,UTM,M. - KOЛЬЦO
 WR 1:,XTA,X1
 E,AOX,MTR
 E,ATX,MTR
 R,UJ,
C
C*****   YCTAHOBKA CTPOБOB CTAPTOB И BЫДAЧИ
C
 FREE*TL:,ENTRY,
 FREE:,BSS,
 ,AOX,B/FREE
 ,UJ,PUT*
C
C*****   ГAШEHИE CTPOБOB CTAPTOB И BЫДAЧИ
C
 BUSY*TL:,ENTRY,
 BUSY:,AEX,FULWOR
 ,AAX,B/FREE
 PUT*:,ATX,B/FREE
 ,APX,MOT
 ,ATX,N/FREE
 R,UJ,
C*****
 ,END,
*edit
*ll
 MPX*T:,NAME,
C
C*****   ДPAЙBEP MYЛЬTИПЛEKCOPA ГOИ
C
 *TLSIZE:LC,BLOCK,(3),XSS
 *TLBUFI:LC,BLOCK,CIN
 *TLSTAT:LC,BLOCK,(5),REQOUT(2),JIN,JOUT
 *TLPRMT:LC,BLOCK,(2),MOT(10),COUNT
 *TLSYS :LC,BLOCK,STATSS
 BUSY*TL:,SUBP,
 FREE*TL:,SUBP,
 IFJOB*TL:,SUBP,
 RETURN:,SUBP,
 WSD:LC,BLOCK,WS1(2),NBB
 C377:,LC,1
 C7:,LC,1
 *700:B,BLOCK,B48(2),B46(30)
 ,CONT,B16(4),B12(11),B1
 *12:,BLOCK,S,F,N,R
 NSS:,WEQ,XSS
 NS/1:,EQU,NSS-1
C
C=====   PEAKЦИЯ HA ПPEPЫBAHИE
C
 ,XTA,COUNT
 ,ATI,F
 ,ARX,B1
 ,AAX,C7
 ,ATX,COUNT
 F,V1M,CONT1
 N,EXT,4174B
 ,ATX,SYM
 ,XTA,C377
 N,EXT,174B
 ,ATX,
 CONT1:,XTA,SYM
 ,UZA,CONT2
 ,AEX,C377
 ,UZA,CONT2
 F,AOX,BUFSYM
 F,ATX,BUFSYM
 F,XTA,B46
 ,APX,MOT
 ,AOX,JIN
 ,ATX,JIN
 CONT2:N,EXT,4174B
 ,ATX,SYM
 F,XTA,BUFSYM
 ,ASN,64+8
 ,UZA,CONT3
 ,ATX,CIN
 F,XTA,B46
 R,VJM,FREE*TL
 F,XTA,B46
 ,APX,MOT
 ,AAX,REQOUT
 ,UZA,PUT
 ,AOX,JOUT
 ,ATX,JOUT
 F,XTA,B46
 R,VJM,BUSY*TL
 PUT:F,XTA,BUFSYM
 ,AAX,C377
 F,ATX,BUFSYM
 ,XTA,CIN
 CONT3:N,EXT,174B
 ,ATX,
 F,UTM,-7
 F,VZM,RETMPX
 N,XTA,B12
 ,EXT,142B
 ,UJ,IF JOB
 RETMPX:R,VTM,-NS/1
 ,XTA,
 :R,AOX,STATSS+NS/1
 R,VLM,*
 ,ASN,64-6
 ,AUX,MOT
 ,AAX,MPXCH
 ,U1A,IF JOB
 N,XTA,B12
 ,CALL,CLOPIR
 IF JOB:14,VTM,RETURN
 ,UJ,IFJOB*TL
C
C=====   ПEPEДAЧA BBEДEHHOГO CИMBOЛA
C
 MPX*INP:,ENTRY,
 F,XTA,BUFSYM-3
 ,AAX,C377
 F,AEX,BUFSYM-3
 F,ATX,BUFSYM-3
 ,YTA,
 14,UJ,
C
C=====   БYФEPИЗAЦИЯ CИMBOЛA ДЛЯ BЫBOДA
C
 MPX*OUT:,ENTRY,
 ,ITA,S
 S,VZM,CL
 ,ASN,64-8
 ,UTC,B16-B48
 CL:,AEX,B48
 F,AOX,BUFSYM-3
 F,ATX,BUFSYM-3
 14,UJ,
C
C=====
C
  MPX*TIM:,ENTRY,
  14,UJ,
C
 MPXCH:,OCT,1774
 SYM:,BSS,1
 BUFSYM:,BSS,8
 ,END,
*ee
*      edit
*      LL
C
CS DUMMY MPX*T
C
 MPX*T:,NAME,
C
C***** PSEUDO  ДPAЙBEP MYЛЬTИПЛEKCOPA ГOИ
C
 *TLSIZE:LC,BLOCK,(3),XSS
 *TLBUFI:LC,BLOCK,CIN
 *TLSTAT:LC,BLOCK,(5),REQOUT(2),JIN,JOUT
 *TLPRMT:LC,BLOCK,(2),MOT(10),COUNT
 *TLSYS :LC,BLOCK,STATSS
 BUSY*TL:,SUBP,
 FREE*TL:,SUBP,
 IFJOB*TL:,SUBP,
 RETURN:,SUBP,
 WSD:LC,BLOCK,WS1(2),NBB
 C377:,LC,1
 C7:,LC,1
 *700:B,BLOCK,B48(2),B46(30)
 ,CONT,B16(4),B12(11),B1
 *12:,BLOCK,S,F,N,R
 NSS:,WEQ,XSS
 NS/1:,EQU,NSS-1
C
C=====   PEAKЦИЯ HA ПPEPЫBAHИE
C
 ,EXT, 4177B
 14,VTM,RETURN
 ,UJ,IFJOB*TL
C
C=====   ПEPEДAЧA BBEДEHHOГO CИMBOЛA
C
 MPX*INP:,ENTRY,
 14,UJ,
C
C=====   БYФEPИЗAЦИЯ CИMBOЛA ДЛЯ BЫBOДA
C
 MPX*OUT:,ENTRY,
 14,UJ,
C
C=====
C
  MPX*TIM:,ENTRY,
  14,UJ,
C
 MPXCH:,OCT,1774
 SYM:,BSS,1
 BUFSYM:,BSS,8
 ,END,
*      ee
 CONINT/I:,NAME,
C
C*****   OБPAБOTKA ПPEPЫBAHИЙ ПO
C*****   ПAPAЛЛEЛЬHЫM TEPM.KAHAЛAM
C
  *TLPRMT:LC,BLOCK,(2),MOT
 *TLSTAT:LC,BLOCK,(5),REQOUT(2),JIN,JOUT
  *CB:LC,BLOCK,(1),C17
 IFJOB*TL:,SUBP,
  FREE*TL:,SUBP,
 BUSY*TL:,SUBP,
   CLOPIR:,SUBP,
   RETURN:,SUBP,
  WSD:LC,BLOCK,WS1
 *700:B,BLOCK,B48(11),B37(18)
 ,CONT,B19(3),B16(4),B12(2)
 ,CONT,B10(6),B4(2),B2,B1
C
C*****   CONSUL VT/CONS - INPUT
C
 14,XTA,B12
 ,CALL,CLOPIR
  14,XTA,B48
  14,VTM,RETURN
C
  IF*INP:,ENTRY,
C
  ,APX,MOT
 ,AOX,J IN
 ,ATX,J IN
  ,UJ,IFJOB*TL
C
C*****   CONSUL VT/CONS - OUTPUT
C
 CONINT/O:,ENTRY,
 14,XTA,B10
 ,CALL,CLOPIR
  14,XTA,B48
  14,VTM,RETURN
C
  IF*OUT:,ENTRY,
C
  ,ATX,WS1
  ,APX,MOT
  15,VTM,IFJOB*TL
 ,AAX,REQOUT
 ,UZA,PUT
 ,AOX,J OUT
 ,ATX,J OUT
  ,XTA,WS1
  ,UJ,BUSY*TL
  PUT:,XTA,WS1
  ,UJ,FREE*TL
 ,END,
 SDRV*INP:,NAME,
C
C*****   ПAPAЛЛEЛЬHЫЙ ФИЗ.ДPAЙBEP
C*****       YPOBHЯ  "C I O"
C
 *TLBUFI:LC,BLOCK,CHR(0),CIN
 *TLBUFO:LC,BLOCK,WS
 WSD:LC,BLOCK,WS1(2),NBB
 *TLCONST:LC,BLOCK,(5),M*TYC(6),C360
 SER*INP:,SUBP,
 SER*OUT:,SUBP,
 PERPIR:,SUBP,
  MPX*INP:,SUBP,
  MPX*OUT:,SUBP,
 C377:,LC,1
 *700:B,BLOCK,B48,B47(23),B24(5),B19(7)
  ,CONT,B12,B11,B10,B9,B8,B7
  ,CONT,B6,B5,B4,B3,B2,B1
 N:,EQU,10
 S:,EQU,12
  F:,EQU,13
C
C*****   BBOД C TEPMИHAЛOB
C
 N,XTA,CIN
 ,APX,M*TYC.ПOCЛEД.KAHAЛ
C ,UZA,SER*INP.-> ДA
C ,33,
 ,EXT,4174B
 ,ATX,CHR
 INP 1:,XTA,B12
 ,CALL,PERPIR
 RET 2:,XTA,CHR
 ,AAX,C377
 14,UJ,
C
C*****   BЫBOД HA TEPMИHAЛЫ
C
 SDRV*OUT:,ENTRY,
 N,XTA,CIN
 ,APX,M*TYC.ПOCЛEД.KAHAЛ
C ,UZA,SER*OUT.-> ДA
 ,XTA,B10
 ,CALL,PERPIR
 CONO/1:,ITA,S
 ,AOX,B9
 ,EXT,174B
 14,UJ,
 ,END,

  PRINTL:,NAME,
  INITSTR:,ENTRY,
  CHAT:,ENTRY,
  ERROR:,ENTRY,
  WRSUM:,ENTRY,
  :,33,
  ,UJ,*
 ,END,
 TRAM70  :  ,NAME,
C---------
C
C       MAЙ  1983.  H.C.ГOHЧAPOB.
C
C   (И ДO HEГO MHOГИE XOTEЛИ ПEPEПИCATЬ
C    ЗAДAЧY OБMEHA, HO HИKTO HE PEШИЛCЯ).
C
C--------
C
 *ИПД:LC,BLOCK,(16)
  ,CONT,XLTF,LENTDIS
 ,CONT,(1),Ш/ДИCП,(22)
  ,CONT,OUTJOBSC
  ИПЗ/ECTЬ:,EQU,*ИПД+42B
  MTRACNT:,EQU,*ИПД+27B
  LQTRA:,WEQ,MTRACNT.ДЛИHA WL
 LTF:,WEQ,XLTF.ДЛИHA FATA
C---------
  CHEKMDFL:,LC,1.БЛOKИP.OШ.MБ
C---------
 SELECT:,SUBP,.
 COБЫTИE:,SUBP,
 CLANEWTA:,SUBP,.
 TTPRINT :  ,SUBP,
 ERRPRI  :  ,SUBP,
  OШMAШИHЫ:,EQU,ERRPRI
 OUTCW:,LC,1.
 OUTJOB:,SUBP,.
 ABCHT:,SUBP,
C
  TIMERS:LC,BLOCK,TMБ(2),TMД(2),MTTIMCNT(4)
  *TAKCA:LC,BLOCK,(1),DISTIME,TAPTIME
C
 *677:B,BLOCK,BITS,BIT48,BIT47,BIT46,BIT45,BIT44,BIT43,BIT42,BIT41,
       , CONT,     BIT40,BIT39,BIT38,BIT37,BIT36,BIT35,BIT34,BIT33,
       , CONT,     BIT32,BIT31,BIT30,BIT29,BIT28,BIT27,BIT26,BIT25,
       , CONT,     BIT24,BIT23,BIT22,BIT21,BIT20,BIT19,BIT18,BIT17,
       , CONT,     BIT16,BIT15,BIT14,BIT13,BIT12,BIT11,BIT10,BIT9 ,
       , CONT,     BIT8 ,BIT7 ,BIT6 ,BIT5 ,BIT4 ,BIT3 ,BIT2 ,BIT1
 BITTRA:,EQU,BIT27.
 PEBEPC:,EQU,BIT24.
 HAЛOЖ:,EQU,BIT23.
 CЛYЖCЛOB:,EQU,BIT21.
 ЛИCTOБM:,EQU,BIT19.
 ЧTEHИE:,EQU,BIT18.
 ПOДBOД:,EQU,BIT16.
 BITПOДB:,EQU,BIT6.
 BITИMЯ:,EQU,BIT5.
  MAXTIMET:,EQU,BIT8.TAЙMEP MЛ
  CREADIS:,EQU,BIT5.B T1MT
  CONTROL:,EQU,BIT4.B T1MT
 *C :,LC,
 *CB:,LC,
  *TLCONST:,LC,
  MASK12:,LC,3
 FULWOR  :  ,LC,1
 *YCЫ:LC,BLOCK,ГYC,RDSCAL,COMMIR
 ДHЗ:LC,BLOCK,(1),BITJ,ZBITJ
C
  EXCHJOB:,LC,2.KЛИEHTЫ SWING-A
C
 JNREC:,EQU,27B.OПOЗHABAHИE
 SMASAV:LC,BLOCK,
            ,CONT,      SAVAC
            ,CONT,      SAVSR
 ,CONT,(1)
            ,CONT,      SAVI15
C
C<<<<<<<<<<<<<<< И П З >>>>>>>>>>>>>>>>
C
 ST/EXCH:,EQU,-2
 *0:,BLOCK,(3),S33(2),S27(3),I14(16)
    ,CONT ,TMATH(0),TПPИП(8),TЗAЩ(4),(28)
    ,CONT ,(6),COPYS33(2),BISTIME(2),SCEX(3)
    ,CONT ,SYSMD(10),MЛД,PЛД
  REGSCAL:,EQU,100B.ФЛAГИ
  CWPLAC:,EQU,150B.MECTO CЛ.CЛOB
C
C<<<<<<< OБЬEKTЫ ЗAДAЧИ OБMEHA >>>>>>>>
C
 PUT16:,SUBP,
 TYPEMT:,SUBP,
  FIX DENS:,EQU,TYPEMT+5
 BUFTRA:LC,BLOCK,(1),IFTRA(36).
 DRIDENT:LC,BLOCK,(1),KYC
  TRA MASK:LC,BLOCK,(1),FINZON
 TRABUS  :  ,LC,1
 TRACNT  :  ,LC,1
 FULQUESC:  ,LC,1
 TPAKTCT:,EQU,541B
C
  REWINDSC:LC,BLOCK,(1),ЗAKЗAП,CONTRWR
 ,CONT,CHBOSS(4)
C..  CMЫCЛ ЯЧEEK БЛOKA REWINDSC :
C   4-7.   48P.- HYЖEH ПEPEXBAT ПPEPЫBAHИЯ
C              ПO ПPOXOДY ЗOHЫ B 3-6 HAПP.
C      1-15P.- AДPEC ПPOЦEДYPЫ ПEPEXBATA.
C  /ЗAKAЗ ПEPEXBATA БЛOKИPYET PAБOTY ЗAДAЧИ
C  OБMEHA CO BCEM   Y B Y  - HAПPABЛEHИEM/.
C
C   1.  '1' B 48-17 PAЗPЯДAX ECTЬ
C     ПPИЗH. HECMOTKИ HA HAЧAЛO MЛ 30-67.
C
C   2.  '1' B 48-1 P. - ПPИЗH. PAЗPEШEHИЯ
C     ЗAПИCИ HA MЛ/MOДEЛЬ  10-67.
C
C   3.   '1' B 48-1 P. - HYЖEH KOHTPOЛЬ
C     COCTOЯHИЯ ЗAПИCИ HA MЛ/MOДEЛИ 10-67.
C
C-----------------------------------------
 IW70    :  ,LC,1
 CWJFOTRA:  ,LC,1.YC ЗAKAЗЧИKA
 TPHYS   :  ,LC,1
 RETTRA:LC,BLOCK,W04265.ПEPEXBAT "KЧ"
 TASKSTRA:LC,BLOCK,.
  ,CONT,TBUC(6),TBUT(6)
 ,CONT,CSUM(0),CSUMSTAT(6)
 SCALSTRA:LC,BLOCK,.
  ,CONT,SBUC(6),SRJC(6)
  ,CONT,SJMT(32)
  ,CONT,SJMD(16),SCADDS
  ,CONT,БYPЖYЙ
  ,CONT,SCSWING.ШK.OБMEHOB ПOДKAЧKИ
C
C---   OЧEPEДЬ ЗAKAЗOB HA OБMEH:
C
 LISTSTRA:LC,BLOCK,WL1.KYC-Ы
  WL2:,EQU,WL1+LQTRA.KOHTP.CYMMЫ
C
  TABLEMT:LC,BLOCK,T1MOD(16),T1MT(32)
  ,CONT,T2MOD(16),T2MT(32)
  ,CONT,TNMOD(16),TANAMEMT(32)
C
C       TANAMEMT - ИMEHA ЛEHT
C               T1MT :
C    48-43 : ДHЗ BЛAДEЛЬЦA ИЛИ 0
C    42-37 : MATEMATИЧECKИЙ HOMEP
C    36-31 : БИTЫ OШИБOK (CЛEBA HAПPABO:
C            OШM,OШ/ЗП,OШ/CC,KO/ЗП,KC,CИ)
C    30-26 : CЧETЧИK ЧИCЛA OШИБOK TEK.OБMEHA
C    24-13 : CMEЩEHИE ПO ЛEHTE (*TAPES)
C     12   : CБPOC ДBИЖ.ПO TAЙMEPY (KДB)
C    11-10 : ПЛOTHOCTЬ ЗAПИCИ
C      8   : БЫЛ ПPИKAЗ "KO"
C      5   : ИДET KOHTPOЛЬHOE ЧTEHИE
C      4   : ЗAKAЗAH PEЖИM ЗAПИCИ C KOHTPOЛEM
C     3-2  : CTATYC ЛEHTЫ
C      1   : ПPИЗHAK ПEPBOГO OБPAЩEHИЯ
C                T2MT :
C     48   : PAБOTAEM C ДYБЛEM ЗOHЫ
C     44   : OШИБKA B OCHOBHOЙ ЗOHE
C    43-42 : CЧETЧИK HAЛOЖEHИЙ ПPИ ЧTEHИИ
C    41-25 : PEГИCTP COCTOЯHИЯ ДЛЯ MЛ EC
C    24-13 : TPEБYEMAЯ ЗOHA (ЧETHAЯ!)
C    12-1  : TEKYЩAЯ ЗOHA
C
 LTDIS:,WEQ,LENTDIS.ДЛИHA TAБЛ.ЭKCTEHTOB
 LTD:,EQU,LTDIS-1.
 LTD*3:,P*P,(LTD)3
 *TDIS:,LC,LTDIS.TAБЛИЦA
 TDIS:,EQU,*TDIS.ЭKCTEHTOB
  *DISCS:LC,BLOCK,(32),ШMOД,ШФЛД
  FATA:,LC,1.TAБЛИЦA ФAЙЛOB
  TSF:,EQU,425B.TAБЛ.SCR-OБЛ. (8 CЛOB)
  LTS:,EQU,7.EE ДЛИHA-1
C
 *KOHCTPA:LC,BLOCK,KOHTPCTP.KOHTP.ЗAП.HA MБ
 SCDICH:,SUBP,
 DISC:,SUBP,.
  DAFSET:,SUBP,.
 RECMDT:,SUBP,.KAHAЛ OПOЗH.
 CHVOP:,SUBP,
 CHPOP:,SUBP,
 PS*VOP:,SUBP,
 PS*POP:,SUBP,
 DISREG  :  ,LC,1
 *SWCMN:LC,BLOCK,(10),SWINGIF(25)
 EXCHSC:,EQU,*SWCMN+3
 *STATBUF:LC,BLOCK,(14),MДMЛMБ
C
C
C...    BXOДЫ OT MACRO70 И TRAPHY:
C
 ADREQ40:,UJ,MACSTART
 ,Z00,PHYREQ40
C
C
  HA COБ:8,,
  ,Z00,HE COБ
 STD:2,Z01,БYCT.
 ,Z02,.
  ST/T:2,Z02,БYCT.
 ,Z02,AFT ST.
 MOШMБ:,LOG,10 00100 010 00100.
 STATIST:,SUBP,.
C
C...  HOMEPA ДИAГHOCTИK  ERRMACRO :
C
  *12:,BLOCK,AOШM(6),ЧYЖAЯMЛ,ЗAKPAБMД
  ,CONT,ЧYЖTPEK,ЗAПPETЗП,ДHEГOT,MЛKAЮK
 A*TOШMБ:,,
 ,Z00,T OШMБ.
 T ЗПMБ:,ISO,6HЗП MБ:
 TEXER0:,BSS,
 ,ISO,6HMБ KO:
 ,ISO,6HMБOШM:
 ,ISO,6HKЧ KC:
 ,ISO,6HЗПOШM:
 TOШMБ:,BSS,1
 БYCT:,BSS,.
 ЖEAД:,BSS,1
 ШИФP:,BSS,
 ПOAД:,BSS,2
 TOШMЛ:,BSS,.
 ,ISO,12HЧYЖAЯ MЛ - '1'
 ,BSS,1
 THEOПO:,BSS,.
 ,ISO,12HHE OПOЗH.MЛ-
  ,BSS,1
 ,ISO,12HHEГOTOB.MФ-'1'
  HOMФ:,BSS,1
  ,ISO,12HHET ЗAП.MФ-'1'
  ,BSS,1
 TЦKMФ:,BSS,3.
 ,LOG,
 TMБCДOX:,BSS,.
 ,ISO,6HЧП MБ.
 ,BSS,1.
  MTDIAGN:,BSS,
  ,ISO,6HOШ/ЧT
  ,ISO,6HOШ/ЗП
  ,ISO,6HOШ/CC
  ,ISO,6HKO/ЗП
  ,ISO,6HKC MЛ
  ,ISO,6HCБ/CИ
 ,ISO,6HKДB MФ
  HEYГACИM:,ISO,18HHET CБPOCA CДB-6'41'''
  TБЯKA   :,ISO,6HДB-0'41'''
  CTMБ:,LC,2
  PERMIR:,SUBP,
 FWCR:,LOG,.ФЛAГ OП.KAH.
  C200*:,EQU,*TLCONST+11B
  B48/43:,EQU,*TLCONST+22B
 B48/13:,EQU,*CB+4
 B41/1 :,LOG,37 7777 7777 7777
 B39/25:,OCT,0007 7777
 B37/31:,OCT,0001 77
  B36/31:,EQU,*CB+3
 B30/16:,LC ,1
 B30*12*5:,LOG,40 0000 4020
 B29/26:,OCT,0000 0036
 B24/14:,LOG,7776 0000
  B24/13:,EQU,MASK12+2
 B24/1 :,LC ,1
 B22*18:,LOG,1040 0000
 B21/19:,LOG,700 0000
 B21/18:,LOG,740 0000
 B21*18:,LOG,440 0000
 B20/19:,LOG,300 0000
 B19*12:,LOG,100 4000
 B16/1 :,LC ,1
 B11/10:,LOG,3000
 C1    :,EQU,BIT1
 C2    :,EQU,BIT2
 C3    :,LC ,1
 C6    :,EQU,*C
 C7    :,LC ,1
 C13   :,LC ,1
 C17   :,EQU,*CB+1
 C37   :,LC ,1
 C77   :,LC ,1
 C377  :,LC ,1
 C1400 :,LC ,1
 C2003 :,LC ,1
 C3777 :,LC ,1
 C7777 :,LC ,1
 C77774:,LOG,77774
 ZBIT13:,OCT,7777 7777 7776 7777
 ZBIT12:,OCT,7777 7777 7777 3777
  ZBIT1:,EQU,*TLCONST+20B
  ZB7*6:,OCT,7777 7777 7777 7637
 ZB37*23:,OCT,7776 7777 5777 7777
 Z21*18*1:,OCT,7777 7777 7337 7776
 ZC7   :,OCT,7777 7777 7777 7770
 BY1/2 :,GOST,6H770000
 BY1/5 :,GOST,6H777770
 BY5/6 :,GOST,6H000077
 Z12   :,ISO ,6H00''''''''
 Z12Ш  :,ISO ,6H00-Ш00
        ,BSS,.MERDE
C
C<<<< KOHCTAHTЫ БЛOKA OБMEHA C MЛ >>>>>
C
 KPYГ  :,LOG,11
 C/ПEP :,OCT,0000 06
 KYCMAC:,OCT,7777 7777 7477 1600
 MASK5017:,LOG,114
 TKYC17  :,GOST,6H000'20''40''101'
 CW0C  :,OCT,2245 1252 5122 4512
 CW36C :,OCT,0007 0707
 CW4C  :,OCT,7163 4760 3471 6347
 LWC   :,OCT,6666 6666 6666 6666
 MASWL :,OCT,7777 7777 5777 7660
 MAST1MT:,OCT,7777 0000 7777 3037
 MAST2MT:,OCT,1577 7777 7777 7777
 TNZON  :,ISO,6H/0000/
        ,BSS,.MERDE
C
C<<< PAБ. ЯЧEЙKИ БЛOKA OБMEHA C MЛ >>>>
C
 CH     :,BSS,1
 IDMT   :,BSS,1
 KYCДB  :,BSS,1
 NZON   :,BSS,1
 DENS   :,BSS,1
 PEFL   :,BSS,1
 CHSUMR :,BSS,1
 TИПOШ  :,BSS,1
 OFFKYC :,BSS,1
 CЧETП  :,BSS,1
 ПPOBMФ :,EQU,TRAMASK
 EXCHFL :,EQU,KYCДB
C
C<<<<< ЭKB-CTИ БЛOKA OБMEHA C MЛ >>>>>>
C
 *1:,BLOCK,OШ/ЧT,OШ/ЗП,OШ/CC,KO/ЗП
    ,CONT ,KC/MЛ,CБ/CИ,KДB/MФ
 TPDI:,EQU,6
 BITDI:,EQU,BIT8
C
C<<<<< PEГИCTPЫ БЛOKA OБMEHA C MЛ >>>>>
C
 *1 :,BLOCK,(1),NF,MF,ER
 *6 :,BLOCK,(1),RF,( 2 ) ,DC
 *11:,BLOCK,CW ,N ,I
C
C<<<<<<<<<                    >>>>>>>>>
C
 B12/11  :  ,LOG, 00000000 00006000
 BITNAM:,LOG,400 0020
 BITMOV:,LOG,400 0040
 RBITSM:,OCT,1474 0000 004
 MDRADM:,LOG,37 0037
 KDRADM:,LOG,30 0000 0000 1777
 DIDM:,OCT,7700 0000 0077 3740
 SCR*D:,OCT,5750
 SCR*P:,OCT,37 37 77
 KUNITM:,LOG,70 0000 0000 1600
 MS/70:,OCT,4000 0003 0000 03
 MS/KYC:,LOG,100 6003
 MMNTPL:,OCT,0007 7007 7
 KPMASK:,LOG,1 4037 0000
 PПMASK:,LOG,420 0000 0037
 TMPMASK:,LOG, 1 0400 0037
 ,LOG, 2 1000 1740
 ,LOG, 4 2007 6000
 ,LOG,10 4370 0000
 BC1700:,ENTRY,
 BC1700:,BSS,1.CBOБOДHA !
 ,LOG,10.
 ,LOG,20.
 ,LOG,30.
 ,LOG,40.
 ,LOG,50.
 ,LOG,60.
 TSCRFI:,BSS,7.PAБ. ПOЛE ЗAД. OБMEHA
 KYC39:,BSS,1
 KYC40:,BSS,1
  COMPUT16:LC,BLOCK,NH,N8,BN8
  PRTMTCH:,OCT,754
 NOCHECK:,TEXT,8HNO CHECK
 ДHЗПOДK:,LOG,24.
 CON1:,LOG,37 373.
 CON6:,LOG,7777 7776 3740 7777
 CON7:,LOG,7776 0001 4037 1774.
C
 STATUSMT:LC,BLOCK,(1),OLD(4),NEW(4)
 ,CONT,W05741(4)
 ,CONT,SOST,(1),ДBECA(4),SCANER
 ,CONT,ШKEC(4),SCMT57(4)
 ,CONT,ШHГ(4)
  ,CONT,MISTERX
C
 BY4/5:,GOST,6H000770
 KAДMБ:,ISO,6H00000.
 KШИ:,ISO,6H'1''1'Ш00''
 UPM 1:,GOST,6H373730
 B25*16:,LOG,1 001 00000
  C/MГЗ:,EQU,BIT36
  COUNT:,BSS,4
 SAVI11:,BSS,.
 ,BSS,1
 Ш ЧYMЛ:,BSS,1
 R1:,BSS,1
 ЖДYKOHTP:,BSS,1
 ЗAHKOHTP:,BSS,1
 OFFESC:,BSS,1.
 QUEONLY:,BSS,1.ФЛAГ BXOДA OT SWING
C
 DISPBASE:LC,BLOCK,(2),*DR2122(2)
  ,CONT,DISPMD(2)
  ,CONT,DSHIFT
C
 YЧET:,ISO,12HYЧET ЗAKPЫT'0'
 BITWP:,EQU,BIT30
 BITPAG:,EQU,BIT19
 BITRD:,EQU,BIT18
 BDSEC:,EQU,BIT7
 C50:,EQU,BC1700+5
 NRB:,EQU,7
 NRBS:,EQU,48-NRB
 TSCRFI:I,BLOCK,SECT(0),WLSUM.DTASKON
 ,CONT,KSJN(0),FSUM.S48TO12
 ,CONT,DISAD(0),RSUM.S48TO12
 ,CONT,IDENT(0),RRR.PRIPTRA
  ,CONT,DID
 ,CONT,KPAGE(0),MPAGE.OPENPAG
 ,CONT,RBITS(0),WS
C
C
 *1 :,BLOCK,U ,CT,NR,MV,R
 *6 :,BLOCK,PD,TP,JN,E ,F
 *6 :,BLOCK,RO,RG(2),RT
 *11:,BLOCK,P ,W ,J ,M ,S
 *11:,BLOCK,L(2) ,IF,B ,RD
 *14:,BLOCK,         C
C
C
C******   BXOД B ЗAДAЧY OT ЭK70
C
C   ------------------------------
C   PAЗPЯДЫ  I W 7 0   ДЛЯ  MБ :
C   ------------------------------
C     1-5  - HOMEP TPAKTA
C     7-8  - HOMEP CEKTOPA
C     ( ПPИ '1' B 36-M PAЗPЯДE IW70
C      HOMEP CEKTOPA ЗAДAETCЯ B 1-2 P.
C      A HOMEP TPAKTA - B 3-7 P. )
C     13-17  - HOMEP MБ
C     25-26  - HOMEP AБЗAЦA
C     31-35  - HOMEP ЛИCTA
C       39    - ФИЗИЧECKИЙ OБMEH
C       40    - ЧTEHИE
C       48    - CEKTOPHЫЙ OБMEH
C
C
C   ------------------------------
C   PAЗPЯДЫ  I W 7 0   ДЛЯ  MЛ :
C   ------------------------------
C     1-12  - HOMEP ЗOHЫ
C     13-18  - HOMEP MЛ
C     31-35  - HOMEP ЛИCTA
C       39    - ФИЗ. OБMEH
C       40    - ЧTEHИE
C       41    - ПOДBOД
C       42    - ЧTEHИE ИMEHИ
C       43    - PAЗMETKA MЛ EC B PEЖ. EC
C       44    - PAЗMETKA MЛ EC B PEЖ. БЭCM-6
C       46    - ПPИЗHAK MOДEЛИ ФИЗ. MЛ
C              (B ЭTOM CЛYЧAE HOMEP MЛ -
C              - БAPAБAHHЫЙ )
C   ------------------------------
C   ------------------------------
C
C
C   PAЗPЯДЫ  KYC-A  OБMEHA C MБ :
C----------------------------------
C
C   1-2  - HOMEP CEKTOPA
C   3-7  - HOMEP TPAKTA
C   8-10 - HOMEP MБ
C  11-12 - HOMEP AБЗAЦA
C  13-17 - HOMEP ЛИCTA
C    18   - ЧTEHИE
C    19   - ЛИCTOBOЙ OБMEH
C    21   - ЧTEHИE CЛ.CЛOB
C  24-25 - HOMEP CEГMEHTA MOЗY
C    27   - БЫЛA TT-ПEЧATЬ ДИAГHOCTИKИ
C    38   - ЗAKAЗ OT TRAPHY
C    39   - ФИЗ.OБMEH OT Э70
C  40-42 - HOMEP HAПPABЛEHИЯ
C  43-48 - Д H З
C----------------------------------
C
C
C   PAЗPЯДЫ  KYC-A OБMEHA C MЛ :
C----------------------------------
C
C    1   - ПPИЗHAK HAЧAЛЬHOЙ OPИEHTAЦИИ
C    2   - БЫЛO ЧTEHИE CЛ.CЛOB
C          (COCTOЯHИE 1 И 2 PAЗPЯДOB
C           BCEГДA Д.Б. OДИHAKOBЫM )
C    3   - ЛEHTA CTOИT ПЛOXO
C          (B ПPOMEЖYTKE MEЖДY ЗOHAMИ)
C    4   - HET CMOTKИ HA HAЧAЛO
C    5   - ЧTEHИE ИMEHИ
C    6   - ПOДBOД
C    7   - БЫЛA ДИAГHOCTИKA HA TT
C   8-10 - HOMEP MФ
C    11  - ЗAKAЗ ГOTOB K BЫБPOCY
C   12  - PAБOTAEM C ДYБЛEM ЗOHЫ
C          (ДЛЯ PAЗMETKИ MЛ EC)
C  13-17 - HOMEP ЛИCTA
C    18   - ЧTEHИE
C  19-20 - ДЛЯ PAЗMETKИ MЛ EC
C          ('1'-ЗAПИCЬ MГЗ, '2'-ШЗH
C          '3'-ЗAПИCЬ ЗOHЫ, '0'-HOPM.OБMEH)
C   21   - ЧTEHИE CЛ.CЛOB
C   22   - БЛOKИP.ЧTEHИЯ CЛOB C OШИБKAMИ
C   23   - ЧTEHИE C HAЛOЖEHИEM
C  24-25 - HOMEP CEГMEHTA MOЗY
C  26-36 - HOMEP ЗOHЫ
C   37   - ПOBTOPHЫЙ OБMEH
C   38   - ЗAKAЗ OT TRAPHY
C   39   - ФИЗ.OБMEH OT Э70
C  40-42 - HOMEP HAПPABЛEHИЯ
C  43-48 - Д H З
C
C----------------------------------
C
C    BXOД OT 70 ЭKCTPAKOДA
C
C----------------------------------
 MACSTART:,BSS,
 ,XTA,
 ,ATX,KYC
 ,ATX,KPAGE.ДЛЯ ЧT.ИM.
 ,XTA,BIT48
 ,ATX,SECT
 ,XTA,CWJFOTRA
 ,AAX,B48/43
 ,ATX,KSJN. Д H З
 ,ASN,64+42.ЗAKAЗЧИKA
 ,ATI,JN
C..  XAPAKTEPHЫE БИTЫ   I W 7 0
C..  PACTAЩИM B ИHДEKC-PEГИCTPЫ :
C     CT - ФИЗ.ПAKET (CLOSE TASK)
C     NR - ЧTEHИE ИMEHИ
C     MV - ПOДBOД
C     RD - ЧTEHИE
C     PD - ФИЗ.OБMEH
C     TP - MЛ
C=================================
 ,XTA,IW70
 ,ARX,BIT16
 ,APX,RBITSM
 ,ASN,64+NRBS
 ,ATX,RBITS
 M,VTM,1-NRB
 SETREG:,BSS,
 ,XTA,RBITS
 M,AAX,BIT1
 M,ATI,NRB
 M,VLM,SETREG
 U,J+M,TP
 R,MTJ,RD
 ,XTA,IW70
 ,ASN,64+12
 ,AAX,C77
 ,ATI,U.HOMEP YCTP-BA
 ,XTA,IW70
 ,AAX,C7777
 ,ATX,DISAD.HOMEP ЗOHЫ
 R,VTM,PDIMAC
 TP,V1M,TAMAC
 R,VJM,CLOPM70
 ,XTA,IW70
 ,APX,MDRADM. 37 0037
 ,ATX,KYC
 PD,V1M,PDRMAC
C
C...   OБMEH C MAT.MБ
C
 ,ASN,64+38
 ,AEX,KSJN
 ,ATX,DRIDENT
 RG,CALL,GIVTRACK
 ,ATX,KYC.ФИЗ.AДPEC
 M,V1M,MSETSEC
 RD,VZM,MSETSEC
C
C...   ПEPBOE OБPAЩEHИE - ПO ЧTEHИЮ
C      OБHYЛЯEM CEKTOP ИЛИ ЛИCT
C      ЗAMEHИM ЧTEHИE HA ЗAПИCЬ.
C
 RD,VTM,-1777B
 ,XTA,KPAGE
 R,VJM,PRIPTRA
 ,XTA,IW70
 ,ARX,
 ,U1A,SETZ
 RD,VTM,-377B
 ,AEX,BIT48
 ,ATX,IW70
 ,ASN,64+16
 ,AAX,C1400
 ,ATI,M
 SETZ:,BSS,
 :,24,2
 ,XTA,
 M,ATX,70000B
 M,UTM,1
 :,24,3
 RD,VLM,SETZ
 MSETSEC:,BSS,
 ,XTA,IW70
 R,VTM,CHMODRU
 SETSECT:,BSS,
 ,ARX,
 R,U1A,
 ,AEX,BIT48
 ,APX,MS/70
 ,ATX,SECT
 R,UJ,
C
C----------------------
C
  TRASWI:,ENTRY,
C
C***   BXOД OT ПOДKAЧKИ ПO И10.
C      B IW70 - ПPOДYKЦИЯ GIVTRACK-A
C      И.P. 4 - ФИЗ.HOMEP ЛИCTA.
C
 ,XTA,IW70
 ,ATX,KYC
 ,ASN,64+17
 ,AAX,BIT1
 ,ATI,RD
 ,XTA,BIT48
 ,ATX,SECT
 PD,VTM,
 CT,VTM,
  ,ITA,10
  ,ATX,QUEONLY.BOЗBPAT
 ,XTA,CWJFOTRA
 ,AAX,B48/43
 ,ATX,KSJN. Д H З
 ,ITA,4
 ,ASN,64-41
 ,AUX,KPMASK
 ,ATX,KPAGE.ФИЗ.ЛИCT
 4,XTA,TPHYS
 ,APX,MMNTPL
 ,ASN,64+36
 ,ATI,P.MAT.ЛИCT
 R,VJM,CLOPSWI
C
C...   ПPOБA: "TPAKT MБ" BЫДEЛEH
C      B SCRATCH - OБЛACTИ
C
 CHMODRU:,BSS,
 ,XTA,KYC
 ,ARX,
 ,U1A,PHDRUM .-HET
 ,ASN,64+2
 ,ATX,DISAD.HOMEP "TPAKTA"
 ,XTA,KYC
 ,APX,KUNITM
 ,ASN,64+42
 ,ATI,B.HOMEP ЭKCTEHTA
 W,VTM,-LTS
 ,UJ ,SASEAR
C
C...   OБMEH C ФИЗИЧECKИM MБ
C
 PDRMAC:,BSS,
 ,XTA,IW70
 R,VJM,SETSECT
C...   ПPOБA ПEPEAДPECAЦИИ MБ-MД
 R,VJM,CHMODSY
C...   ПPOБA ПOДMEHЫ MБ 21-22 HA MБ
 U,UTM,-21B
 ,ITA,U
 ,ASN,64+1
 ,U1A,NOSUBS
 ,XTA,IW70
 ,AAX,C37
 U,AOX,*DR2122
 ,APX,MDRADM
 ,ATX,KYC
 NOSUBS:,BSS,
 ,XTA,KYC
 ,AUX,KDRADM
 ,ATX,KYC
C
C...   ПOCTAHOBKA MБ-OБMEHA B OЧEPEДЬ
C
 PHDRUM:,BSS,
 ,XTA,SECT
 ,AUX,MS/KYC
 :RD,VZM,*+1
 ,AOX,B22*18
 ,AOX,KYC
 RT,VJM,DTASKON
 ,XTA,BIT1
 ,ARX,MДMЛMБ.CЧETЧИK
 ,ATX,MДMЛMБ.MБ-OБMEHOB
 L,XTA,BIT48
 C,AOX,SRJC-1
 C,ATX,SRJC-1
C
C...   ECЛИ MБ-KAHAЛ CBOБOДEH
C      И ЗAKAЗ HE OT ПOДKAЧKИ
C      TO CPAЗY И BЫПOЛHИM.
C
 ,ACX,
 ,ASN,64+1
 ,AOX,QUEONLY
 RT,VTM,ENDMAC70
 L,UTM,1
 L,MTJ,N
 ,UZA,CONTEXCH
 RT,UJ ,
C
C...   Э070 ЛEHTOЧHOГO TИПA
C
 TAMAC:,BSS,
 CT,V1M,CLOPM70.ФИЗ.ДИCK
 ,XTA,IW70.ПOДГOTOBKA
 ,AAX,B24/1.ИДEHTИФИKATOPOB
 ,AOX,KSJN.ДЛЯ ПOИCKA
 ,APX,DIDM.ПO TAБЛИЦAM
 ,ATX,IDENT.TIMT,FATA
 ,XTA,RBITS
 ,ASN,64+2
 ,AAX,C7
 ,ATI,RD.R.OR.MV.OR.NR
 ,ASN,64+1
 E,VTM,ЧYЖAЯ MЛ
 ,U1A,CHPHYTA.MV.OR.NR
 R,VTM,CHPHYTA
 PD,V1M,CLOPM70
 IF,XTA,PЛД.CTATYC
 U,AAX,BIT48-30B."C ЗAKPЫTИEM
 ,UZA,CLOPM70.ЗAДAЧИ"
 CT,VTM,1. - ДA
C
C...  PAЗБOP: ЗAKPЫTЬ ЗAДAЧY
C     ИЛИ TOЛЬKO EE MAT.ЛИCT
C
 CLOPM70:,BSS,
 ,XTA,IW70
 ,ASN,64+30
 ,AAX,C37
 ,ATI,P.HOMEP ЛИCTA
 IF,UTC,
 P,XTA,TMATH
 ,ASN,64-11
 ,AUX,KPMASK
 ,UZA,BADEXCH
 ,ATX,KPAGE.ФИЗ.ЛИCT
 R,UTC,.ПPOЙДEM
 CT,V1M,.ЧEPEЗ "CLOTA"
C
C...   ПOДПPOГPAMMA ЗAKPЫTИЯ
C      MAT.ЛИCTA ПO ПPИПИCKE
C          И ЗAЩИTE
C
 CLOSPAGE:,ENTRY,
 ,XTA,FULWOR.CHИMAEM
 P,AEX,BIT48.ПPИЗHAK
 IF,AAX,ST/EXCH."ПЛOXOЙ
 IF,ATX,ST/EXCH.OБMEH"
 CLOPSWI:,BSS,
 ,ITA,P
 ,ASN,64+5
 R,U1A,.ФOPM.ЛИCT
 ,ITA,P
 ,ASN,64+3
 ,AEX,C3
 ,ATI,W
 ,ITA,P
 ,AAX,C7
 ,AEX,C7
 IF,J+M,W
 ,ATI,B
 B,XTA,BIT28
 W,AOX,TЗAЩ
 W,ATX,TЗAЩ
 ,ITA,P
 ,ASN,64+2
 ,AEX,C7
 ,ATI,W
 ,ITA,P
 ,AAX,C3
 IF,J+M,W
 ,ATI,B
 B,XTA,TMPMASK
 ,AEX,FULWOR
 W,AAX,TПPИП
 W,ATX,TПPИП
 R,UJ,
C
C...   OПPEДEЛEHИE TИПA YCTPOЙCTBA
C      (MAT., ФИЗ., ЛEHTA, ФAЙЛ)
C
 CHPHYTA:,BSS,
 PD,V1M,PTAMAC.ФИЗ.HOMEP
 F,VTM,LTF+1
 IF,XTA,MЛД
 U,AAX,BIT48-30B
 ,U1A,FIMAC.ФAЙЛ
 W,VTM,-57B
C
C...   ПOИCK ЛOГИЧECKOГO ИДEHTИФИKATOPA
C      ( ДHЗ + MAT.HOMEP )   B T1MT
C
 MTSEAR:,BSS,
 W,XTA,T1MT+37B
 ,AEX,IDENT
  ,AAX,MASK12
 ,UZA,CHWPTA.ECTЬ ЛEHTA
 W,VLM,MTSEAR
 ,UJ ,FIMAC.A BДPYГ ФAЙЛ?
 PTAMAC:,BSS,
 ,XTA,ШФЛД
 U,AAX,BIT48-10B
 ,U1A,MODTAP
 TP,UTM,-100B.OШ.HOMEP
 TP,VZM,ERTREQ.MOДEЛИ
 ,UJ ,PHTAPE
C
C...   ПPOБA: PAЗPEШEHA ЛИ
C      ЗAПИCЬ HA ЛEHTY
C
 CHWPTA:,BSS,
 W,UTM,67B
 E,VTM,ЗAПPET ЗП
 RD,V1M,CHMODTA
 ,XTA,ЗAKЗAП
 W,AAX,BIT48-10B
 ,UZA,ERTREQ
 CHMODTA:,BSS,
 W,MTJ,U
 ,XTA,ШФЛД
 U,AAX,BIT48-10B
 ,U1A,MODTAP.ЛEHTA-MOДEЛЬ
C
C...   ФOPMИPOBAHИE KYC-A ДЛЯ ЛEHTЫ
C
 PHTAPE:,BSS,
 U,XTA,T1MT-30B
  ,ASN,64+12
  ,ARX,DISAD. + ДEЛЬTA
  ,ARX,BIT3. + 4
  ,AAX,C3777.YДBOEHИE
  ,ASN,64-25.HOMEPA ЗOHЫ
 ,ATX,KYC
 ,ITA,U
 ,ASN,64-42
 ,AUX,KUNITM
 ,AOX,KSJN
 ,AOX,KYC
 R,VTM,
 NR,J+M,CT
 :MV,VZM,*+1
 R,VTM,BITMOV
 :NR,VZM,*+1
 R,VTM,BITNAM
 R,AOX,
 R,V1M,TCLOTA
 ,ATX,KYC
 ,XTA,IW70.БИTЫ 44-43
 ,ASN,64-4. - PAЗMETKA
  ,AUX,B19*12.MЛ EC
 :,UZA,*+1
 ,AOX,BIT11
 ,AOX,KPAGE
 ,AOX,KYC
C
C...   ПPOБA: HE ЗAKPЫTЬ ЛИ ЗAДAЧY
C
 TCLOTA:,BSS,
 :CT,VZM,*+1
 CT,VJM,CLOTA.ЗAKPЫTЬ
 :PD,VZM,*+1
 ,AOX,BIT39
 CHRDTA:,BSS,
 :RD,VZM,*+1
 ,AOX,B22*18
 ,ATX,KYC
 ,XTA,
 RD,V1M,TTASKON
C
C...   ПOЛYЧEHИE KOHTPOЛЬHOЙ CYMMЫ
C      ПPИ ЗAПИCИ ЗOHЫ HA ЛEHTY
C
 J,VTM,-1777B
 ,XTA,KYC
 R,VJM,PRIPTRA
 :,24,2
 ,XTA,
 LSTAPE:,BSS,
 J,ARX,71777B
 J,VLM,LSTAPE
 TTASKON:,BSS,
 :,24,3
 RT,VJM,TASKON.B OЧEPEДЬ
 EPHTAP:,BSS,
 ,XTA,BIT25
 ,ARX,MДMЛMБ.CЧETЧИK
 ,ATX,MДMЛMБ.MЛ-OБMEHOB
 L,XTA,BIT48
 U,AOX,SJMT-30B
 U,ATX,SJMT-30B
C
C...  ПOCTAHOBKA PAБOTЫ ПO ЛEHTE
C     B ШKAЛY PAБOT ЗAДAЧИ OБMEHA
C
 :,24,2003B
 ,XTA,TRABUS
 C,AAX,BIT44-3
 ,U1A,ENDMAC70.YЖE ECTЬ
 ,XTA,BIT40
 C,AOX,BIT44-3
 ,AEX,TRABUS
 ,ATX,TRABUS
 C,XTA,BIT36-3
 ,CALL,CLOMIR
 :,24,3
 ,UJ ,ПPOДPAБ
C
C...   OБMEH C ФИЗИЧECKИM ДИCKOM
C
 PDIMAC:,BSS,
 ,XTA,IW70
 ,ASN,64-30
 ,AUX,KUNITM
 ,ATX,DID
 ,XTA,IW70
 ,ASN,64+24.K O П
 ,AOX,BIT5
 ,AAX,C77
 PHDISC:,BSS,
 ,AOX,DID
 ,ATX,KYC
 ,XTA,DISAD
 ,ARX,BIT4
 ,AAX,C7777
 ,ASN,64-25."ЗOHA"
 ,AOX,KYC
 ,AOX,BITPAG
 :CT,VZM,*+1
 CT,VJM,CLOTA
 :RD,VZM,*+1
 ,AOX,BITRD
 ,ATX,KYC
 ,XTA,SECT
 ,AEX,BIT48
 ,UZA,DPAGEX
 ,AEX,BIT48
 ,ASN,64+24
 ,AOX,BDSEC
 DPAGEX:,BSS,
 ,AOX,KYC
 RT,VTM,DAFSET
 DTASKON:,BSS,
 ,AOX,KSJN
 ,AOX,KPAGE
 :PD,VZM,*+1
 ,AOX,BIT39
  :,ATX,KYC
  ,APX,KPMASK
  ,ASN,64+41.БИT 6 B TPHYS -
  ,ATI,L. - ПPИЗHAK, ЧTO
  ,XTA,BIT6.ЛИCT ЗAHЯT B OБMEHE
  L,AOX,TPHYS.C MБ ИЛИ MД.
  L,ATX,TPHYS
 ,XTA,
 RD,V1M,TASKON
C
C...   KOHTPOЛЬHOE CYMMИPOBAHИE ДЛЯ MД/MБ
C
 L,VTM,1-4
 ,XTA,KYC
 R,VJM,PRIPTRA
  M,VTM,
 ,XTA,SECT
 ,ARX,
 ,UZA,LSPAG
 ,ASN,64+37
 ,AAX,C1400
   ,ATI,M
 L,VTM,
 ,XTA,
 LSPAG:,BSS,
 ,ASN,64-12
 ,ATX,WLSUM
 ,XTA,
 :,24,2
 J,VTM,-377B
 LSSEC:,BSS,
  M,ARX,70000B
 M,UTM,1
 J,VLM,LSSEC
 :,24,3
 J,VJM,S48TO12
 ,AOX,WLSUM
 L,VLM,LSPAG
 TASKON:,ENTRY,
 TASKON:,BSS,
 ,WTC,TRACNT
 L,VTM,
 L,ATX,WL2
 ,ITA,L
 ,ARX,C1
 ,ATX,TRACNT
 ,XTA,KYC
 L,ATX,WL1
 ,ASN,64+39
 ,AAX,C7
 ,ATI,C
 ,XTA,
 ,ATX,KYC
  ,XTA,QUEONLY
  RT,UZA,
  L,XTA,BIT48
  ,AOX,SCSWING
  ,ATX,SCSWING
  ,XTA,
  RT,UJ,
C
C...   ПOИCK ЛOГИЧECKOГO ИДEHTИФИKATOPA
C          B TAБЛИЦE ФAЙЛOB
C
 FIMAC:,BSS,
 F,UTM,-1
 F,VZM,ERTREQ.HE HAШЛИ
 F,XTA,FATA-1
 ,AEX,IDENT
  ,AAX,MASK12
 ,U1A,FIMAC
C
C...         ФAЙЛ HAЙДEH
C      ЧTEHИE ИMEHИ - BЫДAДИM 0
C      ПOДBOД - ПPOИГHOPИPYEM
C
 NR,V1M,SETACC
 MV,V1M,ENDMAC70
 U,XTA,BIT48-30B.ЧTOБЫ HE
 IF,AOX,MЛД.ИCKATЬ B T1MT
 IF,ATX,MЛД.B ДPYГOЙ PAЗ
 ,XTA,DISTIME
 IF,A+X,BISTIME
 IF,ATX,BISTIME
 F,XTA,FATA-1
 ,AAX,B36/31
 E,VTM,ЗAKPAБMД
 ,UZA,TDISEAR.SCRATCH-ФAЙЛ
C
C...   ИЗBЛEЧEHИE HOMEPA MOДYЛЯ
C      ИЗ TAБЛИЦЫ ФAЙЛOB
C
 CHESCR:,BSS,
 F,XTA,FATA-1
 ,ASN,64-12
 ,AUX,KUNITM
 ,UZA,BADEXCH
 ,ATX,DID
 RD,V1M,CHDISAD
 E,VTM,ЗAПPET ЗП
 F,XTA,FATA-1
 ,AAX,BITWP
 ,UZA,ERTREQ
C
C...   KOHTPOЛЬ BЫXOДA ЗA ГPAHИЦY ФAЙЛA
C
 CHDISAD:,BSS,
 F,XTA,FATA-1
 ,ASN,64+12
 ,AAX,C7777
 ,ARX,DISAD
 ,ATX,DISAD
 F,XTA,FATA-1
 ,AAX,C7777
 ,NTR,3
 ,A-X,DISAD
 E,VTM,ЧYЖ TPEK
 ,U1A,ERTREQ
 F,XTA,FATA-1
 ,ASN,64-20
 ,AUX,C50
 ,UJ ,PHDISC
C
C...   OБMEH CO SCRATCH-ФAЙЛOM
C
C      ПOИCK B TAБЛИЦE ЭKCTEHTOB
C
 TDISEAR:,BSS,
  ,XTA,IDENT.YЖИMAHИE
  ,ARX,SCR*D.ДHЗ И MN
  ,APX,SCR*P.ДO 5 БИT
  ,ATX,IDENT
  ,ASN,64+16
  ,AOX,IDENT
  ,ASN,64+16.YTPOEHИE
  ,AOX,IDENT.ИДEHTИФИKATOPA
  TDS1:,BSS,
 W,VTM,-LTD
  ,ATX,DID
 SEW:,BSS,.ПO CЛOBAM
 W,AEX,TDIS+LTD
 B,VTM,1-3
 SEB:,BSS,.ПO ЭKCTEHTAM
 ,ASN,64-16
 ,ATX,WS
 ,YTA,
 ,UZA,GLOBYTE.HAШЛИ
 ,XTA,WS
 B,VLM,SEB
 ,XTA,DID
 W,VLM,SEW
C...   ECЛИ HET CBOБOДHOГO MECTA
C      B TAБЛ.ЭKCTEHTOB - БYДEM
C      BЫШИБATЬ ЮЗEPA !!!
 F,XTA,FATA-1.SCRATCH
 ,AAX,C3777.ЮЗEPA
 ,UZA,ERTREQ.ИCЧEPПAH
 F,XTA,FATA-1
 ,ARX,C3777.YMEHЬШAEM
 ,AEX,BIT12.ЛИMИT
 F,ATX,FATA-1
 ,XTA,.И BOЗЬMEM
  ,UJ,TDS1.CBOБ.ЭKCTEHT
C
C...   ЗAXBAT ЭЛEMEHTA
C     B TAБЛИЦE ЭKCTEHTOB
C
 GLOBYTE:,BSS,
 ,ITA,B
 ,ASN,64-4
 ,ATI,R
 ,XTA,IDENT
 R,ASN,64+32
 W,AOX,TDIS+LTD
 W,ATX,TDIS+LTD
 B,UTM,LTD*3+2
 W,J+M,B
 W,J+M,B
 W,J+M,B
 W,VTM,-LTS
C
C...   ПOИCK ЗAXBAЧEHHOГO ЭKCTEHTA
C      B TAБЛИЦE SCRATCH-OБЛACTEЙ
C
 SASEAR:,BSS,
 B,MTJ,R
 W,WTC,TSF+LTS
 B,UTM,1
 ,ITA,B
 ,AAX,BIT15
 ,U1A,SETFAD
 W,VLM,SASEAR
 ,UJ ,BADEXCH
 SETFAD:,BSS,
 ,ITA,R
 ,ASN,64-5
 ,ATX,DID
 W,XTA,TSF+LTS
 ,ASN,64+15
 ,UJ ,MODSYS
C
C...   ПEPEAДPECAЦИЯ ФИЗ.БAPAБAHOB
C      16,17,21,22  HA ДИCK
C
 CHMODSY:,BSS,
  U,XTA,BIT48-10B
  ,APX,DISPMD
  ,UZA,CHMODSY1
  ,AAX,BIT48
  :,U1A,*+1.1-Й MБ (16)
  ,XTA,DSHIFT.2-Й MБ (17)
 :,AAX,C7777
  ,ARX,DISPMD
  E,VTM,DISPMD - SYSMD
  ,UJ,MAKDID
  CHMODSY1:,XTA,Ш/ДИCП
 JN,AAX,BITS
  R,U1A,
 U,XTA,BIT48-10B
 IF,APX,SYSMD
 R,UZA,
 ,ANX,B24/1
 ,ASN,64-5
 IF,ARX,SYSMD
  IF,MTJ,E
  MAKDID:,BSS,
 ,AAX,C7777
 ,ATX,DID
  E,XTA,SYSMD
 ,ASN,64+12
 MODSYS:,BSS,
 ,ATI,F
 ,XTA,DISAD
 ,AAX,C37
 ,ARX,DID
 ,ATX,DISAD
 ,UJ ,CHESCR
C
C...   ЗAKAЗ AДPECOBAH K MOДEЛИ MЛ
C
 MODTAP:,BSS,
 NR,V1M,MODNAM
 MV,V1M,ENDMAC70
 MODTEX:,BSS,
 U,XTA,T1MT-30B
 ,ASN,64+12. + ДEЛЬTA
 ,ARX,DISAD
 ,AAX,C7777
 ,ATX,DISAD
 U,XTA,T2MT-30B.ИHДEKC ПO
 ,ATI,F.TAБЛИЦE ФAЙЛOB
 ,UJ ,CHESCR
 MODNAM:,BSS,
 U,XTA,TANAMEMT-30B
 SETACC:,BSS,
 IF,ATX,
 ,UJ ,ENDMAC70
C
C
C...   ДИCПETЧEPCKИЙ BXOД
C      B ЗAДAЧY OБMEHA
C       И14 - BOЗBPAT
C       И13 - AДPEC KYC-A
C      PEЗYЛЬTAT - B SAVAC
C
 TRAPHY:,ENTRY,
 TRAPHY:,BSS,
 :,24,2003B
 ,RTE,177B
 ,ATX,SAVSR
 ,ITA,S
 S,VTM,SAVI15
 ,ITS,M
 ,ITS,J
 ,XTS,DISREG
 ,ATI,27B
 ,XTA,TRACNT
 ,ARX,MTRACNT
 ,UZA,FULQUE
 ,XTA,ГYC
 ,AAX,B48/43
 J,AOX,
 ,ATX,KYC39.ДOБABKA ДHЗ
 ,XTA,TRABUS
 ,UZA,FRETRA
 ,AOX,BIT39
 ,UJ ,SETBUS
 FRETRA:,BSS,
 ,XTA,KYC39
 ,ATX,KYC40
 ,XTA,BITTRA
 ,AOX,RDSCAL
 ,ATX,RDSCAL
 ,XTA,DISREG
 ,ATX,IFTRA+S27
 ,XTA,ADREQ40
 ,ATX,IFTRA+S33."ФИЗOБM1"
 ,XTA,BIT40
 SETBUS:,BSS,
 ,ATX,TRABUS
 ,UJ ,ETRAPH
C
C...ЗAKPЫTИE KЛИEHTA, ECЛИ OЧEPEДЬ ПOЛHA
C
 FULQUE:,BSS,
 M,VTM,TRAPHY
 ,XTA,BITJ
 ,AOX,FULQUESC
 ,ATX,FULQUESC
 ETRAPH:,BSS,
 M,MTJ,33B
 ,UJ ,CLANEWTA
C
C...   PAБOTA TRAM70 OT 39 P. TRABUS
C      (ИЛИ OT 40, ECЛИ БЫЛA CBOБOДHA)
C
 ФИЗOБM1:,BSS,. OT 39
 ,XTA,KYC39
 ,ATX,KYC40
 ,XTA,TRABUS
C   ПPИ ДOБABЛEHИИ HOBЫX PAБOT
C   CMEHИTЬ AДPEC B CЛEД.KOMAHДE!
 ,AAX,BIT38.!!!
 ,AOX,BIT40.ДЛЯ ПPABИЛЬHOЙ
 ,ATX,TRABUS.PAБOTЫ ENDMACRO
C
 PHYREQ40:,BSS,. OT 40
 ,XTA,KYC40
 ,AAX,KPMASK
 ,ATX,KPAGE
 ,XTA,KYC40
 ,AAX,B48/43
 ,ATX,KSJN
 ,ASN,64+42
 ,ATI,JN
 JN,WTC,*YCЫ
 IF,VTM,
 PD,VTM,
 ,XTA,BIT48
 ,ATX,SECT
 ,XTA,KYC40
  CT,VJM,CLOTA. + BIT38 = KYC
 ,ASN,64+17
 ,AAX,BIT1
 ,ATI,RD
 ,XTA,KYC
 ,APX,KUNITM
 ,ASN,64+42
 ,ATI,U
 ,ASN,64+3
 ,ATI,C
 ,XTA,KYC
 C,VZM,DIPREQ
 C,UTM,-7
 C,VZM,NSPREQ
 ,AAX,BIT39
 ,U1A,MODPREQ
 ,ITA,C
 ,AAX,BIT3
 ,U1A,TAPREQ
 R,VTM,PHDRUM
 ,XTA,KYC
 ,ASN,64+2
 ,ATX,DISAD
 ,AAX,BITPAG+2
 ,U1A,CHMODSY
 ,XTA,KYC
 ,APX,MS/KYC
 ,ATX,SECT
 ,UJ ,CHMODSY
 TAPREQ:,BSS,
 ,XTA,KYC
 ,UJ ,CHRDTA
C
C...  "7" B 40-42 P.  KYC-A  TRAPHY
C   - ЗAKAЗ OT HECTAHДAPTHOГO ДPAЙBEPA.
C   ПPИ ЭTOM B 1-15 P. - AДPEC MACCИBA
C   ИЗ 2 CЛOB :
C  1 : HACTOЯЩИЙ KYC (ПPИ OБMEHE ЛИCT HE 0)
C  2 : AДPECA ПPOЦEДYP ПYCKA И KOHЦA
C   AДPECA ПOMEЩAЮTCЯ B WL2.
C   KYC CHAБЖAETCЯ ДHЗ B 48:43 PAЗP.,
C   "1" 38 P. И ПOMEЩAETCЯ B  WL1.
C
 NSPREQ:,BSS,
 ,ATI,M
 M,XTA,
  ,AOX,KSJN
  ,AOX,BIT38
 ,ATX,KYC
 ,APX,KUNITM
 ,ASN,64+42
 ,ATI,U
 M,XTA,1
  ,AOX,KSJN
 RT,VJM,TASKON
 L,XTA,BIT48
 ,AOX,БYPЖYЙ
 ,ATX,БYPЖYЙ
 L,XTA,WL1
 ,AAX,KPMASK
 ,UZA,EPHTAP
 L,XTA,BIT48
 C,AOX,SRJC-1
 C,ATX,SRJC-1
 ,UJ ,EPHTAP
 DIPREQ:,BSS,
 ,ASN,64+11
 ,AAX,BIT1
 ,ARX,C3
 ,ASN,64-39
 ,UJ ,DPAGEX
 MODPREQ:,BSS,
 ,XTA,KYC
 ,ASN,64+25
 ,ARX,C77774
 ,ATX,DISAD
 ,UJ ,MODTEX
C
C...  ЗAKPЫTИE ЗAДAЧИ, ПOДABШEЙ OБMEH.
C
 CLOTA:,BSS,
 ,AOX,BIT38
 ,ATX,KYC
 :,24,2003B
 JN,XTA,BITS
 ,AEX,FULWOR
 ,AAX,RDSCAL
 ,ATX,RDSCAL
 ,XTA,KYC
 :,24,3
 CT,UJ ,
C
C...  ПPИПИCKA ЛИCTA ИЗ KYC-A OБMEHA
C     K 34 MAT.ЛИCTY ЗAДAЧИ OБMEHA
C
 PRIPTRA:,ENTRY,
 PRIPTRA:,BSS,
 ,APX,KPMASK
 ,ATX,RRR
 ,UZA,BADEXCH
 ,AUX,TMPMASK
 ,ATX,IFTRA+TПPИП
 ,ATX,SWINGIF+TПPИП
 ,ITA,R
 R,VTM,-8
 :,ATX,1
 R,VLM,*
 ,ATI,R
 ,XTA,RRR
 ,AUX,PПMASK
 ,MOD,27B
 ,ATI,
 R,UJ,
C
C...  CBEPTKA 48-PAЗPЯДHOЙ KOHTPOЛЬHOЙ
C     CYMMЫ B 12-PAЗPЯДHYЮ
C
 S48TO12:,ENTRY,
 S48TO12:,BSS,
  ,ATX,FSUM
  ,XTA,
  ,ATX,RSUM
  ,XTA,FSUM
  COMPR:,ASN,64+12
  ,ATX,FSUM
  ,YTA,
  ,ARX,RSUM
  ,ATX,RSUM
  ,XTA,FSUM
  ,U1A,COMPR
  ,XTA,RSUM
  ,ASN,64+36
  ,ARX,RSUM
  ,AAX,C7777
 J,UJ ,
C
C...   БЫЛИ OШИБKИ B ЗAKAЗE HA OБMEH
C
 ERTREQ:,BSS,
 ,ITA,E
 IF,ATX,I14
 IF,XTA,S33
 IF,ATX,COPYS33
 M,VTM,ERRPRI
 ,ITA,M
 IF,ATX,S33
 ,XTA,DISREG
 IF,ATX,S27
 MV,J+M,NR
  N,VTM,.БEЗ TASKOFF
 ,XTA,KYC
 ,AOX,KPAGE
  IF,ATX,.CYMM.=KYC
  ,ATX,OFFKYC
 ,AAX,BIT38
 ,U1A,OPEN
  ,XTA,IW70.CYMM.=IW70
  IF,ATX,
 NR,V1M,ENDMAC70
  ,UJ ,OPEN
C
C
 MSKADRMD:,EQU,KDRADM
 CON14:,EQU,KUNITM
 CBEPKCYM:,EQU,S48TO12
 ПPИПOБME:,EQU,PRIPTRA
 CONTEXCH: ,ENTRY,
C
C*********          BXOД: BЫПOЛHEHИE ЗAKAЗA, YЖE ПOCTABЛEHHOГO B OЧEPEДЬ
C
  CONTEXCH:,24,2003B
  12,XTA,WL2-1
  14,ATX,CSUM-1
  12,XTA,WL1-1
  14,ATX,TBUC-1
  14,ATX,TBUT-1
  9,UZA,
 13,VJM,CHPOP.P-OПEPAЦИЯ
 9,UTC,
 15,V1M,.CEMAФOP ЗAKPЫT
  12,XTA,BITS
  14,ATX,SBUC-1
 EXCHMБ:,BSS,
 14,XTA,TBUC-1
 ,AAX,ЧTEHИE
  ,UIA,BЫT
C.........ФOPMИPOBAHИE CЛYЖEБHЫX CЛOB ДЛЯ ЗAПИCИ HA MБ (TPAKTA)
C         (ЧETHЫE CЛYЖ. CЛOBA - AДPECA CEKTOPOB, HEЧETHЫE - KC)
 14,XTA,TBUC-1
 ,AAX,C3
 ,ASN,64-1
 14,ARX,BC1700
  ,ATI,13
 14,XTA,TBUC-1
  13,ATX,
 ,AAX,ЛИCTOБM.
 ,UZA,MБЗAП1C.
 14,XTA,TBUC-1
 ,ARX,BIT1
  13,ATX,2
 ,ARX,BIT1
  13,ATX,4
 ,ARX,BIT1
  13,ATX,6
 14,XTA,C SUM-1
 WR KS:,ASN,64-12.
 ,ATX,WS
 ,YTA,
   13,ATX,1
  13,UTM,2
  ,ITA,13
 ,AAX,C7
 ,UZA,BЫT
 ,XTA,WS
 ,UJ,WR KS
  BЫT:13,VTM,-7
 :,ATX,1.
  13,VLM,*
C.........BЫДAЧA KOMAHДЫ OБMEHA C MБ
 ,XTA,BIT3.
 14,ATX,TMБ-1.
 14,XTA,TBUC-1
          14,  EXT  ,               .
            ,  ATI  ,               .
  14,XTA,BIT47
  9,MTJ,13
  ,UJ,PERMIR
 MБЗAП1C:,BSS,.
C.........ФOPMИPOBAHИE CЛYЖEБHЫX CЛOB ДЛЯ ЗAПИCИ HA MБ (CEKTOPA)
 14,XTA,C SUM-1.
 ,AAX,C7777.
  13,ATX,1
 ,UJ,BЫT.
C
C
C...   KOHEЦ PAБOTЫ ЗAДAЧИ OБMEHA
C      ПO ПPИEMY ЗAKAЗA OT  Э070
C
 ENDMAC70:,ENTRY,.
 ENDMAC70:,BSS,
  :,24,3
  ,XTA,QUEONLY
  ,UZA,NOSWING
  ,ATI,10
  ,XTA,
  ,ATX,QUEONLY
  10,UJ,.BOЗBPAT B ПOДKAЧKY
 NOSWING:,BSS,
C         KOHEЦ PAБOT ПO ЭK70 ИЛИ KAH.OПOЗ.
 ,XTA,FWCR.БЫЛ OП.KAH.:
 ,U1A,TOTRAINT.ДA
  ,XTA,BIT40
  ,UJ,TO TRAINT
C
C...   KOHEЦ PAБOTЫ ПO ЛEHTOЧHOMY KAHAЛY
C
  ALLEOM:14,XTA,CSUMSTAT-1
  ,AAX,C377
  ,UZA,PUTMASK
  14,XTA,TYPEMT-3
  ,AAX,BIT35.B ДYБHE
  ,ASN,64+2."MФ 40" = CDC608
  ,U1A,PUTMASK.( B 6 HAПP-И )
  14,XTA,BIT39
  PUTMASK:,AOX,TRAMASK
  ,CALL,PERMIR
  14,XTA,BIT47
  TO TRAINT:,24,2003B
  ,AEX,FULWOR
  ,AAX,TRABUS
  ,ATX,TRABUS
 ,XTA,.ЧИCTKA ПPИЗHAKA
 ,ATX,FWCR.KAHAЛA OПOЗH.
C
C
C...   OCHOBHOЙ BXOД B ЗAДAЧY OБMEHA:
C      BЫБOP OЧEPEДHOЙ PAБOTЫ
C
 TRAINT:,ENTRY,.
 TRAINT:,BSS,.
   :   ,24,2003B
 ,XTA,TRABUS
 ,UZA,ENDTRA. HA BЫXOД ИЗ ЗAДAЧИ OБMEHA
  ,AAX,PRTMTCH
  ,U1A,PRT/IS
  ,XTA,TRABUS
  PRT/IS:,BSS,
  ,ANX,
  ,ATI,14
  14,UJ,SW/TRA -1
C
C********
  SW/TRA:,BSS,
  :,UJ,END DRUM
  :,UJ,END DRUM
  :,UJ,DISC
  :,UJ,DISC
  :14,UTM,-2
  ,UJ,END TAPE
  :14,UTM,-2
  ,UJ,END TAPE
  :14,UTM,-2
  ,UJ,END TAPE
  :14,UTM,-2
  ,UJ,END TAPE
  :,UJ,ФИЗOБM1.ПO P.40
 :,UJ,ФИЗOБM1.ПO P.39
 :,XTA,BIT38.KAHAЛ
 ,ATX,FWCR.OПOЗHABAHИЯ
   :   ,24,3
 ,UJ,RECMDT
C
C...   CAMOЗAKPЫTИE TRAM70
C      ПPИ OTCYTCTBИИ PAБOT
C
 ENDTRA  :  ,  BSS  ,               .
            ,  XTA  , RDSCAL        .
 ,AOX,OFFESC.
 ,AAX,ZBITJ
            ,  ATX  , RDSCAL        .
            ,  XTA  ,               .
 ,ATX,OFFESC.
            ,  XTA  , C7            .
   ,ATX,IFTRA+S27
            ,   UJ  , SELECT        .
C
C*****   OБPAБOTKA KOHЦA OБMEHA C MБ
C
  END DRUM:,24,3
C         HE ПPOПYCK ЛИ ЭTO B KPИT.ИHT.
 14,XTA,SBUC-1
 ,UZA,NEWDRTA.ДA
C.....    ЭTO OKOHЧAHИE OБMEHA
C.....   БЫЛИ ЛИ AППAPATHЫE OШИБKИ
 14,XTA,TBUC-1
 ,ASN,64+11. O Ш M  ИЛИ
 ,AAX,B25*16.KOH.OБM.
 ,U1A,OШM/KO. - ДA
 14,XTA,TBUC-1
            ,  AAX  , ЧTEHИE        .
 ,UZA,XOБMБ.
 14,XTA,TBUC-1
 5,VJM,ПPИПOБME.
C.........YCTAHOBKA PEAKЦИИ HA ПPEPЫBAHИE: KOHTPOЛЬ ЧИCЛA
 10,VTM,ПOBTOБM1.C YЧETOM OШM
 ,ITA,10.
            ,  ATX  ,W04265         .
C.........
          14,  XTA  , T B U T -1    .
            ,  AAX  , ЛИCTOБM       .
 ,U1A,БЫЛИЧT1.
          14,  XTA  , T B U T -1    .
            ,  AAX  , B12/11        .
            ,  ASN  ,64+2           .
 ,ATI,11.OTH.AДREC HAЧAЛA CEKTOPA
          14,  XTA  , T B U T -1    .
            ,  AAX  , C3            .
            ,  ASN  ,64-1           .
 14,ARX,BC1700.
            ,  ATI  , 10            .
C---------          KOHTPOЛЬ - 'TO ЛИ MЫ ПPOЧИTAЛИ'
 12,VTM,0
 14,XTA,CSUM-1
 ,ASN,64-36
 ,ATX,R1
 ,UJ,L04361
 ПPOBAДP:,BSS,.
C......   KOHTPOЛЬ ПYTAHИЦЫ CEKTOPOB
 ,ATX,ЖEAД.
 ,XTA,C2003.БЛOKИP.
 ,ATI,21B.OCTAH. ПO KЧ
 ,XTA,ЖEAД.
 10,AEX,.
 ,AAX,MSKADRMD.
 5,UZA,.AДPECA-O'KEY
 ,XTA,CHEKMDFL.AДPEC.KOHTPOЛЬ
 5,UIA,.- БЛOKИPOBAH
 ,XTA,BIT40.
 14,ARX,CTMБ-1.ПYTAHИЦY CEKT.
 14,ATX,CTMБ-1.B CTATИCTИKY
 ,XTA,C13.
 ,ATI,21B.OCTAH. ПO KЧ
 14,XTA,TBUT-1.
 ,AAX,BIT27. OШ.MБ.
 ,UIA,KPAHTЫ. - ПOBTOPHO
 ,XTA,ЖEAД.
C.........ПOДГOTOBKA TT-TEKCTA И BЫДAЧA HA TT 'OШ. MБ...'
 11,VJM ,KOД MБ.
 ,ISO,6HOШ.MБ:
 ,XTA,ШИФP.
 ,ATX,ПOAД+1.
 10,XTA,
 11,VJM,AДP MБ.
 ,ATX,ПOAД
 14,MTJ,11
  :,24,2003B
 14,VTM,TOШMБ
 ,ITA,14.
 14,VJM,TTPRINT.
 11,MTJ,14.
 ,UJ,ПOБ2.
 KPAHTЫ:,BSS,.
C
C.....   ДBYKPATHAЯ ПYTAHИЦA CEKTOPOB
C        (BЫБPOC ЗAДAЧИ)
C
 11,VJM,ДAЙДHЗ
 ,ATI,11.
 11,V1M,HEПOДK1.
C.........COBCEM ПЛOXAЯ CИTYAЦИЯ C MБ
   :   ,24,2003B.
 14,VTM,TMБCДOX.
 ,ITA,14.
 14,VJM,TTPRINT.TT-ПEЧATЬ- 'MБ CДOX'
   :   ,24,3.
 ,UJ,*. ЦИKЛ C OTKP. ПPEP.
C
C.....   ПOДПPOГPAMMЫ ПOДГOTOBKИ COOБЩEHИЙ
C
 KOД MБ:,BSS,
 ,ATX,ЖEAД
 11,XTA,
 ,ATX,TOШMБ
 ,ITA,11
 ,ATX,SAVI11
 11,VJM,ДAЙ ДHЗ.
 ,UZA,ПOДKAЧ
  13,XTA,*YCЫ
  ,ASN,64-27
 ,AUX,BY4/5
 ПOДKAЧ:,AOX,KШИ
 ,ATX,ШИФP
 ,XTA,ЖEAД
 11,VJM,AДP MБ
 ,ATX,ЖEAД
 14,XTA,TBUT-1
 ,AOX,BIT27
 14,ATX,TBUT-1
 ,WTC,SAVI11
 ,UJ,1
 AДP MБ:,BSS,
 ,APX,MSKADRMD
 ,AUX,UPM1
 ,AOX,K AДMБ.
 11,UJ,
 ДAЙДHЗ:,BSS,.
 14,XTA,TBUT-1.
 ,AAX,BIT38.
 ,UZA,NONPHY.
 14,XTA,TBUT-1.
 ,ASN,64+42.
 ,ATI,13.
 ,AEX,ДHЗПOДK.
 11,UZA,.BOЗBPAT(ЗAKAЗ OT ПOДKAЧKИ)
 BДД:,ITA,13.
  11,U1A,
  ,XTA,EXCHJOB+1
  ,ATI,13
 11,UJ,.
 NONPHY:14,XTA,TBUT-1.
 ,APX,KPMASK.
 ,ASN,64+41.
 ,ATI,13.
 13,XTA,TPHYS.
 NPH1:,ASN,64+27.
 ,AAX,C77.
 ,ATI,13.
 ,AEX,ДHЗПOДK.
 ,U1A,BДД.
 ,XTA,EXCHJOB.
 ,UJ,NPH1.
C.....
 OБPKOШMA:,BSS,.
C---------         БЛOK, PAБOTAЮЩИЙ OT ИMEHИ BЫБPACЫBAEMOЙ ЗAДAЧИ
  14,VTM,AOШM
 ,UJ,OШMAШИHЫ
C---------
 L04357:,XTA,DISREG
            ,  ATI  , 21B           .
          14,  XTA  , T B U C -1    .
            ,  AAX  ,BIT39          .
 ,AAX,CHEKMDFL.
            ,  U1A  , XOЧTMБ1       .
 ,XTA,BIT1.
 14,ARX,CTMБ-1. KC MБ
 14,ATX,CTMБ-1.B CTATИCTИKY
 14,XTA,TBUT-1
 ,AAX,BIT27.
 ,U1A,*8ПOBT.
 14,XTA,TBUT-1
 11,VJM,KOД MБ
 ,ISO,6HKC MБ:
 14,MTJ,11
 14,XTA,TBUT-1
 ,AAX,ЧTEHИE
 ,U1A,HE ЗПMБ
 ,XTA,T ЗПMБ.
 ,ATX,TOШMБ
 HE ЗПMБ:,XTA,A*TOШMБ
 14,VJM,TTPRINT.
 11,MTJ,14.
 ,XTA,CHEKMDFL+1
 ,UZA,ПOBTOБMБ
 ,UJ,XOЧTMБ1.
 БЫЛИЧT1:,BSS,.
C---------         ПPOBEPKA ПPOЧTEHHOГO TPAKTA
 12,VTM,-3.
 14,WTC,BC1700.
 10,VTM,.
 11,VTM,0.C HUЛEBOГO CEKTOPA
 14,XTA,CSUM-1
 ,ATX,R1
 L04361:,BSS,.
C.........KOHTPOЛЬHOИ CYMMИPOBAHИE  OДHOГO CEKTOPA
 10,MTJ,9.
 4,VTM,-7.
 5,VJM,CYMMИP.
 9,MTJ,10.
 13,VJM,CBEPKCYM.
 ,ATX,RRR.
C......   HE БЫЛO ЛИ ПYTAHИЦЫ CEKTOPOB
 ,ITA,10.
 ,ASN,64+1.
 ,AAX,C3.
 14,AOX,TBUT-1.
 5,VJM,ПPOBAДP.ЗA KOHTPOЛEM
C......   COBПAЛA ЛИ KOHTP. CYMMA
 ,XTA,RRR.
 10,AEX,1.
 ,XTA, . ********
 ,UIA,L04357. - HET
 14,XTA,TBUT-1
 ,AAX,ЧTEHИE
 ,U1A,L04362
 ,XTA,R1
 ,ASN,64-12
 ,ATX,R1
 ,YTA,
 ,AEX,RRR
 ,UZA,L04362
 14,XTA,TBUT-1
 ,APX,MSKADRMD
 ,AUX,CON1
 ,ASN,64+3
 ,AEX,TPAKTCT.TPAKT CTATИCTИKИ MOЖET БЫTЬ ЗAKPЫT
 ,U1A,L04357
 12,V1M,L04362
 14,MTJ,11
 14,VTM,YЧET
 ,ITA,14
 14,VJM,TTPRINT
 11,MTJ,14
 L04362:,BSS,
 ,XTA,C13.
 ,ATI,21B.OCTAH. ПO KЧ
 10,UTM,2.
 12,VLM,L04361.
 ,UJ,XOБMБ.
 ПOBTOБM1:,XTA,B25*16
 ,ATX,WS
 ,XTA,BIT25
 ,UJ,OШM1
 OШM/KO:,ATX,WS.
 OШM1:,BSS,
 14,ARX,CTMБ-1.
 14,ATX,CTMБ-1
 14,XTA,TBUT-1
 ,AAX,BIT27
 ,U1A,*8ПOBT
 ,XTA,WS
 ,APX,B25*16
 ,ASN,64-2
 ,YTA,
  ,ATI,11
 11,UTM,-1
 11,V1M,ЦK MБ
 14,XTA,TBUC-1
 ,AAX,ЧTEHИE
 ,U1A,ЦK MБ
 11,VTM,3
 ЦK MБ:11,XTA,TEXER0
 ,UTC,TEX/ER
 ,ATX,
 14,XTA,TBUT-1
 11,VJM,KOД MБ
 TEX/ER:,BSS,1
   :   ,24,2003B
  14,MTJ,11
 ,XTA,A*TOШMБ
 14,VJM,TTPRINT
  :,24,3
 11,MTJ,14
 ПOBTOБMБ:,BSS,.
 *8ПOBT:,BSS,.
C.........ЗAKAЗ ПOBTOPEH 8 PAЗ
 14,XTA,CTMБ-1.
 ,AAX,MOШMБ.HAБPAЛИ OШИБOK
 ,UZA,*8П. - HET
 5,VTM,*8П.
  TO CTMБ:,BSS,
 14,XTA,CTMБ-1.
 ,ATX,БYCT+1.
 ,XTA,.
 14,ATX,CTMБ-1.
 14,XTA,TBUT-1.
  ,AOX,BIT27.ДЛЯ
  ,AEX,BIT27.ПOBTOPA
  14,ATX,TBUT-1.ДИAГHOCTИKИ
 ,ATX,БYCT.
 ,ITA,5.
 ,AOX,STD.
 ,UJ,STATIST.
 *8П:,24,2003B
 11,VJM,ДAЙДHЗ.
 ,ATI,11.
C.........ПPOBEPKA: ЗAKAЗ OT ПOДKAЧKИ
 ,UZA,ПOБ2.ДA- ПOBT. OБMEHA
 ,XTA,OUTJOBSC.
 11,AAX,BITS.
C.........ПPOBEPKA: ЗAДAЧA BЫБPACЫBAETCЯ
 ,UZA,ПOБ2.HET- ПOBT. OБMEHA
C.........BЫБPOC ЗAДAЧИ, POCПИCЬ ПИCTA И TPAKTA)ПO BOЗMOЖHOЖHOCTИ
C.........HE ЧTEHИE ЛИ ДИCПETЧEPA
 14,XTA,TBUC-1.
 ,AEX,DISPBASE.
 ,AAX,CON14.
 ,UZA,ПOБ2.ЧTEHИE ДИCП.MБ
 14,XTA,TBUC-1
 ,APX,MSKADRMD
 ,AUX,CON1
 ,ASN,64+3
 ,AEX,TPAKTCT
 ,UZA,ПOБ2
 14,XTA,TBUC-1.
 ,APX,CON14.
 ,ASN,64+42.
 ,ATI,12.
C.........ПOДГOTOBKA K POCПИCИ И POCПИCЬ ЛИCTA OБMEHA
  10,VTM,-1023
 14,XTA,TBUT-1
 5,VJM,ПPИПOБME
   :   ,24,2.
  ,XTA,
 ПOБ1:10,ATX,71777B.
 10,VLM,ПOБ1.
   :   ,24,2003B
C.........ЗAMEHA ЗAKAЗA(ЧTEHИE) - ЗAKAЗOM(ЗAПИCЬ)
C         ДЛЯ POCПИCИ (ПЛOXOГO) TPAKTA
 14,XTA,TBUT-1
 ,AAX,ЧTEHИE.
 ,UZA,XOБMБ.
C.........ПPOBEPKA: MБ ЗAKPЫT HA 1 TP
 ,XTA,1.
 12,AAX,BIT24.
 ,U1A,XOЧTMБ2.ДA- CЧИTAEM: OБMEH ПPOШEЛ
 14,XTA,TBUT-1
 ,AAX,CON7.
 ,AOX,ЛИCTOБM.
 14,ATX,TBUT-1
 14,ATX,TBUC-1
C.........HA ЧИCTKY TPAKTA
 ,XTA,
 14,ATX,CSUM-1
 ПOБ2:,BSS,.
 14,XTA,TBUC-1
 ,AAX,ЧTEHИE
 ,U1A,ПOБ3
 ,XTA,KOHTPCTP
 ,U1A,KOHTPЗAП
 ПOБ3:,BSS,.
 14,XTA,TBUC-1
 14,AEX,TBUT-1
 ,AAX,ЧTEHИE
 ,UZA,ПOБ4
 14,XTA,TBUT-1
 14,ATX,TBUC-1
 ,XTA,TRABUS
 ,AOX,ЖДYKOHTP
 ,ATX,TRABUS
 ,XTA,
 ,ATX,ЗAHKOHTP
 ,ATX,ЖДYKOHTP
 ПOБ4:,BSS,
  9,VTM,L01642
 ,UJ,EXCHMБ
 ЖДEM:14,XTA,BITS
 ,ATX,ЖДYKOHTP
 L01642:,BSS,
  14,XTA,BITS
  ,UJ,TOTRAINT
 HEПOДK1:,24,2003B.
 11,XTA,*YCЫ.
 ,ATX,OUTCW.
 13,VTM,OБPKOШMA.
 ,ITA,13.
 ,CALL,OUTJOB.
C!!!   OUTJOB HE BCEГДA BOЗBPAЩAET
C      B I15 AДPEC ИПЗ
 ,XTA,OUTCW+1
 15,ATX,COPYS33.
 XOЧTMБ1:,BSS,
 XOЧTMБ2:,BSS,
 XOБMБ:,BSS,
   :   ,24,2003B
 ,XTA,KOHTPCTP
 ,UZA,XOБMБ3
 14,XTA,TBUT-1
 ,AAX,ЧTEHИE
 ,U1A,XOБMБ3
 14,XTA,TBUC-1
 ,AAX,ЧTEHИE
 ,U1A,XOБMБ2
 KOHTPЗAП:,BSS,
 ,XTA,ЗAHKOHTP
 ,U1A,ЖДEM
 14,XTA,TBUC-1
 ,AAX,CON6
 ,AOX,KOHTPCTP
 14,ATX,TBUC-1
 ,ATX,ЗAHKOHTP
 ,UJ,ПOБ4
 XOБMБ2:,BSS,
 ,XTA,TRABUS
 ,AOX,ЖДYKOHTP
 ,ATX,TRABUS
 ,XTA,
 ,ATX,ЗAHKOHTP
 ,ATX,ЖДYKOHTP
 XOБMБ3:,BSS,
 14,XTA,SBUC-1
 ,ANX,
 ,ATI,12
 14,XTA,CTMБ-1
  :,UZA,*+1
 5,VJM,TOCTMБ
   :   ,24,3.HA KOHEЦ
 9,VJM,KOHOБMБ.ЗAKAЗA
 ,XTA,
 14,ATX,TBUC-1
 14,ATX,TBUT-1
 13,VJM,CHVOP.V-OПEPAЦИЯ
 NEWDRTA:,BSS,
C   ...   BЫБOPKA HOBOЙ PAБOTЫ HA БAPAБAHE
  14,XTA,SRJC-1
  ,UZA,L01642
  ,ANX,
  ,ATI,12
 9,VTM,L01642
 ,UJ,CONT EXCH.
C
C
C...   PAБOTA OT 44-41 P.  TRABUS :
C      ПPOXOД ЗOHЫ MЛ
C
 END TAPE:,BSS,
 :,24 ,2003B
  ,MOD,237B. Г P П
  ,AOX,FINZON.+ ПPOГP.ДYБЛЬ
 C,AAX,BIT36-3
  ,UZA,HET ПPEP
  ,AEX,FULWOR
  ,AAX,FINZON.ГACИM
  ,ATX,FINZON.B ДYБЛE
 C,XTA,CSUMSTAT-1
  ,AAX,C377.KTO ДBИГAЛCЯ
  ,UZA,LB3.TOГO И
  ,ATX,ПPOBMФ.OПPOCИM
 C,AAX,ДBECA-3
  ,UZA,CLCПP
  ,ANX,C7
  ,AAX,C7
  ,ATI,I
  ,AEX,C7
 C,ARX,BC1700
  ,ATI,U
C
C...   ECЛИ ДBИЖETCЯ MЛ EC
C      OПPOCИM EE БEЗ BЫБOPKИ
C
  ,XTA,B39/25
 R,CALL,LSOSTEC1
 C,XTA,NEW-3
 I,AAX,BIT8
  ,U1A,HET ПPEP
 C,ATX,ДBECA-3
  U,XTA,SJMT-30B
  ,AAX,БYPЖYЙ
  ,UZA,LB3
  ,XTA,SOST.ДЛЯ EC*608
  ,ASN,64+24.ЗДECЬ
  C,ATX,TBUT-1. Ц K C
C
C...   ECЛИ BCE CTOЯT ИЛИ KOHЧИЛ
C      ДBИЖEHИE MФ EC - OПPOCИM BCEX
C
 LB3:,BSS,
  ,XTA,C377
  ,ATX,ПPOBMФ
C
C...   HAЧAЛO ЦИKЛA OПPOCA COCTOЯHИЯ MФ
C
 CLCПP:,BSS,
 :,24 ,3
  ,XTA,ПPOBMФ
  ,ANX,C7
  ,AAX,C7
  ,ATI,I
  ,AEX,C7
 C,ARX,BC1700
  ,ATI,U
 C,XTA,ШKEC-3
  ,AAX,B39/25
  ,AOX,BIT24
 R,CALL,LSOSTEC
 I,XTA,BIT8
  ,AEX,ПPOBMФ
  ,ATX,ПPOBMФ
  ,U1A,CLCПP
  ,MOD,237B
 C,AAX,BIT36-3
  ,UZA,HET ПPEP
C
C...   BCEX OПPOCИЛИ, A ПPEPЫBAHИE
C      HE CБPOCИЛOCЬ !
C
  ,XTA,BIT11
 :,24 ,3
  ,ARX,CЧETП
  ,AAX,B24/1
  ,ATX,CЧETП
  ,U1A,END TAPE
  ,ITA,C
 C,MTJ,I
 C,VTM,HEYГACИM
  ,ASN,64-16
  ,AOX,TБЯKA
 C,ATX,2
  ,ITA,C
 :,24 ,2003B
 C,CALL,TTPRINT
 I,MTJ,C
  ,UJ ,END TAPE
C
C...   ПPEPЫBAHИE "ПPOXOД ЗOHЫ" ПOГAШEHO.
C      HAЧИHAEM PAБOTY HA KAHAЛE.
C
 HET ПPEP:,BSS,
  C,XTA,TYPEMT-3.B ДYБHE
  ,AAX,BIT35. MФ 40 = CDC608
  ,UZA,KOHЦЫ
  ,EXT,4101B.CДB
  ,ASN,64+6.MФ
  ,AAX,BIT1.CDC608
  KOHЦЫ:,24,3
 C,AOX,NEW-3
 C,AOX,ДBECA-3
  ,AAX,C377
  ,AEX,C377
 C,AAX,CSUMSTAT-1
 C,AEX,CSUMSTAT-1
 C,ATX,CSUMSTAT-1
  ,YTA,
 C,AOX,W05741-3.MФ, ГДE
 C,ATX,W05741-3.KOHEЦ ДBИЖEHИЯ
  ,XTA,
  ,ATX,CЧETП
  ,ATX,ПPOBMФ
C
C...   HACTPOЙKA ПAPAMETPOB KBY-KAHAЛA
C
 ПPOДPAБ:,BSS,
 ,XTA,
 ,ATX,CH
 R,VTM,*7
 ,UJ,PUT16
C
C...   ШAГ CKAHEPA ПO KBY-KAHAЛY
C
 *1:,BSS,
 ,XTA,BIT1
 C,ARX,COUNT-3
 ,AAX,C7
 C,ATX,COUNT-3
 ,XTA,BIT1
 ,ARX,CH
 ,ATX,CH
 ,AEX,KPYГ
 ,UZA,ALLEOM.ПPOШЛИ KPYГ
C
C...   BЫБPAЛИ OЧEPEДHOЙ MФ
C
 *7:,BSS,
 C,XTA,ДBECA-3
 ,U1A,ALLEOM.A KAHAЛ ЗAHЯT
 C,XTA,COUNT-3
 ,ATI,I
 ,AEX,C7
 C,ARX,BC1700
 ,ATI,U
 U,XTA,SJMT-30B
 ,UZA,*1.HET PAБOTЫ
 ,ANX,
 ,ATI,N.ИHДEKC ПO WL
 C,XTA,ШHГ-3
 ,ASN,64+8.ЖДEM
 C,AOX,ШHГ-3.ГT/ЗП
 C,AOX,CSUMSTAT-1.ИЛИ YЖE
 I,AAX,BIT8.ДBИЖEMCЯ
 ,U1A,*1
 ,ATX,IDMT
 U,XTA,T2MT-30B
 ,AAX,C/ПEP.ИДET
 ,AEX,C/ПEP.ПEPEMOTKA
 ,UZA,*1
  ,NTR,3
 C,XTA,CHBOSS-3.HA KAHAЛE
 ,UZA,HET MOH.MOHOПOЛИЯ
 ,ASN,64-18. - ПPOПYCKAEM
 N,AEX,WL1-1.TOЛЬKO
 ,AAX,B48/43.EE
 ,UZA,HET MOH.BЛAДEЛЬЦA
 C,XTA,TBUC-1
 ,UZA,*1
 C,XTA,SBUC-1
 N,AEX,BITS
 ,U1A,*1
 HETMOH:,BSS,
 RT,VTM,*1
 N,XTA,WL1-1
 ,ASN,64+42
 ,ATI,JN. Д H З
 JN,MTJ,RF
 RF,UTM,-JNREC.ПP-K OПOЗH.
 N,XTA,WL1-1
 ,AAX,BITПOДB
 ,ATI,MF
 N,XTA,WL1-1
 ,AAX,BITИMЯ
 ,ATI,NF
 N,XTA,WL1-1
 ,AAX,KPMASK
 :,UZA,*+1
 R,VJM,PRIPTRA
 ,XTA,NH
 ,ATI,DC.YBY-KAHAЛ
 DC,XTA,BC1700.AДPECA
 ,ATI,CW.CЛYЖ.CЛOB
 C,XTA,W05741-3
 I,AAX,BIT8
  C,AEX,W05741-3
  C,ATX,W05741-3
   ,YTA,.БЫЛ ПPOXOД
 ER,VTM,24.ЗOHЫ
  R,VTM,KO БЭCM. - ДA
   ,U1A,SWI
 C,XTA,SRJC-1.ECTЬ ГOTOBЫЙ
 N,AAX,BITS.K OБMEHY ЗAKAЗ
 ER,VTM,
 R,VTM,ПOДЗAKMЛ. - HET
  ,UZA,SWI
 R,VTM,BЫПOЛH
 C,XTA,TBUC-1.YBY-KAHAЛ
 ,WTC,N8.CBOБOДEH
 ,AOX,TBUC-1
 ,U1A,*1. - ЗAHЯT
C
C...   YXOД, ECЛИ HAДO, HA ПPOЦEДYPY
C      OTPAБOTKИ ПO HECTAHД. ЗAKAЗY
C      BOЗBPAT OTTYДA - ПO  RT.
C
 SWI:,BSS,
  ,XTA,
 ,ATX,CH
 ,XTA,БYPЖYЙ
 N,AAX,BITS
 R,UZA,
  N,XTA,WL2-1
 ER,ASN,64
   ,ATI,R
 :ER,V1M,*+1
 RO,VJM,PRT UP
  N,XTA,WL1-1
  R,UJ ,
C
C...   OPГAHИЗAЦИЯ ПPEДBAPИTEЛЬHЫX
C              ПOДBOДOB
C
 ПOДЗAKMЛ:,BSS,
 R,VTM,C17
 NF,V1M,KORRTAB
 U,XTA,T1MT-30B
 ,AAX,BIT1
 ,UZA,HE ИMЯ
 N,XTA,WL1-1
 ,AAX,BIT38
 R,VTM,C7
 ,U1A,KORRTAB
 R,VTM,C1
 KORRTAB:,BSS,
 R,XTA,
 N,AOX,WL1-1
 N,ATX,WL1-1
  ,XTA,ZBIT1
 U,AAX,T1MT-30B
 U,ATX,T1MT-30B
 U,XTA,T2MT-30B
  ,AAX,B48/13
  ,AOX,C1
 U,ATX,T2MT-30B
 HEИMЯ:,BSS,
 N,XTA,WL1-1
  ,ASN,64+12
 U,AEX,T2MT-30B
  ,AAX,B24/14
 U,AEX,T2MT-30B
 U,ATX,T2MT-30B
  ,ARX,C7777
  ,AAX,C7777
 ,ATX,NZON.CZ-1
  ,AEX,C7777
 R,VTM,ПOДЗAK1
  ,UZA,ПPЯДBИЖ1
 ПPOBPEB:,BSS,
 U,XTA,T2MT-30B
  ,ASN,64+12
 ,AAX,C7777.DZ
 ,X-A,NZON.CZ-DZ-1
 ,U1A,ПPЯДBИЖ1.CZ.LE.DZ
 ,ATX,WS
 I,XTA,PEBEPC
 C,AOX,CSUMSTAT-1
 C,ATX,CSUMSTAT-1
  ,XTA,C2
  ,ARX,NZON
  ,ATX,NZON.CZ+1
  ,A-X,WS.DZ+2
  ,A-X,WS.2*DZ+3-CZ
  ,UZA,SET2MT.CZ.LE.2*DZ+3
 C,XTA,ШKEC-3
  ,UZA,SET2MT
   ,ARX,
 :,U1A,*+1
   ,UTC,BIT9-BIT7
  ,XTA,BIT7
 U,EXT,100B-30B
  ,XTA,C/ПEP
 U,AOX,T2MT-30B
 U,ATX,T2MT-30B
  ,XTA,
  ,ATX,NZON
 ПPЯДBИЖ1:,BSS,
  ,XTA,FULWOR
 I,AEX,PEBEPC
 C,AAX,CSUMSTAT-1
 C,ATX,CSUMSTAT-1
 SET2MT:,BSS,
 U,XTA,T2MT-30B
  ,AAX,B41/1
  ,AAX,B48/13
  ,AOX,NZON
  SETT2:,BSS,
 U,ATX,T2MT-30B
 R,UJ,
  KOPPHЗOH:,XTA,C1
   U,ARX,T2MT-30B
  ,AAX,ZBIT13
  ,UJ,SETT2
 ПOДЗAK1:,BSS,
 NF,V1M,HA BЫП
  ,XTA,REWINDSC
 U,APX,BIT48-30B
  ,ASN,64+44
 N,AOX,WL1-1
 N,ATX,WL1-1
  ,AAX,BIT4
 R,VTM,HA BЫП
  ,U1A,NO REW
  ,XTA,B39/25
 U,AAX,T2MT-30B
 U,ATX,T2MT-30B
  ,XTA,C7
 N,AOX,WL1-1
 N,ATX,WL1-1
  ,UJ,HA BЫП
 NO REW:,BSS,
 N,XTA,WL1-1
  ,AAX,BIT19
  ,U1A,KOPPHЗOH
 N,XTA,WL1-1
  ,AAX,C1
  ,U1A,HA BЫП
 HA ПOДB:,BSS,
 I,XTA,ПOДBOД
 C,AOX,CSUMSTAT-1
 C,ATX,CSUMSTAT-1
 U,XTA,T2MT-30B
  ,AAX,C/ПEP
  ,AEX,C/ПEP
  ,UZA,*7
 C,XTA,ШKEC-3
 I,AAX,BIT24
  ,U1A,OБM5017
 C,XTA,CSUMSTAT-1
 I,AAX,PEBEPC
 :,UZA,*+1
  ,UTC,C7777-C1
 R,VTM,
 R,XTA,C1
 U,ARX,T2MT-30B
  ,AAX,ZBIT13
 U,ATX,T2MT-30B
  ,AAX,C7777
  ,ATX,NZON
 U,XTA,T2MT-30B
  ,ASN,64+12
  ,AAX,C7777
  ,A-X,NZON
  ,ATI,L.DZ-CZ
 L,V1M,NEQNZ
 N,XTA,WL1-1.DZ=CZ
  ,AAX,B21/19
  ,UZA,HA BЫП
 R,V1M,HA ДBИЖ
 MF,V1M,XOPOБM1
 HA ДBИЖ:,BSS,
 MV,VTM,1
 R,VZM,EXCHДBИЖ
 ШЗH:,BSS,
 MV,VTM,3
 ,UJ,EXCHДBИЖ
 NEQNZ:,BSS,
 N,XTA,WL1-1
  ,APX,B21*18
  ,ASN,64+46
  ,ATI,F
 F,V1M,CHEFLA
  ,XTA,B21*18
 N,AOX,WL1-1
 N,ATX,WL1-1
 CHEFLA:,BSS,
 L,UTM,-1
 L,V1M,HA ДBИЖ
 F,UTM,-1.DZ=CZ+1
 F,VZM,HA ДBИЖ.READ
C
C...   ЗAKAЗ ГOTOB K OБMEHY
C
 HA BЫП:,BSS,
  ,XTA,FULWOR
 I,AEX,ПOДBOД
 C,AAX,CSUMSTAT-1
 C,ATX,CSUMSTAT-1
 N,XTA,BITS
 C,AOX,SRJC-1
 C,ATX,SRJC-1
  ,UJ,*7
C
C...   YBY-KAHAЛ ЗAKAЗY BЫДEЛEH
C
 BЫПOЛH:,BSS,
  ,XTA,REWINDSC
 U,AAX,BIT48-30B
  ,U1A,HE CMOTKA
 C,XTA,ШKEC-3
 I,AAX,BIT8
  ,UZA,ЧT CC
 MЛHAHAЧ:,BSS,
 U,XTA,BIT48-30B
  ,AOX,REWINDSC
  ,ATX,REWINDSC
  ,XTA,ZC7
 N,AAX,WL1-1
 N,ATX,WL1-1
  ,AAX,BIT19
  ,UZA,EXCHMЛ1
  ,UJ,ЗAПMГЗ
 HECMOTKA:,BSS,
 N,XTA,WL1-1
  ,AAX,C3
  ,U1A,BЫПOЛH/
 N,XTA,WL1-1
  ,AAX,B20/19
  ,UZA,BЫПOЛH/
  ,AEX,B20/19
  ,UZA,EXCHMЛ1
  ,AEX,BIT19
  ,UZA,ШЗH
 ЗAПMГЗ:,BSS,
 MV,VTM,20B
 ,UJ,EXCHДBИЖ
C
C...   ЗAXBAT YBY-KAHAЛA
C
 BЫПOЛH/:,BSS,
  U,XTA,T1MT-30B.KOHTPOЛЬ
  ,AAX,CREADIS.ЗAПИCИ BKЛЮЧEH
  ,UZA,BЫПOЛH//.-HET
  R,VJM,POP.ЗAXBAT ЛИCTA
  P,VIM,ALLEOM.HE ДAЛИ
  C,WTC,TYPEMT-3
  CW,VTM,.BOCCTAHOBЛEHИE
  N,XTA,WL1-1
  ,AEX,SCDICH
  ,AAX,KPMASK.P-OПEPAЦИЯ
  ,UZA,BЫПOЛH//.YЖE БЫЛA
C...  ЗAMEHA B KYC-E HOMEPA ЛИCTA
  N,XTA,WL1-1
  ,AEX,ЧTEHИE
  C,ATX,TBUT-1.CTAPЫЙ KYC
  ,AEX,SCDICH.HOBЫЙ ЛИCT
  ,AAX,KPMASK
  N,AEX,WL1-1
  N,ATX,WL1-1
  BЫПOЛH//:,BSS,
 N,XTA,BITS
 C,ATX,SBUC-1
 N,XTA,WL1-1
 C,ATX,TBUC-1
 R,VJM,ДYБ
 U,XTA,T2MT-30B
  ,AAX,C7777
  ,U1A,EXCHMЛ1
 N,XTA,WL1-1
  ,AAX,B20/19
  ,UZA,ЧTCC
 N,AEX,WL1-1
  ,AOX,BIT19
  ,AAX,ZBIT12
 N,ATX,WL1-1
 ЧTCC:,BSS,
  ,XTA,BIT3
 N,AEX,WL1-1
  ,AOX,BIT2
 N,ATX,WL1-1
 EXCHCЛCЛ:,BSS,
 N,XTA,WL1-1
  ,AAX,B21*18
  ,U1A,EXC2
  ,XTA,REWINDSC
 U,AAX,BIT48-30B
  ,UZA,EXC2
  ,XTA,B21*18
 N,AOX,WL1-1
 N,ATX,WL1-1
 EXC2:,BSS,
 N,XTA,BITS
 C,ATX,SBUC-1
 N,XTA,WL1-1
 C,ATX,TBUC-1
  ,XTA,
 CW,ATX,3
 CW,ATX,6
  R,VJM,ДYБ
  R,VTM,B21*18
  N,XTA,WL1-1
   ,AAX,BIT4
   ,ASN,64+2
   ,AEX,C3
   ,ATI,MV
 ЗAПYCKMЛ:,BSS,
 RT,VTM,*7
 RO,VTM,-8
  :,ATX,1
 RO,VLM,*
  C,XTA,TBUC-1
  R,AOX,
   ,AAX,KYCMAC
   ,AOX,BN8
 DC,EXT,
   ,XTA,FULWOR
  :,24 ,2003B
 DC,AEX,BIT29-3
   ,ATI,
   ,MOD,37B
   ,ATI,
 RO,VJM,PRT UP
  :,24 ,3
   ,UTC,BIT14
 EXCHДBИЖ:,BSS,
   ,XTA,
   ,ATX,EXCHFL
  ,XTA,FIX DENS.BЫCOK.ПЛOTH.
  ,U1A,SET DENS.ФИKCИPOBAHA
 U,XTA,T1MT-30B
  ,AAX,B11/10
  SET DENS:,ATX,DENS
 RF,V1M,NO DENS
 NF,VZM,NO DENS
  U,XTA,T1MT-30B.BAPЬИPYEM
   ,AAX,BIT26.ПЛOTHOCTЬ
   ,ASN,64+15.ПPИ
   ,AOX,DENS.OПOЗHABAHИИ
   ,ATX,DENS
 NO DENS:,BSS,
 C,ARX,ШKEC-3
  ,U1A,MTEC5012
 ,XTA,DENS
 ,ASN,64-1
 ,ATX,DENS
  ,ITA,MV
  ,ATX,WS
  ,AAX,BIT2
  ,ASN,64+1
  ,AEX,B16/1
  ,AAX,WS
  ,ASN,64-2
  ,ATI,MV
  ,APX,MASK5017
  ,ANX,
  ,ASN,64-3
  ,ATI,R
  ,XTA,EXCHFL
  ,U1A,MTEC5012
 C,XTA,ШKEC-3
  ,AAX,BIT47
  ,UZA,MTEC5012
  ,XTA,TKYC17
 R,ASN,64-8
  ,AAX,C377
  ,ATI,MV
 MTEC5012:,BSS,
  ,ITA,MV
  ,AOX,DENS
 :,24,2003B
   ,AOX,EXCHFL
 U,EXT,100B-30B
   ,ASN,
   ,ASN,
  R,CALL,LSOSTEC1
  C,XTA,NEW-3
  I,AAX,BIT16
   ,U1A,NO READY
  I,XTA,BIT8
  C,AOX,CSUMSTAT-1
  C,ATX,CSUMSTAT-1
  I,XTA,BIT8
  C,AAX,ШKEC-3
  C,ATX,ДBECA-3
  ,XTA,MAXTIMET.YCT.TAЙMEPA
 C,ATX,MTTIMCNT-3
 C,XTA,BIT36-3
  ,AOX,TRAMASK
  ,ATX,TRAMASK
 :,24,3
 RT,UJ,
C
C...   ЗAXBAT YBY-KAHAЛA
C
 EXCHMЛ1:,BSS,
 N,XTA,BITS
 C,ATX,SBUC-1
 N,XTA,WL1-1
 C,ATX,TBUC-1
  ,AAX,ЧTEHИE
  ,U1A,EXЧTMЛ1
C
C...   ФOPMИPOBAHИE CЛYЖEБHЫX CЛOB
C      ЗAПИCЫBAEMOЙ ЗOHЫ
C
 :,24 ,2
  ,XTA,71777B
 :,24 ,3
 CW,ATX,5.ПOCЛ.CЛOBO
   ,XTA,CW0C
 CW,ATX,.CПEЦKOД
  U,XTA,TANAMEMT-30B
 CW,ATX,1.ИMЯ
 CW,ATX,2.ЛEHTЫ
   ,XTA,LWC
  :,24 ,2
   ,ATX,71777B
  :,24 ,3
  U,XTA,T2MT-30B
   ,AAX,B24/1
   ,AOX,CW36C
 CW,ATX,3.HOMEP
 CW,ATX,6.ЗOHЫ
  N,XTA,WL2-1
 CW,ATX,7.KOHTP.CYMMA
  N,XTA,WL1-1
  R,CALL,AUTOR
  CW,ATX,4.KTO ПИCAЛ
 EXЧTMЛ1:,BSS,
 C,XTA,TBUC-1
  ,AAX,BIT37
 :,UZA,*+1
  ,UTC,MAST2MT-B41/1
  ,XTA,B41/1
 U,AAX,T2MT-30B
 U,ATX,T2MT-30B
 EXCHДYБ:,BSS,
  R,VTM,
 MV,VTM,1
 DC,XTA,BIT5-3
   ,EXT,31B
  C,XTA,TBUC-1
   ,AAX,ЧTEHИE
   ,UZA,ЗAПYCKMЛ
  C,XTA,TBUC-1
   ,AAX,CЛYЖCЛOB
   ,U1A,HE ЧT ЗOH
  :,24 ,2
   ,ATX,71777B
 HEЧTЗOH:,BSS,
 :,24 ,3
  ,XTA,
 CW,ATX,3
 CW,ATX,6
 C,XTA,TBUC-1
  ,AAX,HAЛOЖ
  ,UZA,ЗAПYCKMЛ
 U,XTA,T2MT-30B
  ,AAX,BIT43
  ,UZA,ПPOДHAЛ
 U,AEX,T2MT-30B
 U,ATX,T2MT-30B
  ,XTA,HAЛOЖ
 C,AEX,TBUC-1
 C,ATX,TBUC-1
  ,UJ ,ЗAПYCKMЛ
 ПPOДHAЛ:,BSS,
  ,XTA,BIT42
 U,ARX,T2MT-30B
 U,ATX,T2MT-30B
  ,UJ ,ЗAПYCKMЛ
C
C...   OTPAБOTKA KOHЦA ДBИЖEHИЯ
C      MЛ B PEЖИME БЭCM-6
C
 KO БЭCM:,BSS,
   ,XTA,OUTJOBSC
 JN,AAX,BITS.HA CHЯTИE
   ,U1A,*OUTJOB.ЗAДAЧИ
  C,XTA,NEW-3
  I,AAX,BIT16
   ,U1A,NO READY.ПPOПAЛA ГT
  N,XTA,WL1-1
   ,AAX,BIT18
   ,U1A,MФ ГOT
   ,XTA,CONTRWR
  U,APX,BIT48-10B
  I,ASN,64+24
  C,AAX,NEW-3
   ,UZA,MФ ГOT
   ,UTC,3.ПPOПAЛA ЗП
C
C...   OTPAБOTKA HEГOTOBHOCTИ MФ
C
 NO READY:,BSS,
  R,VTM,3
  C,MTJ,RO
   ,ASN,64+8
   ,ATX,KYCДB
 MF,V1M,XOPOБM1.HA ПOДBOДE
 :,24,3
 RF,V1M,HE OПO
 NF,V1M,CT1
  I,XTA,BIT48
   ,AOX,KYCДB
   ,ATX,KYCДB
 HE OПO:,BSS,
 N,XTA,WL1-1
  ,AAX,BIT7. TT-ПEЧATЬ
  ,U1A,HE ГOTOB.YЖE БЫЛA
  ,XTA,BIT7
 N,AOX,WL1-1
 N,ATX,WL1-1
  ,ITA,U.COOБЩEHИE
 M,VJM,MФ ПЛOX.OПEPATOPY
 RO,MTJ,C
  I,XTA,BIT8
  C,AOX,W05741-3
  C,ATX,W05741-3
  C,XTA,TBUC-1
   ,UZA,*7
  C,XTA,SBUC-1
  N,AEX,BITS
   ,U1A,*7
C
C...   OCBOБOЖДEHИE YBY-KAHAЛA
C
  C,ATX,TBUC-1
  C,ATX,SBUC-1
 DC,XTA,BIT5-3
   ,EXT,31B
  N,XTA,BITS
  C,AEX,SRJC-1
  C,ATX,SRJC-1
  CW,MTJ,MF.AДP.CЛ.CЛ.
  R,VJM,VOP.ПOPTИT CW
  R,VTM,8
  ,CALL,YCTBEPCB.OT ГPEXA
  C,WTC,TYPEMT-3
  CW,VTM,
  N,XTA,WL1-1
   ,AAX,B21/18
 RO,VTM,*7
   ,UZA,RES LW
   ,AEX,B20/19
 RO,U1A,
C
C...   BOCCTAHOBЛEHИE ПOCЛEДHEГO
C      CЛOBA ЛИCTA OБMEHA
C
  RES LW:,BSS,
  CW,XTA,5
  :,24,2
  ,ATX,71777B
  :,24,3
  RO,UJ,
C
 HE ГOTOB:,BSS,
  ,XTA,KYCДB
 C,AOX,ШHГ-3
 C,ATX,ШHГ-3
  ,UJ,*1
C
C
 MФ ГOT:,BSS,
 ER,VTM,KO/ЗП
  N,XTA,WL1-1
   ,AAX,BIT7
   ,UZA,MФ TIM
  N,XTA,WL1-1.ECЛИ БЫЛA
   ,AAX,MASWL.ПPOПAЖA
   ,AEX,C13.ГT/ЗП,
  N,ATX,WL1-1.ПPOBEДEM
   ,XTA,B48/13.ЗAHOBO
  U,AAX,T2MT-30B.ИHИЦИAЛИЗAЦИЮ
  U,ATX,T2MT-30B
   ,UJ,HA BЫП
 MФ TIM:,BSS,
 U,XTA,T1MT-30B
  ,AAX,BIT12
 :,UZA,*+1
 R,VJM,TAPERR.БЫЛO "KДB"
  :MF,MTJ,ER.ПPЯЧEM
 MF,V1M,MФ ГOT1
 JN,XTA,*YCЫ
   ,ATI,R
   ,AAX,B30/16
   ,UZA,MФ ГOT1
  U,XTA,T1MT-30B
   ,AAX,B29/26
   ,U1A,MФ ГOT1
   ,XTA,TAPTIME
  R,A+X,BISTIME
  R,ATX,BISTIME
 MФ ГOT1:,BSS,
 C,XTA,CSUMSTAT-1
 I,AAX,ПOДBOД
  ,U1A,HA ПOДB
C
C...   ПPABKA "KЧ" B CЛYЖ.CЛOBAX OБMEHA
C
  CW,MTJ,MF
  R,VTM,8
  I,MTJ,RO
  :,24,2003B
  ,CALL,YCTBEPCB
  :,24,3
  RO,MTJ,I
  ER,MTJ,MF
  ER,VTM,KO/ЗП
C
 N,XTA,WL1-1
  ,AAX,C3
  ,AEX,C3
  ,U1A,БЫOБMЛ1
 N,XTA,WL1-1
  ,AAX,BIT3
  ,U1A,ЧTCC
  ,MOD,237B
 DC,AAX,BIT29-3
  R,VTM,HAЧ OБM MЛ
   ,UZA,TAPERR
  N,XTA,WL1-1
   ,AAX,BIT4
   ,UZA,EXCH CЛ CЛ
  N,AEX,WL1-1
  N,ATX,WL1-1
 NF,V1M,KOHTPИM
   ,XTA,REWINDSC
  U,AAX,BIT48-30B
   ,UZA,MЛ HA HAЧ
C
C...   KOHTPOЛЬ CЛYЖ.CЛOB ПPИ
C      ИHИЦИAЛИЗAЦИИ ЗAKAЗA
C
 KOHTPИM:,BSS,
   ,EXT,4035B
 DC,AAX,BIT12-3.OШBY
   ,U1A,OШMBY
 RO,VJM,ПPOB CЛ CЛ
   ,XTA,NZON
   ,AAX,ZBIT1
   ,ASN,64-12
   ,AEX,NZON
   ,AAX,B24/13
   ,U1A,TAPERR
 CW,XTA,1
   ,ATX,IDMT
 CW,AEX,2
   ,UZA,COBП ИД MЛ
 CW,XTA,4
   ,AEX,CW4C
 :NF,V1M,*+1
  R,VTM,COBП ИД MЛ
  :,UZA,TAPERR
   ,XTA,TP DI
   ,AAX,BIT DI
   ,UZA,TAPERR
 CW,XTA,2
  R,CALL,MTDISPAC
   ,ATX,IDMT
 RT,VTM,*1
 COBПИДMЛ:,BSS,
 NF,V1M,CT1
   ,XTA,IDMT
  U,AEX,TANAMEMT-30B
   ,UZA,COBП TN
  U,XTA,TANAMEMT-30B
   ,AEX,NO CHECK
   ,AAX,B48/13
   ,UZA,MT NO CH
   ,XTA,ШЧYMЛ
  U,AAX,BIT48-30B
   ,U1A,HAЧ OБM MЛ
  U,XTA,BIT48-30B
   ,AOX,ШЧYMЛ
   ,ATX,ШЧYMЛ
   ,ITA,U
  R,VTM,-3
  C,MTJ,RO
  M,VJM,MФ ПЛOX
 RO,MTJ,C
 HAЧOБMMЛ:,BSS,
  ,XTA,BIT4
 N,AOX,WL1-1
 N,ATX,WL1-1
  ,UJ,EXCH CЛ CЛ
 MT NO CH:,BSS,
  ,XTA,IDMT
 U,ATX,TANAMEMT-30B
 COBП TN:,BSS,
 N,XTA,WL1-1
  ,AEX,C3
 C,ATX,TBUC-1
 TO ПPOPE:,BSS,
  ,XTA,C7777
  ,AAX,NZON
  ,ATX,NZON
 R,VJM,ПPOB PEB
 N,XTA,BITS
 C,AEX,SRJC-1
 C,ATX,SRJC-1
 C,XTA,TBUC-1
  ,AAX,ZB37*23
 N,ATX,WL1-1
  ,XTA,
 C,ATX,TBUC-1
  ,UJ,HA ПOДB
C
C...   OKOHЧEH OБMEH C ЗOHOЙ
C      ИЛИ ЧTEHИE CЛYЖ.CЛOB ПPEДЗOHЫ
C
 БЫOБMЛ1:,BSS,
 R,VTM,OK OБ
 N,XTA,WL1-1
  ,AAX,B21*18
  ,U1A,KOPPHЗOH
 N,XTA,WL1-1
  ,AAX,B20/19
  ,UZA,KOPPHЗOH
  ,AEX,B20/19
 RO,VTM,*3 ЭT PAЗM
 R,VTM,RES LW
  ,UZA,KOPPHЗOH
 C,XTA,ШKEC-3
  ,AAX,BIT47
  ,ASN,64+28
  ,ARX,BIT19
 N,ARX,WL1-1
 ЗK*:,BSS,
 N,ATX,WL1-1
  ,UJ,HA BЫП
 *3ЭTPAЗM:,BSS,
 N,XTA,WL1-1
  ,AAX,BIT12
  ,U1A,XOPOБM1
 C,ATX,TBUC-1
  ,XTA,BIT20
  ,AEX,BIT12
 N,AEX,WL1-1
  ,UJ,ЗK*
C
C...   OKOHЧEH OБMEH C ЗOHOЙ
C
 OK OБ:,BSS,
 C,XTA,TBUC-1
  ,AAX,ЧTEHИE
 R,VTM,OШOБMЛ1
 RO,VTM,БЫЛOЧTMЛ
  ,U1A,ПPOB CЛ CЛ
  ,MOD,237B.KOHEЦ
 DC,AAX,BIT29-3.OБMEHA
   ,U1A,CH OШM ЗП. - БЫЛ
 RO,VTM,TAPERR. - HET
 R,VJM,RES LW
  ,XTA,B21*18.ПOBTOPИM
 C,AOX,TBUC-1.ЗAПИCЬ
 C,ATX,TBUC-1.OБEИX ЗOH
  ,UJ,ПOBOБMЛ3
 CH OШMЗП:,BSS,
   ,EXT,4035B
 DC,AAX,BIT5-3.OШM
 ER,VTM,
   ,UZA,CH ДYБ ЗП. - HET
 ER,VTM,OШ/ЗП
  R,VJM,TAPERR
 CH ДYБЗП:,BSS,
 U,XTA,T2MT-30B.OCHOBHAЯ
  ,ARX,BIT48.ЗOHA
  ,UZA,ЗAДYБЗOH. - ДA
 RO,VJM,RES LW
 ER,VZM,XOPЗAП1.ДYБЛЬ O'KEY
  U,XTA,T2MT-30B
   ,AAX,BIT44
   ,UZA,XOPЗAП1.OCH. O'KEY
 ПOBOБMЛ3:,BSS,
 C,XTA,TBUC-1
  ,AOX,BIT37
 N,ATX,WL1-1
C
C...   PEBEPC MЛ ДЛЯ ПOBTOPEHИЯ OБMEHA
C
  ПOBOБMЛ1:5,VJM,VOP
  BЗAД:,BSS,
  ,XTA,
 C,ATX,SBUC-1
 C,ATX,TBUC-1
 I,XTA,ПOДBOД
 I,AEX,PEBEPC
 C,AOX,CSUMSTAT-1
 C,ATX,CSUMSTAT-1
 N,XTA,BITS
 C,AEX,SRJC-1
 C,ATX,SRJC-1
 RT,VTM,TRAINT
  RO,VTM,ШЗH
 PRT DOWN:,BSS,
  ,XTA,PRTMTCH
 C,AEX,BIT44-3
  ,WTC,N8
  ,AOX,BIT44-3
 PRT SET:,BSS,
   ,ATX,PRTMTCH
 RO,UJ,
 PRT UP:,BSS,
  ,XTA,FULWOR
  ,WTC,N8
  ,AEX,BIT44-3
  ,AAX,PRTMTCH
 C,AOX,BIT44-3
  ,UJ,PRT SET
C
C...   ЗAПИCЬ ДYБЛЯ ЗOHЫ
C
 ЗAДYБЗOH:,BSS,
 :ER,VZM,*+1
   ,AOX,BIT44.OШИБKA B
  U,ATX,T2MT-30B.OCH.ЗOHE
  R,VJM,ДYБ
   ,XTA,C1
 CW,ARX,6.+1 K HOMEPY
 CW,ATX,6.ЗOHЫ
 CW,ATX,3
   ,UJ,EXCH ДYБ
C
C...   ЗAПИCAЛИ OБE ЗOHЫ
C
  XOPЗAП1:,BSS,
  U,XTA,T1MT-30B
  ,AAX,CONTROL
  ,UZA,XOPOБM1
C
C...   OPГAHИЗAЦИЯ KOHTPOЛЬHOГO ЧTEHИЯ
C
  ,XTA,CREADIS
  U,AOX,T1MT-30B
  U,ATX,T1MT-30B
  ,XTA,ЧTEHИE
  N,AOX,WL1-1
  N,ATX,WL1-1
  ,UJ,BЗAД
C
C...   P-  И  V-OПEPAЦИИ ДOCTYПA
C      K ЛИCTY CEKTOPHOГO OБMEHA
C      OБPAЩEHИE ПO  R.
C
  POP:C,XTA,BIT47
  ,UJ,PS*POP.ПOPTИT CW
C
  VOP:,BSS,.ПOPTИT CW,DC
  U,XTA,T1MT-30B
 ,AAX,CREADIS
  ,UZA,VOP1
  U,AEX,T1MT-30B
  U,ATX,T1MT-30B
  ,XTA,ЧTEHИE
  VOP1:N,AEX,WL1-1
  N,ATX,WL1-1
  ,AEX,SCDICH
  ,AAX,KPMASK
  R,U1A,.HE БЫЛO P
  C,XTA,TBUT-1.BOCCTAHOBЛEHИE
  N,ATX,WL1-1.ЗAKAЗA
  C,XTA,BIT47.HA ЗAПИCЬ
  ,UJ,PS*VOP
C
C...   OKOHЧИЛOCЬ ЧTEHИE ЗOHЫ
C
 БЫЛOЧTMЛ:,BSS,
  ,XTA,NZON
  ,AAX,ZBIT1
  ,ASN,64-12
  ,AEX,NZON
  ,AAX,B24/13.HOMEP ЗOHЫ
  ,U1A,TAPERR.БPEДOB
  ,XTA,NZON
  ,ARX,C1
 U,AEX,T2MT-30B
  ,AAX,C7777
 R,VTM,TO ПPOPE
  ,U1A,TAPERR.HE TA ЗOHA
 N,XTA,WL1-1
  ,AAX,CЛYЖCЛOB
  ,U1A,БЫ ЧT CC2
 RO,VTM,KЧ ЧT
  ,ITA,RO
  ,ATX,RETTRA
  ,XTA,C2003
  ,ATI,21B
 :,24 ,2002B
  ,XTA,71777B
 :,24 ,2003B
  ,AEX,LWC
 ER,VTM,CБ/CИ
  R,VTM,ГAШ HAЛ
   ,U1A,TAPERR
   ,XTA,C13
   ,ATI,21B
   ,EXT,4035B
   ,ATX,PE FL
   ,ASN,64+7
   ,AOX,PE FL
 DC,AAX,BIT5-3
   ,ATX,PE FL
   ,UZA,MAKE KS
  R,VJM,OШMBY
  C,XTA,TBUC-1
   ,AAX,HAЛOЖ
   ,UZA,SET HAЛ
 MAKE KS:,BSS,
 CW,XTA,7
   ,ATX,CHSUMR
 CW,XTA,5
  :,24 ,2002B
   ,ATX,71777B
  :,24 ,2003B
 ER,VTM,-37B
 CW,VTM,
  R,VJM,SUMMING
   ,AEX,CHSUMR
   ,U1A,OШ KC
  U,XTA,T1MT-30B
  ,AAX,CREADIS
  ,UZA,XOPOБM1
C
C...  ПPИ KOHTPOЛЬHOM ЧTEHИИ
C     CPABHИM CЧИTAHHYЮ KC C
C     CYMMOЙ ЛИCTA, ЧTOБЫ ЗACEЧЬ
C     CИTYAЦИЮ, KOГДA ЗAПИCЬ
C     HE ПPOИЗOШЛA BOOБЩE.
C
  ,XTA,CHSUMR
  N,AEX,WL2-1
  ,UZA,XOPOБM1
C
C...     CYMMЫ HE COBПAЛИ
C
  OШ KC:,BSS,
 ER,VTM,KC/MЛ
  R,VJM,TAPERR
   ,XTA,PE FL
   ,U1A,SET HAЛ
 ГAШ HAЛ:,BSS,
  C,XTA,TBUC-1
  ,AOX,HAЛOЖ
  ,AEX,HAЛOЖ
  ,UJ,WR TBUC
 KЧ ЧT:,BSS,
 R,VJM,OШMBY
 SET HAЛ:,BSS,
  ,XTA,HAЛOЖ
 C,AOX,TBUC-1
 WR TBUC:,BSS,
 C,ATX,TBUC-1
  ,XTA,NH
  ,ATI,DC
 DC,XTA,BC1700
  ,ATI,CW
  ,XTA,BIT48
 U,ARX,T2MT-30B
  ,U1A,ПOBOБMЛ3
 U,ATX,T2MT-30B
 R,VTM,EXCH ДYБ
  ,UJ,ДYБ
 OШOБMЛ1:,BSS,
 N,XTA,WL1-1
  ,AAX,CЛYЖCЛOB
  ,UZA,ГAШ HAЛ
 MF,V1M,ГAШ HAЛ
  ,UJ,ПOBOБMЛ1
 БЫЧTCC2:,BSS,
 MF,V1M,XOPOБM1
  ,XTA,Z21*18*1
 N,AAX,WL1-1
 N,ATX,WL1-1
  ,UJ,BЫПOЛH
 OШMBY:,BSS,
 ER,VTM,OШ/ЧT
   ,UJ,TAPERR
C
C...  ИЗBЛEЧEHИE HOMEPA ЗOHЫ
C     ИЗ CЛYЖEБHЫX CЛOB
C
 ПPOBCЛCЛ:,BSS,
 CW,XTA,3.KOПИЯ 1
 CW,AOX,6.KOПИЯ 2
   ,ATX,NZON
   ,AAX,B39/25
   ,AEX,CW36C.CПEЦKOД
 ER,VTM,OШ/CC
 RO,UZA,
C
C...   OБPAБOTKA OШИБOK OБMEHA C MЛ
C
 TAPERR:,BSS,
   ,XTA,C13
   ,ATI,21B
   ,XTA,BIT26
  U,ARX,T1MT-30B.CЧ-K ПO
  ER,AOX,BIT36-1
  ,ATX,WS
  U,ATX,T1MT-30B.TEK.OБMEHY
   ,APX,B30*12*5.HE ЛOПHYЛ
  R,UZA,
  ,ASN,64-1
  ,UZA,TERR1.ER ЗAДAHO
  ER,VTM,OШ/ЗП
   ,AAX,BIT48
  :,UZA,TERR1
 ER,VTM,KДB/MФ
  TERR1:,BSS,
  C,MTJ,RO
 RF,VZM,ERREC
  M,VTM,TЦKMФ
 ER,XTA,MTDIAGN-1
  M,ATX,
   ,ITA,U
   ,ASN,64-42
   ,AUX,BY1/2
   ,AOX,Z12Ш
  M,ATX,1
 JN,XTA,*YCЫ
   ,ASN,64-27
   ,AUX,BY5/6
  M,AOX,1
  M,ATX,1
  U,XTA,T2MT-30B
   ,ASN,64+13
   ,ARX,C77774
   ,AAX,C3777
   ,ATX,TИПOШ
   ,ASN,64-33
   ,AUX,BY1/5
   ,AOX,TNZON
  M,ATX,2
   ,ITA,M
  M,VJM,TTPRINT+1
  :,24 ,3
 RO,MTJ,C
  ,XTA,B36/31.ГACИM "KO","KДB"
  ,AOX,MAST1MT.И CЧ-K OШИБOK
  U,AAX,T1MT-30B
  U,ATX,T1MT-30B.ГOTOBИM CYMMATOP
  ,ASN,64+6.ДЛЯ "ПЛOXAЯ MЛ":
  ,ASN,64-12.MATHOM, БИTЫ OШИБOK,
  ,AOX,TИПOШ.HOMEP ЗOHЫ
  ,ATX,TИПOШ
   ,XTA,WS
   ,ASN,64+6
   ,AOX,WS
   ,AAX,C6
  R,UZA,
   ,AEX,BIT2
  :,U1A,*+1
   ,UTC,MЛKAЮK
 RF,VTM,.7
 RT,VTM,TИПOШ.9
  L,VTM,.11
  N,XTA,WL1-1
 RO,VTM,XOPOБM1
   ,UJ,ABCHT
 CT1:,BSS,
  ,XTA,NZON
  ,ARX,C1
  ,AAX,C7777
  ,ATX,NZON
 R,VJM,SET2MT
  ,XTA,IDMT
  ,UJ,CT2
 ERREC:,BSS,
   ,ITA,U
  R,VTM,
 :NF,VZM,*+1
  M,VJM,MФ ПЛOX
 RO,MTJ,C
   ,XTA,
 CT2:,BSS,
 JN,WTC,*YCЫ
   ,ATX,
   ,ITA,U
 JN,WTC,*YCЫ
   ,ATX,I14
   ,UJ,XOPOБM1
 *OUTJOB:,BSS,
 NF,V1M,XOPOБM1
 MF,V1M,XOPOБM1
   ,XTA,
  :,24 ,2
  R,VTM,-1777B
 :R,ATX,71777B
  R,VLM,*
  XOPOБM1:,24,3
  U,XTA,T1MT-30B
  ,ASN,64-12.OШИБKИ
  ,AUX,C200*.TEK.OБMEHA
  ,UZA,AFT ST. - HE БЫЛO
  ,ASN,64+7
  ,ATX,БYCT+1.CБPOC OШИБOK
  N,XTA,WL1-1.B ИHЖ.
   ,ATX,БYCT.CTATИCTИKY
  ,XTA,ST/T
  ,UJ,STATIST
  AFT ST:,BSS,
   ,XTA,FULWOR
  I,AEX,ПOДBOД
  I,AEX,PEBEPC
  C,AAX,CSUMSTAT-1
  C,ATX,CSUMSTAT-1
  C,XTA,SBUC-1
  N,AEX,BITS
   ,U1A,CLEAR
  C,ATX,TBUC-1
 RO,VJM,PRT DOWN
 CLEAR:,BSS,
   ,XTA,MAST1MT
  U,AAX,T1MT-30B
  U,ATX,T1MT-30B
   ,XTA,FULWOR
  U,AEX,BIT48-30B
   ,AAX,ШЧYMЛ
   ,ATX,ШЧYMЛ
  R,VJM,VOP
  RT,VTM,TRAINT.BOЗBPAT
  MF,VIM,TASKOFF.ПOДBOД
  C,WTC,TYPEMT-3
  CW,VTM,.AДPEC CЛ.CЛOB
C
C...   БЛOK OKOHЧAHИЯ ЗAKAЗA HA OБMEH
C      RT - BOЗBPAT
C      N - ИHДEKC ПO WL1 ИЛИ 0
C      CW - AДPEC CЛYЖ.CЛOB (ДЛЯ MЛ И MД)
C
C      ПPOИЗBOДИTCЯ, ECЛИ HAДO, ПEPEПИCЬ
C      CЛYЖ.CЛOB B ИПЗ ЮЗEPA.
C      OTKPЫBAETCЯ ЗAДAЧA ИЛИ EE MAT.ЛИCT
C      ПPИ HEHYЛEBOM  N ДEЛAETCЯ BЫЧИCTKA
C      ЗAKAЗA ИЗ OЧEPEДИ OБMEHOB.
C
C      ПOPTЯTCЯ: MV,R,TP,F,P,S
C
C
 KOHOБMEH:,ENTRY,.MЛ/MД
  :,24,3
  N,XTA,WL1-1
  ,ASN,64+42
  ,ATI,F. Д H З
  ,XTA,SCSWING
  N,AAX,BITS
  ,U1A,KOHOБMБ.ПOДKAЧKA
  ,XTA,ИПЗ/ECTЬ
  F,AAX,BITS
  ,UZA,KOHOБMБ.HET ИПЗ
  ,AAX,Ш/ДИCП
  ,U1A,KOHOБMБ.ДИCП.ЗAKAЗ
C
C...   ПPOБA: ECTЬ ЛИ ЗAKAЗ HA
C      ПEPECЫЛKY CЛYЖ.CЛOB ЮЗEPY
C
  F,WTC,*YCЫ
  S,VTM,CWPLAC
  S,XTA,REGSCAL-CWPLAC
  ,AAX,BIT39
  ,UZA,KOHOБMБ
  R,VTM,-7
C
C...   ПEPEПИCЬ B ИПЗ ЮЗEPA
C!!!   ECЛИ ЗAДAЧA BЫДAЛA ЗAKAЗ
C      HA ЧT.ИMEHИ, A EE ИПЗ YKAЧAЛИ
C???   BЫЙДET HEПPИЯTHOCTЬ
C
  :CW,UTC,
  R,XTA,7
  S,ATX,
  R,VLM,*-1
C
  KOHOБMБ:N,XTA,WL1-1.BXOД ДЛЯ MБ
  ,ATX,OFFKYC
C
  OPEN:,BSS,.BXOД ДЛЯ ERTREQ
   ,XTA,OFFKYC
   ,APX,KPMASK
   ,ASN,64+11
   ,ATX,MPAGE
  ,ASN,64+30
   ,ATI,P.ФИЗ.ЛИCT
C   ЧT.ИMEHИ MЛ - P=0
C   RECMDT      - P=77
   ,XTA,OFFKYC
   ,AAX,BIT38
   ,ASN,64+23
  ,ATI,TP.TИП ЗAKAЗA
 P,XTA,TPHYS
  ,ASN,64-15
  :TP,VZM,*+1.ИЗ TPHYS
  ,XTA,OFFKYC.ИЗ KYC-A
  :,ASN,64+42
  ,ATI,F.ДHЗ
C   F=0 : TP>0  RECMDT
C         TP=0  SWING, ECЛИ ЗAKPЫTA ПO RP,
C               A ЗAKOHЧEH OБMEH C NEWRP
 F,XTA,*YCЫ
  ,ATI,S.ИПЗ
 TP,V1M,OPETA
  ,AAX,B37/31
  ,AEX,MPAGE
  ,U1A,CLEXB
C...   ЗAДAЧA ЗAKPЫTA "ПO ЛИCTY"
  ,XTA,MPAGE
 F,AEX,*YCЫ
 OPETA:,BSS,
 F,ATX,*YCЫ
  ,XTA,EXCHSC.BЫЧИCTKA ИЗ
  F,AOX,BITS.ШKAЛЫ ЗAДAЧ,
  F,AEX,BITS.ЗAKPЫTЫX ПO
  ,ATX,EXCHSC.OБMEHY
 F,XTA,BITS
  ,AOX,OFFESC
  ,ATX,OFFESC
  CLEXB:,XTA,ZB7*6
  P,AAX,TPHYS
  P,ATX,TPHYS
  F,VZM,ENDOP.RECMDT,SWING
  ,APX,MMNTPL
  ,ASN,64+36
  ,ATI,MV.MAT.ЛИCT
  ,ASN,64+5
  ,U1A,ENDOP
  ,XTA,Ш/ДИCП
  F,AAX,BITS
  ,U1A,ENDOP
C   ДЛЯ ЗAKAЗA OT ЭKCTPAKOДA
C   C ЗAKPЫTИEM ЗAДAЧИ (ФИЗ.ПAKET,
C   TECT.CTATYC) HYЖHO ЧИCTИTЬ TMATH
  TP,VIM,NOP
  N,VZM,HE COБ
C
C...   ГEHEPAЦИЯ ДЛЯ ЮЗEPA
C      COБЫTИЯ: "KOHEЦ OБMEHA"
C
  S,XTA,SCEX
   ,UZA,HE COБ
 MV,AOX,BIT48
  S,ATX,SCEX
  F,XTA,*YCЫ
   ,AEX,
   ,XTA,HA COБ
   ,UJ,COБЫTИE
C
C...   OTKPЫTИE ЛИCTA ЮЗEPA
C      ПO ПPИПИCKE И ЗAЩИTE
C
 HE COБ:,BSS,
 :,24 ,3
  ,ITA,MV
 ,ASN,64+3
 ,AEX,C3
 ,ATI,R
  ,ITA,MV
 ,AAX,C7
 ,AEX,C7
  S,J+M,R
 ,ATI,TP
 TP,XTA,BIT28
 ,AEX,FULWOR
 R,AAX,TЗAЩ
 R,ATX,TЗAЩ
 ,ITA,MV
 ,ASN,64+2
 ,AEX,C7
 ,ATI,R
 ,ITA,MV
 ,AAX,C3
  S,J+M,R
 ,ATI,TP
 ,ITA,P
 ,ASN,64-41
 TP,AUX,TMPMASK
 R,AOX,TПPИП
 R,ATX,TПPИП
 NOP:,BSS,
  P,VZM,ENDOP.ЧT.ИMEHИ
  S,J+M,MV
  ,XTA,FULWOR
  ,AEX,BIT46
  MV,AAX,TMATH
  MV,ATX,TMATH
 ENDOP:,BSS,
  N,VZM,ENDMAC70
C
C...   ПOДПPOГPAMMA ИCKЛЮЧEHИЯ ЗAKAЗA
C      ИЗ OЧEPEДИ ЗAKAЗOB HA OБMEH
C
 TASKOFF:,ENTRY,
 TASKOFF:,BSS,
 LL1:,EQU,LQTRA-1
 LL2:,EQU,LQTRA-2
  ESJMT:,EQU,SCSWING
 :,24 ,2003B
  ,XTA,FULQUESC
  ,AOX,OFFESC
  ,ATX,OFFESC
  ,XTA,
  ,ATX,FULQUESC
 :,24 ,3
  ,XTA,TRACNT
  ,UZA,BADEXCH
  ,ARX,C377
  ,AAX,C377
  ,ATX,TRACNT
  ,XTA,FULWOR
 N,AEX,BITS
  ,ATX,OFFKYC
 F,VTM,SBUC-ESJMT
 OFFSC:,BSS,
 F,XTA,ESJMT
  ,APX,OFFKYC
 F,ATX,ESJMT
 F,VLM,OFFSC
 N,XTA,WL1-1
  ,ATX,OFFKYC
 N,MTJ,F
 F,UTM,-LL1
 OFFWL:,BSS,
 F,XTA,WL1+LL1
 F,ATX,WL1+LL2
 F,XTA,WL2+LL1
 F,ATX,WL2+LL2
 F,VLM,OFFWL
 RT,UJ,
C
C-----
C
C......OPГAHИЗAЦИЯ ПEPEXOДOB ЧEPEЗ MГЗ
C      ДЛЯ ЗABOДCKOГO KOHTPOЛЛEPA .....
 ДYБ:,BSS,
 14,XTA,ШKEC-3
 ,AAX,BIT47
 5,UZA,
  ,ITA,5
  14,ATX,SCMT57-3
  14,XTA,ШKEC  -3
 13,AAX,BIT24
 ,UZA,SKIPMARK
 OБM5017:,BSS,
 14,AEX,ШKEC-3
 14,ATX,ШKEC-3
  U,XTA,T2MT-30B
 ,AAX,C/MГЗ
  ,U1A,MAPKEP
 SKIPMARK:,BSS,
 14,XTA,CSUMSTAT-1
 13,AOX,ПOДBOД
 14,ATX,CSUMSTAT-1
  13,XTA,BIT24
 14,AOX,ШKEC-3
 14,ATX,ШKEC-3
 MV,VTM,1
  ,UJ,EXCHДBИЖ
  MAPKEP:,XTA,FULWOR
  13,AEX,PEBEPC
  13,AEX,ПOДBOД
  14,AAX,CSUMSTAT-1
  14,ATX,CSUMSTAT-1
C
C...   ,XTA,.CБPOC
C      ,EXT,32B.БAYCA
C
  14,XTA,SCMT57-3
  ,ATI,5
  5,UJ,
C
C---
C
 MФ ПЛOX:,ENTRY,
 MФ ПЛOX:,BSS,
C...OTPAБOTKA HEГOTOBHOCTИ MФ
C...HA CYMMATOPE N. MФ
C...И.P.5 - TИП CИTYAЦИИ
C   0 - HE OПOЗHAHA MЛ.
C   3 - HE ГOTOB MФ.
C   6 - HET ЗAПИCИ HA MФ.
 ,ATX,HOMФ
 5,VIM,MФП.HEГOTOB.
 ,XTA,MISTERX.
 ,WTC,HOMФ.
 ,AOX,BIT48-30B.
 ,ATX,MISTERX.
 MФП:,XTA,HOMФ.
 ,ASN,64-42.
 ,AUX,BY1/2.
 ,AOX,Z12.
 5,UTM,THEOПO.
 5,ATX,2.
 ,ITA,5.
 14,MTJ,5.
   :   ,24,2003B.
 14,VJM,TTPRINT.
   :   ,24,3.
 5,UJ,.
 SUMMING:,ENTRY,.
 SUMMING:,BSS,
C
C    I4 И I11 ЗAДAЮTCЯ ПPИ OБPAЩEHИИ
C
 CYMMИP:,XTA,.
 ,ATX,RRR.
 L01551:10,VTM,-31.
 ,XTA,C2003.
 ,ATI,21B.HET OCT. ПO KЧ
 ,XTA,RRR.
   :   ,24,2002B.
 11,ARX,70000B.
 11,UTM,1.
 10,VLM,*-1.
   :   ,24,2003B.
 ,ATX,RRR.
 ,XTA,C13.
 ,ATI,21B.OCTAHOB ПO KЧ
 ,ATI,.
 ,ATI,.
 4,VLM,L01551.
 ,XTA,RRR.K.CYMMA
 5,UJ,.
C
C-----  OCTAHOB ПO BCЯKИM БЯKAM
C
  BAD EXCH:,BSS,
  ,33,70707B
  ,UJ,*
C-----
 ,END,

 DISC:,NAME,
C
C......   MOДYЛИ ДИCKOBOГO ДPAЙBEPA
C          ПAPAMETPЫ CПOCOБA ПOДKЛЮЧEHИЯ
 NCHD:,EQU,2.ЧИCЛO KAHAЛOB
 NMODG:,EQU,16.ЧИCЛO MOДYЛEЙ
C         БЛOKИ, OБЩИE C ЗAДAЧEЙ OБMEHA
  *ИПД:LC,BLOCK,(4),XNW,(22),MONOS
  ,CONT,(14),OUTJOBSC
  MTRACNT:,EQU,*ИПД+27B
  LQTRA:,WEQ,MTRACNT
 RETTRA:LC,BLOCK,RECONNUM
  SCALSTRA:LC,BLOCK,(12),(32),SQTM(16),SCTMOD
  LISTSTRA:LC,BLOCK,WL1
  WL2:,EQU,WL1+LQTRA
 TRABUS:LC,BLOCK,SCTATRA
 SCDICH:,SUBP,.
 SCDICH:I,BLOCK,(1),REGW,TYPWR,TYPD
 KOWNP:,EQU,SCDICH.CBOЙ ЛИCT (KYC)
C         ЭKBИBAЛEHTHOCTИ ЧИCЛAM
 BEGMT:,EQU,30B
 SM:,EQU,3
 M14:,EQU,8
 NMAT1:,EQU,30B
C      ЗHAЧEHИЯ CЛYЖEБHЫX CЛOB.
 NAMED1:,EQU,3.ПEPBOE ИMЯ.
 NAMED2:,EQU,NAMED1+4.BTOPOE ИMЯ.
  IDEN:,EQU,1.ABTOP ЗAПИCИ
C         OБAЗHOЧEHИE ИHДEKC-PEГИCTOB.
 K:,EQU,1.HOMEP MOДYЛЯ
 KI:,EQU,2.K.EQ.7
 J8:,EQU,3.HOMEP KAHAЛA*8
 C:,EQU,4.
 R5:,EQU,5.
 W:,EQU,7.
 P:,EQU,8.TИП OБMEHA
 R9:,EQU,9.
 A:,EQU,6.N MOД.AБCOЛЮTH.
 N:,EQU,11.
 M:,EQU,12.MECTO B ГЛAB.OЧ.
 R:,EQU,13.PEГ. BOЗBPATOB
 RL:,EQU,15.ДOП. BOЗBPAT
 J:,EQU,14.HOMEP KAHAЛA
C   TИПЫ OБMEHA.
 WRITE:,EQU,0.ЗAПИCЬ
 READ:,EQU,2.ЧTEHИE
 RAZMT:,EQU,1.PAЗMETKA
 CONTR:,EQU,3.CPABHEHИE
 COWRI:,EQU,4.ЗAП.C CPABHEHИEM
 RHEA:,EQU,6.ЧTEH. ЗAГOЛOBKA
 QUREA:,EQU,7.OПPOC ГOTOBHOCTИ
 *677:B,BLOCK,BITS
 ,CONT,BIT48,BIT47,BIT46,BIT45
 ,CONT,BIT44,BIT43,BIT42,BIT41
 ,CONT,BIT40,BIT39,BIT38,BIT37
 ,CONT,BIT36,BIT35,BIT34,BIT33
 ,CONT,BIT32,BIT31,BIT30,BIT29
 ,CONT,BIT28,BIT27,BIT26,BIT25
 ,CONT,BIT24,BIT23,BIT22,BIT21
 ,CONT,BIT20,BIT19,BIT18,BIT17
 ,CONT,BIT16,BIT15,BIT14,BIT13
 ,CONT,BIT12,BIT11,BIT10,BIT 9
 ,CONT,BIT 8,BIT 7,BIT 6,BIT 5
 ,CONT,BIT 4,BIT 3,BIT 2,BIT 1
C         HAЗBAHHЫE БИTЫ
 MABT:,EQU,BIT4
 B1PCK:,EQU,BIT3
 BNEWM:,EQU,BIT11.HECOBП.AДP.
 BITDISK:,EQU,BIT46
 BERCPU:,EQU,BIT25
 BITCO:,EQU,BIT44..CЧET.OШИБOK
 BITDIAG:,EQU,BITCO-3.
 BSKAL:,EQU,BIT46.CЧET. YCT 0
 BINTR:,EQU,BIT12.БИT AB. B WL1
 BBSTOP:,EQU,BIT6.ДЛЯ MYЛЬTИTAЙПA
  MBIDEN:,EQU,BIT12.HECOBП.ИД.
 XRAZMT:,EQU,BIT1.RAZMT B MOЗY
 XREAD:,EQU,BIT2.READ B MOЗY
 BSECT:,EQU,BIT7.CEKTOPHЫЙ OБMEH
  BMONCH:,EQU,BIT39.ПP.ЗAK. C MOHOП.
C         БИTЫ CTATYCA B CUORD
 BBLTT:,EQU,BIT4.БЛOKИPOBKA TT
 BTMOD:,EQU,BIT5.HAПPABЛEH HA MOД
 B1O:,EQU,BIT6.OДHOKPATHOCTЬ
 BNOTST:,EQU,BIT38.ПEPEXBAT OTKAЗA
C         БИTЫ CTATYCA B WL1
 BCLOSE:,EQU,BIT38.БЛOK.ЗAKAЗЧИKA
 BTMODE:,EQU,BIT5.HAПPABЛEH HA MOД
C         KOHCTAHTЫ B KOMMOHAX
 C3:,LC,1
 C7:,LC,1
 C377:,LC,1
 C7777:,LC,1
 B24/1:,LC,1
 FULWOR:,LC,1
 C13:,LC,1
  C37:,LC,1
 C2003:,LC,1
 DISREG:,LC,1
 B48/31:,LC,1
  B48/47:,LC,1
 C1400:,LC,1
 C14:,LC,1
 *CB:LC,BLOCK,
 ,CONT,CDIAG(0),B48/44
 ,CONT,B4/1,ZPAG(0),Z25/7
 ,CONT,(1),B48/13,B13/2
C         CПEЦ.HAЗBAHИЯ KOHCTAHT B KOMMOHAX
 XRAEQRE:,EQU,C3.XRAZMT.EQ.XREAD
 XQREA:,EQU,C7
C         CEMAФOPHЫE OПEPAЦИИ БЫCTPЫX KПДП
 CHPOP:,SUBP,
 CHVOP:,SUBP,
C         CEMAФOPHЫE OПEPAЦИИ ЛИCTA
  PS*POP:,SUBP,
  PS*VOP:,SUBP,
C         T A Й M E P Ы
  TIMERS:LC,BLOCK,(2),TIMEX(2)
 MAX TIME:,EQU,BIT9.ПPИMEPHO 1.8 C.
C         BXOДЫ B TRAM70
 KOHOБMEH:,SUBP,
 ENDMAC70:,SUBP,
 PRIPTRA:,SUBP,
 SUMMING:,SUBP,
 TASKOFF:,SUBP,
 S48TO12:,SUBP,
 TRAINT :,SUBP,
  GIVTRA:,SUBP,
  AFGVTR:,SUBP,
  BC1700:,SUBP,
C         БЛOKИ CBЯЗИ C KAHAЛOM OПOЗHABAHИЯ
 ENSPTR:,SUBP,
 CMDREC:LC,BLOCK,DREADY(0),SCKILD
C         ПPOЧИE ДИCПETЧEPCKИE БЛOKИ
  *SWCMN:LC,BLOCK,(3),EXCHSC
  DISPBASE:LC,BLOCK,(6),NEVM.B 48-47 P.
 ABCHT:,SUBP,.П/П AB.CHЯTИЯ
 RETURN:,SUBP,
 STATIST:,SUBP,
 CLOMIR:,SUBP,
 PERMIR:,SUBP,
 TTPRINT:,SUBP,
 ERRMACRO:,SUBP,
  DECLET:,SUBP,
  *ПДИCK:LC,BLOCK,PRDK
C---- TEKCTЫ ДИAГHOCTИK HA AЦПY :
  *24:,BLOCK,DERT,TASD,TWBP
 *YCЫ:LC,BLOCK,ГYC,RDSCAL
  SMASAV:LC,BLOCK,SAVAC
 *DISCS:LC,BLOCK,DNAMIN(16)
 *STATBUF:LC,BLOCK,(14),COUNTTR
C         OПИCAHИE ПPИЗHAKA ПP.AND..NOT.ПЗ
 REGSCAL:,EQU,100B
 BLMUL:,EQU,BIT6
C......   COБCTBEHHЫE БЛOKИ ДИCKOB
 *DSCRFI:LC,BLOCK,YINT,YINT1,Y1,Y,Y5
 ,CONT,QRTR(2) .OЧ.ГOT.OБMEHOB
  ,CONT,FLMNCH(2)
  CUORD:,EQU,*DISCS+NMODG
C
C----------------------------------
C
C   +++   OПИCAHИE CTPYKTKP ЯЧEEK:
C
C......   01   WL1 ИЗ COMMON LISTSTRA
C         ПPEДCTABЛЯET ЗAKAЗ(KYC) HA OБMEH
C         B ГЛABHOЙ OЧEPEДИ ЗAДAЧИ OБMEHOB.
C         48-43P - ДHЗ ЗAДAЧИ, ДABШEЙ ЗAKAЗ
C         42-40P - HOMEP HAПPABЛEHИЯ.
C       39 PAЗP - ПPИЗH.CBЯЗИ C MOHOПOЛИEЙ
C         38P    - ПPИЗHAK ЗAKAЗA OT TRAPHY.
C         34-26P - HOMEP ДOPOЖKИ + 8
C         25-24P - HOMEP CEГMEHTA MOЗY.
C         19P   -ПPИЗHAK ЛИCTOBOГO OБMEHA.
C         18P И 3-1P - KOД TИПA OБMEHA.
C         17-13P - HOMEP ЛИCTA ФИЗИЧECKИЙ.
C         10- 8P - HOMEP MOДYЛЯ.
C         6P - ПPИЗHAK ПEPEXBATA PEAKЦИИ
C                  HA OШИБKИ.
C
C......   02 CUORD
C         ПPEДCTABЛЯET TEKYЩEE COCTOЯHИE
C                  OБMEHA HA MOДYЛE.
C
C         48-44P - CЧETЧИK OШ. OБMEHOB.
C              1KAЛИБP.=4OШ.OБM.
C         38P   - ФЛAГ ПEPEXBATA PEAKЦИИ.
C         37-26P - TEKYЩИЙ HOMEP ДOPOЖKИ.
C         25-24P - HOMEP CEГMEHTA MOЗY.
C         19P - ПPИЗHAK ЛИCTOBOГO OБMEHA.
C         18P - ПPИЗHAK CЧИTЫBAHИЯ YBY.
C         17-13P - HOMEP ЛИCTA ФИЗИЧECKИЙ.
C         6P - ПPИЗHAK ПEPEXBATA PEAKЦИИ.
C         10-8P - TEKYЩИЙ ИДEHTИФИKATOP.
C         3-1P - TИП OБMEHA(ЧTEHИE,ЗAП....)
C
C----------------------------------
C
C++++++++++++++++++++++++++++++++++
C    OБPAБOTKA ДИCKOBЫX ПPEPЫBAHИЙ
C    BEPCИЯ OT 30'06'73
C++++++++++++++++++++++++++++++++++
C         ПOЛYЧEHИE HOMEPA KAHAЛA * 8
 ,ITA,J
 ,ASN,64-3
 ,ATI,J8
 ,NTR,7
   :   ,24,3
C         ПPOБA, CBOЙ ЛИ KMД
 J,XTA,KMD OWN-SM.
 ,UZA,GET KMD.БЫЛ ЧYЖOЙ
C         ПPEPЫBAHИE HE ПO ПPИBЯЗKE
C          HE БЫЛO ЛИ ACД:
 J,XTA,FLASD-SM
 ,U1A,MINT.HE БЫЛO
C          HET KOHЦA OБMEHA ИЛИ
C          ПOДBOДA, CЛEД. ИMEEM ACД.
 J,XTA,CNMOD-SM.N TEKYЩEГO
 ,ATI,K.MOДYЛЯ B ПPЯMOM
 ,AEX,C7.И OБPATHOM
 ,ATI,KI.KOДAX
C         YCTAHOBKA A(AБCOЛЮTHЫЙ HOMEP)
 RL,VJM,YCTA
C         CБPOC ФЛAГA ASD(FLASD.NE.0)
 ,XTA,BIT40
 J,ATX,FLASD-SM
 KI,XTA,BIT8
   ,AEX,FULWOR
  J,AAX,CODM-SM
  J,ATX,CODM-SM
C          BЫБPOC И OTKPЫTИE ЗAДAЧИ -
C          - XOЗЯЙKA ЗAKAЗA C ACД.
 W,VTM,T ASD+1
C       HA БЛOK ПPOBEPKИ CTATYCA.
C       ЗAKAЗ ЛИБO БYДET CHЯT KAK
C       ABAPИЙHЫЙ, ЛИБO ПEPEBEДEH
C       B COCTOЯHИE HEГOTOBHOCTИ.
 ,UJ,ISTHISAC.HE KAЮK ЛИ
 MINT:,BSS,.(4)
C......   OЬPAБOTKA ПPEPЫBAHИЙ ПOДBOДA
 J,XTA,DINT-SM
 ,UZA,TINT.HA OБP.OБMEHA
C         YCTAHOBK.ПAPAMETPOB MOДYЛЯ(K,KI,A)
 RL,VJM,YCTAPE
C         BЫЧEPKИBAHИE БИTA ИЗ DINT.
 KI,XTA,BIT8
 J,AEX,DINT-SM
 J,ATX,DINT-SM
 RL,VTM,MINT.HA CЛEД.MOДYЛЬ
 SETRT:,BSS,
C......   ПOДПPOГPAMMA. BOЗBPAT - RL.
C         ЗAHECEHИE B OЧEPEДЬ
C         ГOTOBЫX OБMEHOB
 ,ITA,K
 J,ASX,QRTR-SM.CДBИГ HA KOHEЦ
 J,AOX,QRTR-SM.ЗAHECEHИE
 ,E+N,64-3.ИЗMEHEHИE KOHЦA
 J,ATX,QRTR-SM.
 RL,UJ,
 TINT:,BSS,.(6)
C         BCE ПOДBOДЫ OБPAБOTAHЫ
 J,XTA,DBUC-SM.БЫЛ ЛИ OБMEH
  ,UZA,IFLTWSC
C......   OБPAБOTKA OБMEHA
C         OПPEДEЛEHИE HOMEPA MOДYЛЯ
 J,XTA,QRTR-SM.OЧ.ГOT.OБMEHOB
 ,AAX,C7
 ,ATI,K.N MOДYЛЯ
 ,AEX,C7
 ,ATI,KI.B OБPATHOM KOДE
C         YCTAHOBKA A (A=J*8+K-30B)
 RL,VJM,YCTA
  R5,VTM,8.ПPABKA "KЧ"
  R,VJM,YCTBEPCB.B CЛ.CЛOBAX
C         YCTAHOBKA TИПA OБMEHA B P.
 J,XTA,DBUC-SM
 ,AAX,C7.
 ,ATI,P.
C         A HE KOHEЦ ЛИ ЭTO KAЛИБPOBKИ:
 J,XTA,SKAL-SM
 ,U1A,AFTKAL.ДA
C         ДЛЯ БOЛЬШИX ДИCKOB HYЖHO OCBOБO-
C         ДИTЬ PECYPC БЫCTPЫX KПДП.
 J,ARX,TYPD-SM
 ,U1A,YCTW0.MAЛЫE ДИCKИ
 R,VJM,CHVOP.V-OПEPAЦИЯ
 YCTW0:,BSS,
C         YCT.ПPИЗH.ЗAKAЗ ПPOШEЛ(W=0)
 W,VTM,
C......   AHAЛИЗ ПPOШEДШEГO OБMEHA
 J,XTA,COPC1-SM.KOПИЯ PC 1.
 ,ATX,Y1
 P,MTJ,R
C....HE ЧTEHИE ЛИ ЗAГOЛOBKA
 R,UTM,-RHEA
 R,VZM,TRHEA
C         ПPOBEPKA HE CPABHEHИE ЛИ.
 R,UTM,RHEA-CONTR
 R,VZM,CONTROL.БЫЛO CPABHEHИE
C         ПPOБA HA 'OБMEH HE ИДET B KMД'.
 P,UTM,-READ
 ,XTA,MABT.MACKA ABAPИИ
 P,V1M,*3.HE ЧTEHИE
 ,AOX,B1PCK.ДЛЯ ЧTEHИЯ
 *3:,AAX,Y1
 ,U1A,ERRT.ABAPИЯ
C         ПPOBEPKA HE PAЗMETKA ЛИ.
 R,UTM,CONTR-RAZMT.
 R,VZM,OPENT.PAЗMETKA
C         ПPOБA OШM
 ,EXT,4035B
 J,AAX,BIT8
 ,U1A,ERR PROG.ECTЬ OШM
C         ПPOГPAMMHЫЙ KOHTPOЛЬ
C
 R9,VTM,-8.ЦИKЛ HA 2 ШAГ 4
C      ПOЛYЧEHИE AДPECA.
 R,VTM,YPY.YCT.PEAKЦ. YПP
 CRADR:,BSS,.
C......   ПOДПPOГPAMMA ПOЛYЧEHИЯ
C      ИHДEKCA ЗAKAЗA B MACCИBE
C      ГЛABHOЙ OЧEPEДИ B M И
C      HAЧAЛЬHOГO AДPECA B Y1.
C         ПOPTИT RL.
 RL,VJM,PRIRIND
 ,ATI,M.OЧEPEДИ+1
 M,XTA,WL1-1
 ,ASN,64+24
 ,AAX,B13/2
 ,ATX,Y1
 R,UJ,
 YPY:,BSS,.
C         YCT.CTPEЛKИ KC HA CBOЙ ЛИCT(MAT.)
 N,VTM,66000B-70000B
C      YCT.PEAKЦИИ HA YПP ПPИ K.C.
 R,VTM,CONTNUM
 ,ITA,R
 ,ATX,RECONNUM
C         YCTAHOBKA ПPИПИCKИ
 A,XTA,CUORD
 R5,VJM,PRIPTRA
C         ПPOБA: HE CEKTOPHЫЙ ЛИ OБMEH.
C         ПPИ HEM ЧTEHИE ЦЫЛO HA CBOЙ ЛИCT
 J,XTA,DBUC-SM
 ,AAX,BSECT
 ,U1A,LTKC.CEKTOPHЫЙ
C         HE CEKTOPHЫЙ, CTPEЛKY KC HA
C         70000B - MAT.AДP., ДABAEMЫЙ
C         ЛИCTY, HA KOTOPЫЙ БЫЛ OБMEH.
 N,VTM,
 LTKC:,BSS,
C......   ЦИKЛ ПPOBEPKИ KC И AДPECOB
 W,VTM,KEERA-KEERYBY
C         ПEPEHOC CЛYЖ.CЛOBA B PAБ.ЯЧ. Y3
 J8,UTC,
 R9,XTA,8.CЛYЖ.CЛOBO 0
 ,ATX,Y3
C         ПPOBEPKA AДPECA ДOPOЖKИ
 ,ASN,64+24
 ,AEX,Y1
C .+++++++++++++++++++++++++
C ,33,5
C ,U1A,ERR PROG1.AДPEC HE TOT
 ,XTA,Y1.AДPEC
 ,ARX,BIT1.2-OЙ ДOPOЖKИ
 ,ATX,Y1.ПAPЫ
C         ПPOBEPKA KC
 W,VTM,KEERC-KEERYBY
 C,VTM,-7.CYMMA ПO AБЗAЦAM
  M,XTA,WL2-1
  R9,UTM,4
  :R9,VZM,*+1
  ,ASN,64+24
  :P,VIM,KS/WR.ПPИ ЗAПИCИ
 R5,VJM,SUMMING.48PKC
 R,VJM,S48TO12.12PKC
 ,ASN,64-12
 ,ATX,Y
 C,VTM,-7
 R5,VJM,SUMMING.
 R,VJM,S48TO12
 ,AOX,Y
  KS/WR:,BSS,
 ,AEX,Y3.CЛYЖ CЛOBA
 ,AAX,B24/1
  ,xta, . +++++++++++++++++++
C ,33, 2
C  ,U1A,ERR PROG1.CYMMA HE TA
 R9,V1M,LTKC
  P,VIM,ENDWR.ПPИ ЗAПИCИ
C         YCT.ПPИЗH.ЗAKAЗ ПPOШEЛ
 W,VTM,
C      ПPOБA: HE HAДO ЛИ ИMЯ ПAKETA.
 A,XTA,CUORD
 ,AAX,BTMOD
 ,UZA,ENREAD.HE HAДO
C      YCTAHOBKA CИГHAЛA 'ИMЯ' (W=1).
 W,VTM,1.
C      ЗAПИCЬ ПOKA 0 KAK ИMEHИ.
 ,XTA,
 ,ATX,Y.ИMЯ.
C      ПPOБA HA TOЖДECTBEHHOCTЬ ИMEH.
 J8,XTA,NAMED1
 J8,AEX,NAMED2
C ,33,21  .+++++++++++++++++++++++++
 ,U1A,OPENT.HET, ИMЯ=0.
C      ЗAHECEHИE ИMEHИ B Y.
 J8,XTA,NAMED1
 ,ATX,Y
 ENREAD:,BSS,
C......   ЧTEHИE ПPOШЛO XOPOШO. ECЛИ И
C         TOЛЬKO ECЛИ OHO БЫЛO ДЛЯ CEKTOP-
C         HOГO OБMEHA, TO (N.EQ.0)
 N,V1M,OPENT.HE CEKTOPHЫЙ
C         ПPOБA: HE ЗAKAЗ ЛИ ЭTO ЧTEHИЯ
C         ИMEHИ KAHAЛOM OПOЗHABAHИЯ
 M,XTA,WL1-1
 ,ASN,64-6
 ,YTA,
 ,UZA,OPENT.KAHAЛ OП.
C         БЫЛ CEKTOPHЫЙ. ПOДГOTOBKA
C         ПEPEПИCИ ДЛЯ CEKTOPH. ЧTEHИЯ.
C         (N.EQ.0) - ПPИЗH. CEKTOP.ЧTEHИЯ
 A,XTA,CUORD
 ,ASN,64+11
 ,ATX,Y3
 ,AAX,C1400.OTH.AДP.CEKTOPA
 ,ATI,P
 P,UTM,66000B.AДPEC CEKTOPA
 ,XTA,Y3
 ,ASN,64+2
 ,AAX,C1400.OTN.AДP.AБЗAЦA
 ,ATI,RL
 RL,UTM,70000B.AДPEC AБЗAЦA
C         A HE CEKTOPHAЯ ЛИ ЗAПИCЬ:
 A,XTA,CUORD
 ,AAX,BIT18
   :   ,24,2.HA ПPИПИCKY
  R9,VTM,1-400B.CЧETЧИK ЦИKЛA
 ,U1A,LMSEC.ЧTEHИE
C         ЗAMEHA YKAЗATEOEЙ ПEPEПИCИ,ИБO ЗAП
 RL,MTJ,N.ПPИ ЭTOM ПPИЗH.N
 P,MTJ,RL.MEHЯET ЗHAЧEHИE
 N,MTJ,P.HA HE 0 - ЗAПИCЬ
 LMSEC:,BSS,
C         ЦИKЛ ПEPEПИCИ CEKTOPA
 P,UTC,
 R9,XTA,377B
 RL,ATX,.RL:,EQU,15
 R9,VLM,LMSEC
   :   ,24,3
C         ПPИ CEK.ЧTEHИИ - KOHEЦ ЗAKAЗA
 N,VZM,OPENT.ЧTEHИE
C......   OPГAHИZAЦИЯ ZAПИCИ ЗOHЫ C
C         HOBЫM ЗHAЧEHИEM OДHOГO CEKTOPA.
C
C   ...   ФOPMИPOBKA B WL2 KC CEKTOPOB C
C         YЧETOM HOBOГO CEKTOPA.
  A,XTA,CUORD
 ,ASN,64+17.ПOЛYЧEHИE
  ,AAX,C14.N CEKTOPA*12
 ,ATX,Y3.ДЛЯ CДBИГA KC
 ,ASN,64-1.HA CBOE
  ,ARX,Y3.MECTO B WL2
 ,ATI,R9
 M,XTA,WL2-1.ПOЛYЧEHИE ИHBEP-
 ,AEX,C7777.TИPOBAHHOЙ KC
 R9,ASN,64-36.HOBOГO CEKTOPA
     M,ATX,WL2-1.HA CBOEM MECTE
C         ПOЛYЧEHИE CTAPOГO ЗHAЧEHИЯ WL2.
 J8,XTA,4.KC2 И KC3
 ,AAX,B24/1
 J8,ATX,4
 J8,XTA,.KC0 ИKC1
 ,ASN,64-24
 J8,AOX,4
 J8,ATX,4.ЗHAЧEHИE WL2
C         ПOЛYЧEH8E HOBOГO ЗHAЧEHИЯ WL2
 ,XTA,C7777
 R9,ASN,64-36
 J8,AOX,4
 M,AEX,WL2-1
 M,ATX,WL2-1
C         ФOPMИPOBKA B CUORD ЗAKAЗA HA
C         ЗAПИCЬ (ЗAДAHHOГO TИПA) ЗOHЫ.
 A,XTA,CUORD.N ДOPOЖKИ, KOП
 ,AAX,ZPAG.И CПEЦ.БИTЫ
 ,AOX,KOWNP.CBOЙ ЛИCT И Б19
 A,ATX,CUORD
  ,UJ,IFLTWSC.HA ЗAПYCK ЗAП.
 AFTKAL:,BSS,
C   ...   ЗAKOHЧИЛACЬ YCTAHOBKA HA 0.
 ,ATX,Y1.KOПИЯ PC
 ,XTA,.ЧИCTKA
 J,ATX,SKAL-SM.ФЛAГA YCT.0
 ,UJ,TESCOBT
 CONTNUM:,BSS,
C   ...   PEAKЦИЯ HA YПP ПPИ KOHT.CYMM.
C
C         CHЯTИE БЛOKИPOBKИ OCTAHOBA ПO YПP.
  R,VTM,ERR PROG
  ,UJ,CHCTY
C---
  ERR PROG1:P,VZM,ERR PROG.ЧTEHИE
  W,VTM,KERSS-KEERYBY
 ERR PROG:,BSS,
C   ...   OШИБKA, OБHAPYЖEHHAЯ CPU(ЦП),
C         T.E. OШM ИЛИ ПPOГPAMMHЫЙ KOHTPOЛЬ
 W,XTA,BERCPU.YCTAHOBKA
 ,ATX,Y1.PACШИPEHHOГO PC
C       B CЛYЧAE HE OШM(W.NE.0) ДAДИM
C       HA BЫXOД AДPEC ИЗ CЛYЖ.CЛOBA
  W,VZM,TOTTPRG.OШM
  J8,XTA,
  ,ASN,64+24.BЫДEЛИЛИ
  ,ASN,64-30.AДPEC
  ,AOX,Y1
  ,ATX,Y1
 TOTTPRG:W,UTM,KEERYBY
 R,VJM,TTPRCO
 TESCOBT:,BSS,
C   ...   ПPOBEPKA YCЛOBИЙ CHЯTИЯ
C         ЗAKAЗA ПO ПPИЧИHE OШИБOK
 W,VTM,DERT+1.AЦПY ДИAГH.
 A,XTA,CUORD.
 ,ARX,.KAK CЧETЧИK OШ.
 ,U1A,NOTENDCO.EЩE HE CДOX
 ISTHISAC:,BSS,
C         ECTЬ ЛИ ПEPEXBAT PEAKЦИИ:
C         (ECЛИ ДA, TO ЗAKAЗ CHИMAETCЯ)
 A,XTA,CUORD
 ,AAX,BNOTST
 ,U1A,OPENT.ECTЬ ПEPEXBAT
C         OM-HMД ДOЛЖEH БЫTЬ ПEPEBEДEH
C         B COCTOЯHИE 'HE ГOTOB'.
 RL,VJM,MOQTR.ДOЛOЙ ИЗ OЧ.
 R,VTM,ST1.ПOCЛE ДИAГH
 W,VTM,KERCMO.TT - TEKCT
 ,UJ,TTPRWBL.HA ДИAГH.
 NOTENDCO:,BSS,
C         HET ЛИ ПPИЗHAKA OДHOKPATHOCTИ:
 ,AAX,B1O
 ,UZA,IFLTWSC.ПOBTOPИM OБMEH
C         CHЯTИE ЗAKAЗA BBИДY OШИБOK
 OPENT:,BSS,.
C......   KOHEЦ OБPAБOTKИ OБMEHA,
C         KOHEЦ BCEГO ЗAKAЗA.
C
C       YДAЛEHИE ИЗ OЧEPEДИ HA OБMEH
  RL,VJM,MOQTR
C       YДAЛEHИE ИЗ OЧEPEДИ HA HMД
C       T.E. ИЗ CИCTEMЫ B ЦEЛOM
  R,VTM,IFLTWSC
  ,UJ,MOVEQUE
 MOQTR:,BSS,.
C......   ПOДПPOГPAMMA. BOЗBPAT - RL.
C         ПPOДBИЖKA QRTR(J) -
C         OЧEPEДИ ГOTOBЫX OБMEHOB.
C           ПOPTИT PEГИCP R.
C
C         CHЯTИE ФЛAГA OБMEHA
  J,XTA,DBUC-SM
 RL,UZA,
 ,XTA,.
 J,ATX,DBUC-SM.
 J,XTA,QRTR-SM.
 ,AAX,B48/31.ПOЛYЧEHИE
 ,E+N,64+3.HOBOГO KOHЦA OЧ
 J,A+X,QRTR-SM.ПPOДBИЖKA
 J,ATX,QRTR-SM.
C   ...   V-OПEPAЦИЯ ДOCTYПA K CBOEMY ЛИCTY
  J,XTA,BITDISK-SM
  RL,MTJ,R5
  ,UJ,PS*VOP
 ENDWR:,BSS,.
C         KOHEЦ KOHTPOЛЯ ЗAПИCИ.
C         ECЛИ TPEБYETCЯ CPABHEHИE,
C         TO ЗAKAЗЫBAETCЯ HOBЫЙ OБMEH.
  W,VTM,.ЗAKAЗ ПPOШEЛ
  J8,XTA,NAMED1
  J8,AEX,NAMED2
  ,U1A,ERR PROG1
 P,UTM,READ
 P,VZM,OPENT.ПPOCTAЯ ЗAПИCЬ
C         YCTAHOBKA ЗAKAЗA HA CPABHEHИE.
 A,XTA,CUORD.ЗAMEHA
 ,AEX,C7.ЗAПИCИ
 A,ATX,CUORD.HA CPABHEHИE
  ,UJ,IFLTWSC
 TRHEA:,BSS,
C......   OБPAБOTKA ПPOЧИTAHHOГO ЗAГOЛOBKA
C---------
C   ...   YЧET ГPEБEHKИ BO 2-OM CЛOBE,
C         PEЗYЛЬTAT B Y4.
 R5,VTM,-45
 ,XTA,
 ,ATX,Y4.PEЗYЛЬTAT
 J8,XTA,2.CЛYЖ.CЛOBA
 LUHE:,BSS,
C         ЦИKЛ BЫБOPKИ CЛOГOB.
 ,APX,MCAM.MACKA ГPEБEHKИ
 R5,UTM,9
 R5,ASN,64+36
 ,AOX,Y4.HOBЫЙ CЛOГ
 ,ATX,Y4.B PEЗYЛЬTAT
C         ПPOДBИЖKA ЗA OЧEPEДHЫM CЛOГOM.
 J8,XTA,2
 ,ASN,64-1
 J8,ATX,2
 R5,V1M,LUHE
C   ...   ЗAHECEHИE AДPECA PAБOTЫ ПOCЛE
C         ИЗЬЯTИЯ ЗAKAЗA ИЗ OЧ.ГOT.OБM.
 RL,VTM,IFLTWSC.BOЗBPAT ИЗ MQTR
C   ...   AHAЛИЗ ПEPEПAKOBAHHOГO ЗAГOЛOBKA.
C         ИЗYЧEHИE ИДEHTИHИKATOPA.
 ,XTA,Y4
 ,ASN,64-3
 ,ATX,Y4
 ,YTA,.ИД.+1 БИT AM
 ,AEX,BIT3.ПPOБA HA 0
 ,33,3 .++++++++++++++++
 ,UZA,NEWARH.0,HOBЫЙ OБM.
 ,AEX,BIT1.ПPOБA HA 1
 ,33,4  .++++++++++++++++++
 ,U1A,NOT1.ИДEHT.HE 1
C   ...   HOPMИPOBKA HOBOГO  AДPECA.
 ,XTA,Y4
 ,ASN,64-8
 ,ATX,Y4
 ,YTA,.ЦИЛИHДP
C         YMHOЖEHИE ЦИЛИHДPA HA 10.
 ,ATX,Y5
 ,ASN,64-2
 ,ARX,Y5
 ,ASN,64-1
 ,ATX,Y5.N ЦИЛ.*10
C         ДOHOPMИPOBAHИE N ДOPOЖKИ + 8.
 ,XTA,Y4
 ,ASN,64-4
 ,YTA,.N ДOPOЖKИ
 ,ARX,Y5.ПOЛHЫЙ AДPEC
 SNEWAD: ,ATX,Y5
C         YCTAHOBKA HOBOГO AДPECA
C         HA MECTO CTAPOГO B CUORD.
 A,XTA,CUORD
 ,ASN,64+25
 ,AAX,C7777.CTAPЫJ AДPEC
 ,AEX,Y5
 ,ASN,64-25
 A,AEX,CUORD
 A,ATX,CUORD
 NEWARH:,BSS,
C         ЗAHECEHИE B MWSC HOBOГO ПOДBOДA
 KI,XTA,BIT8
 J,AOX,MWSC-SM
 J,ATX,MWSC-SM
C         CHЯTИE OБMEHA И HA ПOДBOД.
 ,UJ,MOQTR
 NOT1:,BSS,
C      ECЛИ ИД = 3, TO CДEЛAEM PAЦИOHAЛЬHYЮ
C      ЗAMEHY, ИHAЧE ДOPOЖKA ДPЯHЬ.
 W,VTM,TWBP+1
 ,AEX,BIT2.
 ,U1A,ISTHISAC.CTOП ЗAДAЧИ
C         ЗAMEHИM ДOPOЖKY ПO HOBOMY
 ,XTA,Y4
 ,ASN,64+36
 ,UJ,SNEWAD
 CONTROL:,BSS,.
C         OKOHЧИЛOCЬ CPABHEHИE.
 J,XTA,COPC1-SM
 ,AAX,MCOMP.MACKA CPABH.
 ,UZA,OPENT.XOPOШO ПPOШЛO
C         BOCCTAHOBЛEHИE ЗAKAЗA HA ЗAПИCЬ
C         ПPИ ПЛOXOM CPABHEHИИ.
 A,XTA,CUORD
 ,AEX,C7.
 A,ATX,CUORD
 ERRT:,BSS,.
C         OШИБKA OБHAPYЖEHA KMД
 J,XTA,COPC1-SM
C         ECTЬ ЛИ ГOTOBHOCTЬ, HET - CTOП
  ,AEX,CREADY
 ,AAX,CREADY
 R,VTM,TESCOBT.ПOCЛE TTPRCO
 W,VTM,KER  KMD.TEKCT HA TT
 RL,VTM,STOP
C ++++++++++++++++++++++!
C ,U1A,MOQTR.HET ГOTOBH.
C         ПPOBEPKA HECOBПAДEHИЯ AДPECOB.
C         ECЛИ HET - ПOBTOP OБMEHA.
 J,XTA,COPC1-SM
 ,AAX,MNEWM.CПA + POФ
 ,AEX,BNEWM.CПA
 ,U1A,TTPRCO
C         OPГAHИЗAЦИЯ KAЛИБPOBKИ
C         ПOCЛE ПЛOXOГO OБMEHA
C       BЫДAЧA CTATИCTИKИ
  W,VTM,KERBM
  RL,VJM,GOSTA
C       ПPOДBИЖKA CЧETЧИKA KAЛИБPOBOK
  A,XTA,CUORD
  ,ARX,BSKAL
  A,ATX,CUORD
C       BЫДAЧA ДИAГHOCTИKИ HA TT
  R,VJM,TTPR
C       YCTAHOBKA ФЛAГA KAЛИБPOBKИ
  ,XTA,Y1.ЗAПOMИHAHИE PC
  J,ATX,SKAL-SM.B ФЛAГE YCT.0
 LTWSC:,BSS,.
C......   ЦИKЛ BKЛЮЧEHИЯ ЖДYЩИX
C         ЗAKAЗOB HA ПOДBOД,
C         ПPOCTABЛEHHЫX B TWSC
 J,XTA,MWSC-SM
 ,UZA,NOMOVE.БOЛЬШE HET
C         YCTAHOBKA HE 0 B Y, ЧTO CYЩECT-
C         BEHHO ПPИ OПPOCE ГOTOBHOCTИ.
 ,ATX,Y
C         YCT.ПAPAMETPOB MOДYЛЯ(K,KI,A)
 RL,VJM,YCTAPE
C         YCT.B PEГИCTPE P TИПA ЗAKAЗA.
 A,XTA,CUORD
 ,AAX,C7
 ,ATI,P
C         BЫЧEPKИBAHИE ИЗ TWSC
  KI,XTA,BIT8
 ,AEX,FULWOR
 J,AAX,MWSC-SM.
 J,ATX,MWSC-SM.
C         ПPOBEPKA ГOTOBHOCTИ MOДYЛЯ.
 R,VJM,READPC2
 ,AEX,CREADY.
 ,AAX,CREADY.
 ,xta, .+++++++++++++++++++++
C         ПPИ ГOTOBHOCTИ CYMMATOP - 0.
 R,VTM,LTWSC.ПOCЛE ДEЙCTBИЯ
 ,U1A,STOP.MOД.HE ГOTOB
C         MOДYЛЬ ГOTOB.
C         ПPИ OTCYTCTBИИ COBMEЩEHИЯ
C         ЗAKAЗ ИДET B OЧ.ГOT.OБMEHOB
C         ПPИ ЗAKAЗE HA OПPOC ГOTOBHOCTИ
C         ЗAДAЧA-XOЗЯЙKA OTKPЫBAETCЯ.
C
C         YCT.ПPИЗH.ЗAKAЗ ПPOШEЛ.
 W,VTM,1
C         ПPOБA: HE OПPOC ЛИ ГOTOBHOCTИ
 P,UTM,-QUREA
 P,VZM,MOVEQUE.ДA
C         ЗAKAЗ - HE OПPOC ГOTOBHOCTИ
 RL,VTM,LTWSC
C         ECTЬ ЛИ COBMEЩEHИЯ:
 ,XTA,REGW
 ,U1A,SETRT.HET COBMEЩEHИЙ
C         ЗAHECEHИE БИTA B KOПИЮ MACKИ.
 KI,XTA,BIT8
 J,AOX,CODM-SM.KOПИЯ MACKИ
 J,ATX,CODM-SM
C         BЫДAЧA ПOДBOДA И HA CЛEД.MOДYЛЬ.
 ,UJ,MOVE
C
C.....   PAЗЛИЧHЫE BAPИAHTЫ ПOBEДEHИЯ
C        B CЛYЧAЯX  OДHO-  И  MHOГO-
C        MAШИHHOГO KMД.
  IFLTWSC:,XTA,NEVM.HOMEP ЭBM
  ,AAX,B48/47.ПOДKЛ.K KMД
  ,UZA,LTWSC.- OДHA MAШИHA
 NOMOVE:,BSS,.(10)
C         OKOHЧATEЛЬHAЯ PAБOTA
C
C         ECTЬ ЛИ ГOTOBЫЙ OБMEH
 J,XTA,QRTR-SM
 ,ARX,
 ,UZA,SEXIT.HET OБMEHA
C         OБMEH ECTЬ,OПPEДEЛEHИE
C         ПAPAMETPOB MOДYЛЯ(K,KI,A)
 ,AAX,C7.
 ,ATI,K.HOMEP MOДYЛЯ
 J,ATX,CNMOD-SM.
 ,AEX,C7
 ,ATI,KI
 RL,VJM,YCTA
C         A HET ЛИ ПO ЭTOMY ЛБMEHY ЗAЯBKИ
C         HA YCTAHOBKY 0-ГO ЦИЛИHДPA.
 J,XTA,SKAL-SM
 R,VTM,ENDDSDM.ПOCЛE MOVE0
 ,U1A,MOVE0.ECTЬ
C         ДЛЯ БOЛЬШИX ДИCKOB HYЖHO ЗAXBATИTЬ
C         PECYPC БЫCTPЫX KПДП.
 J,ARX,TYPD-SM
 ,U1A,IFSECTR.MAЛЫE ДИCKИ
 R,VJM,CHPOP.P-OПEPAЦИЯ
 15,V1M,END DISC.CEMAФOP ЗAKPЫT
 IFSECTR:,BSS,
C         HE ECTЬ ЛИ OБMEH CEKTOPHЫЙ.
C         TOГДA HYЖEH CBOЙ ЛИCT.
 A,XTA,CUORD
 ,AAX,BSECT
 ,UZA,COMTRA.HE CEKTOPHЫЙ
C   ...   P-OПEPAЦИЯ ДOCTYПA K CBOEMY ЛИCTY
  J,XTA,BITDISK-SM
  R5,VJM,PS*POP
  N,VIM,END DISC.ЛИCT ЗAHЯT
C         ЗAHECEHИE B YKAЗATEЛЬ TEKYЩEГO
C         OБMEHA ЧTEHИЯ ЗOHЫ HA CBOЙ ЛИCT
 ,XTA,MADORSPB.ЧИCTKA ЛИCTA
 A,AAX,CUORD.И KOП
 ,AOX,KOWNP
 ,AOX,GKYCREAD.OБЩ.KYC ЧTEHИЯ
 J,ATX,DBUC-SM
 ,UJ,TRM2
 COMTRA:,BSS,
C         OБЫЧHЫЙ OБMEH (HE CEKTOPHЫЙ)
C
C         OБHYЛEHИE CЛYЖEБHЫX CЛOB
 R,VTM,1-8.ДЛЯ OБHYЛEHИЯ
 W,VTM,1-8.ДЛЯ BЫTAЛKИBAHИЯ
 *4:J8,UTC,
 R,ATX,8-1
 R,VLM,*4
C         YCTAHOBKA TEKYЩEГO OБMEHA.
 A,XTA,CUORD
 J,ATX,DBUC-SM
C         ПPOBEPKA HA ЧTEHИE И PAЗMETKY.
C         ПPИ HИX CЛYЖEБHЫE CЛOBA HE HYЖHЫ.
 ,AAX,C7
 ,AEX,XRAZMT
 ,UZA, TRM1.PAЗMETKA
 ,AEX,XRAEQRE
 ,UZA,TRM2.ЧTEHИE
C......   ФOPMИPOBKA CЛYЖEБHЫX CЛOB
C
C      ЗA AДPECOM И ИHДEKCOM ЗAKAЗA
 R,VJM,CRADR
  M,XTA,WL1-1
  R5,CALL,AUTOR
  J8,ATX,IDEN
 ,XTA,Y1.ПOЛHЫЙ AДPEC
 ,ASN,64-24
 ,ATX,Y1
 M,XTA,WL2-1.KC
 ,ASN,64+24
 ,AOX,Y1.KC И AДPEC
 J8,ATX,.1-OЙ ДOPOЖKИ
 M,XTA,WL2-1
 ,AAX,B24/1
 ,AOX,Y1.
 ,ARX,BIT25.KC И AДPEC
 J8,ATX,4.2-OЙ ДOPOЖKИ
C         ЗAHECEHИE ИMEHИ ПAKETA.
 A,XTA,DNAMIN
 J8,ATX,NAMED1
 J8,ATX,NAMED2
 ,U1A,TRM1
  ,33,700B.HET ИMEHИ MД
C         BЫTAЛKИBAHИE
 TRM1:,ATX,1
 W,VLM,TRM1
 TRM2:,BSS,.
   :   ,24,2003B
C         COБCTBEHHO BЫДAЧA OБMEHA.
 R,VJM,TRANSF
 ENDDSDM:,BSS,
C         YCTAHOBKA ФЛAГA ИДYЩEГO OБMEHA:
C         ( COPC1(J) .EQ. 0B ).
 ,XTA,
 J,ATX,COPC1-SM
C         YCTAHOBKA MACKИ ГPП И KOHEЦ PAБOTЫ
  R,VJM,PERMIR/T
 END DISC:,BSS,.
C         ГAШEHИE ПPИЗHAKA ЗAПYCKA
C         OБPAБOTKИ ДИCKOBЫX ПPEPЫBAHИЙ.
C         ГAШEHИE БИTA B ШKAЛE
C         PAБOT ЗAДAЧИ OБMEHA
   :   ,24,2003B
 ,XTA,SCTATRA
 J,AOX,BITDISK-SM
 J,AEX,BITDISK-SM
 ,ATX,SCTATRA
 ,UJ,TRAINT
 GET KMD:,BSS,.
C......   ПPИBЯЗKA KMД K MAШИHE
  ,XTA,
  J,ATX,WAITKMD-SM.HE ЖДEM
C         YCTAHOBKA ФЛAГA 'KMД ПPИBЯЗAH'.
 ,XTA,BIT48
 J,ATX,KMD OWN-SM
C         CБPOC ФЛAГA ACД (FLASD(J).NE.0)
 J,ATX,FLASD-SM
 ,UJ,LTWSC
C
C*****
C
  YCTBEPCB:,ENTRY,
C
 YCTBEPCB:,BSS,
C......   ПOДПPOГPAMMA YCTAHOBKИ BEPHOЙ
C      CBEPTKИ B CЛYЖEБHЫX CЛOBAX.
C      R5 - ЧИCЛO CЛYЖEБHЫX CЛOB.
C      ПOPTИT R5.
C
C      YCTAHOBKA PEAKЦИИ HA YПP.
  ,XTA,R/INTCN
 ,ATX,RECONNUM
C      ЦИKЛ YCT.BEPH.CBEPTKИ
 YCB1:,XTA,C2003.БЛOKИPOBKA
 ,ATI,21B.OCTAHOBA
 ,ATI,.ЛO YПP
 J8,UTC,
 R5,XTA,-1
 ,ATI,
 CONTL:,BSS,.ЗAПИCЬ
 J8,UTC,
 R5,ATX,-1
 R5,UTM,-1
 R5,V1M,YCB1.KOHEЦ ЦИKЛA
C++++++++++++++++++++++++++
 a,xta,DNAMIN+34
 j8,atx,NAMED1
 j8,atx,NAMED2
C++++++++++++++++++++++++++
 CHCTY:,BSS,
C......   ПOДПPOГPAMMA CHЯTИЯ БЛO-
C      KИPOBKИ OCTAHOBA ПO YПP.
 ,XTA,C13
 ,ATI,21B
 ,ATI,
 R,UJ,
  R/INTCN:,,
  ,Z00,AFINTCN
 AFINTCN:,BSS,.
C  PEAKЦИЯ HA YПP
C  B SAVAC 48-1 ЧИCЛA
 ,XTA,SAVAC
 ,UJ,CONTL
 STOP:,BSS,
C......   БЛOK PEAKЦИИ HA HEГOTOBHOCTЬ MOД.
C         ПPИ HAПPABЛEHHOCTИ ЗAKAЗA HA
C         MOДYЛЬ OH CHИMAETCЯ. ИHAЧE ЗAKAЗ
C         OTKЛAДЫBAETCЯ ДO ПOЯBЛEHИЯ ГT.
C
C         MOЖHO ЛИ OCBOБOДИTЬ MOДYЛЬ:
  ,XTA,MONOS
  A,AAX,BIT48
 W,VTM,KERNOR.TT TEKCT 'HEГT'
 R,VTM,ST1.ПOCЛE TTPR
 ,U1A,TTPRWBL.ПAKET ЗAHЯT
C         HE БЫЛ ЛИ MOДYЛЬ YЖE CBOБOДEH:
 A,XTA,DNAMIN
 ,UZA,ST1.ДA
C         OCBOБOЖДEHИE MOДYЛЯ OT ПAKETA И
C         OPГAHИЗAЦИЯ COOБЩEHИЯ OБ ЭTOM B
C         CTATИCTИKЫ. (BPEMEHHO ПOPTИM A).
C         ЧИCTKA ГPЯЗИ B SCKILD.
  A,UTM,30B
  :,24,2003B
  ,ATX,YINT1.ИMЯ ПAKETA
 A,XTA,BIT16-30B
 ,AEX,FULWOR
 ,AAX,SCKILD
 ,ATX,SCKILD
 ,XTA,
 A,ATX,DNAMIN-30B
 ,ITA,A.HOMEP MOДYЛЯ
 ,ATX,YINT
 ,XTA,CODSTNR
 ,UJ,STATIST
 CODSTNR:2,Z03,YINT
 ,Z01,ST0
 ST0:,24,3
 A,UTM,-30B.ИCПPABИM A
 ST1:,BSS,
C         ПEPEBOД OM-HMД B COCTOЯHИE 'HEГT'
 A,XTA,BIT48
 ,AEX,FULWOR
 ,AAX,DREADY
 ,ATX,DREADY
C         A KYДA CEЙ ЗAKAЗ HAПPABЛEH:
 A,XTA,CUORD
 ,AAX,BTMOD
  R,VTM,IFLTWSC
 ,U1A,MOVEQUE.ЗAKAЗ HA MOДYЛЬ
C         YCTAHOBKA ПPИЗHAKA ABAPИИ B
C         ЗAKAЗ (HA ПAKET) B WL1.
 A,XTA,SQTM
 ,ANX,
 ,ATI,M.ИHДEKC ЗAKAЗA
 M,XTA,WL1-1.KYC
 ,AOX,BINTR.ПPИЗHAK AB.
  M,ATX,WL1-1
  ,ASN,64+42
  ,ATI,RL
  RL,VZM,IFRDTA
  ,XTA,FULWOR.BЫЧИCTKA ИЗ
  RL,AEX,BITS.ШKAЛЫ ЗAДAЧ,
  ,AAX,EXCHSC.ЗAKPЫTЫX ПO OБMEHY
  ,ATX,EXCHSC
 ,UJ,IFRDTA
 MOVEQUE:,BSS,
C......   ПOДПPOГPAMMA ПPOДBИЖKИ OЧEPEДИ
C         ПO K-TOMY MOДYЛЮ.
C         ЗAДAЧA-XOЗЯЙKA ЗAKAЗA OTKPЫBAETCЯ.
C         ECЛИ B OЧEPEДИ ПO K-MY MOДYЛЮ
C         ECTЬ EЩE ЗAKAЗЫ, TO K-ЫЙ БИT
C         ЗAHOCИTCЯ B ШKAЛY
C         ЖДYЩИX ЗAKAЗOB TWSC.
C         ЧИCTЯTCЯ CЧETЧИKИ OШИБOK:
C         COBT И COKAL.
C         ПOPTИTCЯ R9,W,P,N,RL,Y3,Y4,Y5,R5
C         ECЛИ W.NE.0, TO ЗAДAЧE
C         ГOTOBИTCЯ BЫXOД B ERRMACRO.
C         W-AДPEC TEKCTA AЦПY.
C   ...   OTKPЫTИE ЗAДAЧИ-XOЗЯЙKИ.
C         OПPEДEЛEHИE MECTA B ГЛAB.OЧ.
 RL,VJM,PRIRIND
  R,UZA,
 ,ATI,M
C         HE HAДO ЛИ ДABИTЬ ЗAДAЧY
 W,VZM,OPT.HE HAДO
C         HAДO, БYДET ПPABKA ИПЗ ЗAДAЧИ.
C         OПPEДEЛEHИE ДHЗ И AДPECA ИПЗ.
  KOKHEM:,BSS,
 M,XTA,WL1-1.KYC ЗAKAЗA
 ,ASN,64+42
 ,ATI,R9.ДHЗ ЗAДAЧИ
 R5,VTM,OPT.BOЗB.CПEЦ.KAH.
C         HE ИMЯ ЛИ ЧИTAЛOCЬ:
 W,UTM,-1
 W,VZM,*2.O ДA
 ,XTA,
 ,ATX,Y.0 ДЛЯ CYMMATOPA
 *2:,XTA,Y.ИMЯ ПAKETA
 R9,VZM,ENSPTR.KAKHAЛ OПOЗH.
  KILLOPER:,BSS,
C         C ЗAKPЫTИEM ЛИ ЗAДAЧИ CEЙ ЗAKAЗ:
 M,XTA,WL1-1
 ,AAX,BCLOSE
 ,UZA,NONOWN.CTAHД.CЛYЧAЙ
C         ECTЬ ЗAKPЫTИE ЗAДAЧИ.
 N,VTM,Y.ДЛЯ CYMMATOPA
 R9,VTM,Y1.ДЛЯ PMP
 ,UJ,TOABCHT.HA KOKAHЬE
 NONOWN:,BSS,
C         HEYДAЧA HA ЗAKAЗE БEЗ БЛOKИPOBKИ
 M,MTJ,R9.ФOPMИPOBKA
 R9,UTM,WL1-1.ДЛЯ CYMMATOPA
C         ПOДЛEЖИT ЛИ ЗAДAЧA BЫБPOCY:
 M,XTA,WL1-1
 ,AAX,BBSTOP
 ,UZA,TOABCHT.ПOДЛEЖИT
 W,VTM,
 TOABCHT:,BSS,
C   ...   OKOHЧATEЛЬHAЯ ПOДГOTOBKA BЫЗO-
C         BA П/П ABAPИЙHOГO CHЯTИЯ.
  M,XTA,WL1-1
 A,VJM,ABCHT
  RL,VJM,YCTA
 OPT:,BSS,.
C         COБCTBEHHO OTKPЫTИE ЗAДAЧИ.
C+
C++++   ИДEЯ M.ГYPEBИЧA O MOHOПOЛИЗAЦИИ
C++++   ПOKA HE ПPOXOДИT ИЗ-ЗA R-TPAKTA
C+      HE ECTЬ ЛИ ЭTO ЗAKAЗ, CBЯЗAHHЫЙ
C+      C MOHOПOЛИЗAЦИEЙ HAПPABЛEHИЯ
C+M,XTA,WL1-1
C+,AAX,BMONCH
C+,UZA,CLOSQTM.HET
C+      CHЯT CПEЦ.ЗAKAЗ.  ECЛИ БЫЛO
C+      ЧTEHИE, FLMNCH(J) YBEЛИЧИBAETCЯ
C+      HA 1 (MOHOПOЛИЗAЦИЯ),  B
C+      ПPOTИBHOM CЛYЧAE YMEHЬШAETCЯ HA 1
C+A,XTA,CUORD
C+,AAX,C7.KOП
C+,AEX,XREAD
C+,UZA,/1.ЧTEHИE
C+,XTA,BIT2
C+/1:,X-A,BIT1
C+J,A+X,FLMNCH-SM
C+,UZA,/2.BCE B ПOPЯДKE
C+       ЛИШHЯЯ  V-OПEPAЦИЯ
C+,XTA,
C+ /2:J,ATX,FLMNCH-SM
C+CLOSQTM:,BSS,
C   ...   ПPOДBИЖKA OЧEPEДИ ЗAKAЗOB.
C         XOЗЯЙKA YЖE OTKPЫTA.
  J8,MTJ,11.AДPEC CЛ.CЛOB
  R9,VJM,KOHOБMEH
C         HE БЫЛ ЛИ CЛEДYЮЩИЙ ЗAKAЗ
C         BЫKИHYT OПEPATOPOM.
 A,XTA,SQTM
 RL,VJM,*6
 R,UZA,.HET ЗAKAЗOB
 ,ATI,M.ИHДEKC ЗAKAЗA
C         A БЫЛA ЛИ HA HEM ABAPИЯ:
 M,XTA,WL1-1
 ,AAX,BINTR
 ,UZA,IFRDTA.HET
C         HET ЛИ ПPИKAЗA CHЯTИЯ 'ДKO'.
 ,XTA,SCKILD
 A,AAX,BIT16
  ,UZA,HET ДKO
  ,AEX,SCKILD
  ,ATX,SCKILD
  W,VTM,DERT
  ,UJ,KILLOPER
  HET ДKO:,BSS,
 M,XTA,WL1-1
 ,ASN,64+42.ДHЗ ЗAДAЧИ-
 ,ATI,RL.-XOЗЯЙKИ
 RL,XTA,BITS
 ,AAX,OUTJOBSC
  ,UZA,IFRDTA
  W,VTM,1.БEЗ
  ,UJ,KOKHEM.ERRMACRO
 IFRDTA:,BSS,
C         A HET ЛИ EЩE ЗAKAЗOB
 RL,VJM,PRIRIND
 R,UZA,.HET
 ,ATI,M
C         ПPИEM HOBOГO ЗAKAЗA B OБCЛYГY.
 INTOSYS:,BSS,
C......   ПOДПPOГPAMMA ПPИEMA B OБCЛYГY.
C      MECTO HOBOГO ЗAKAЗA - WL1(M)
C      YCTAHAB.HOBЫЙ TEKYЩИЙ ЗAKAЗ(CUORD)
C      ПPИ YCTAHOBKE ЧИCTЯTCЯ CЧETЧИKИ.
C      CTABИTCЯ БИT B OЧEPEДЬ ЖДYЩИX
C      ПOДBOДOB(MWSC(J). CTAPOE ЗHAЧEHИE
C      MWSC ПOMHЩAETCЯ B YЗ.
C      ПOPTЯTCЯ: R9,Y3,Y4,Y5,RL=15
C
C01      ПEPEKOДИPOBKA ПOЛЯ KOMAHДЫ
C      (18 И 3-1) C YЧETOM REGW.
C
C  O2     YCT.CTPOKИ TAБЛИЦЫ KOДOB.
 R9,VTM,TABKYC.TAБЛ.ПEPEKOД.
 ,XTA,TYPWR
 ,UZA,IS1.PEЖИM ПPOCTOЙ
 R9,UTM,2.ЗAПИCЬ C CPABH.
 IS1:,BSS,
 M,XTA,WL1-1
 ,AAX,BIT18.CTAPШ.БИT KOM.
 ,UZA,IS2
 R9,UTM,1.
C  02
 IS2:,BSS,
C  02     YCT.HOBOГO KOДA ПO
C         CTPOKE И PAЗPЯДAM 3-1 CTAP.
 M,XTA,WL1-1
 ,AAX,C7.3-1 CTAPOЙ
 ,ASN,64-2.*4
 ,ATI,RL
 R9,XTA,.CЛOBO TAБЛИЦЫ
 RL,ASN,64-28
 ,AUX,MAKOM.HOBAЯ KOM.
 A,ATX,CUORD
C  02
C01
CO1       YCTAHOBKA KYCA YBY И TEKYЩEГO
C         AДPECA, PABHOГO ПOKA HAЧAЛЬHOMY
 ,XTA,MASK WL1.ЧTO BЗЯTЬ ИЗ WL1
 M,AAX,WL1-1
 ,A-X,BIT28
 A,AOX,CUORD
 A,ATX,CUORD
C01        KOPPEKЦИЯ AДPECA ДOPOЖKИ B
C         COOTBETCTBИИ C TИПOM ДИCKOB.
 J,ARX,TYPD-SM
  ,UZA,BIGDI.ДИCKИ-29
C  02     KOPPEKЦИЯ ДЛЯ MAЛЫX ДИCKOB:
C         ПEPEXOД OT ЗOH K ДOPOЖKAM.
 ,ASN,64+25
 ,AAX,C7777
 ,ATX,Y5.ЗOHA
 ,ASN,64-1.ПEPEHECEHИE 1PЦГ
 ,ARX,B48/13.B MЛAДШИЙ PAЗPЯД
 ,AAX,C7777.ПOЛЯ AДPECA
 ,UJ,ECORAD
C  02
  CORCAM:,BSS,..........
C   02        KOPPEKЦИЯ ДЛЯ ЗABOДCKOГO KMД
  A,XTA,CUORD
  ,ASN,64+25
  ,AAX,C7777
  ,ATX,Y5
C   03       ЦИKЛИЧ.CДBИГ 12P BПPABO
  ,ASN,64+1
  ,ATX,Y4
  ,YTA,
  ,ASN,64+36
  ,AOX,Y4
  ,UJ,ECORAD
 BIGDI:,BSS,
C  02     KOPPEKЦИЯ ДЛЯ БOЛЬШИX ДИCKOB:
C         ЗABOДCKOЙ ЛИ KMД :
  ,XTA,PRDK
  J,AAX,BIT24-1
  ,UZA,CORCAM.-ДA
C   02        ДYБHEHCKИЙ KMД-29
  A,XTA,CUORD
  RL,VJM,DIV10
C         BЫДEЛEHИE HOMEPA ГPYППЫ ГOЛOBOK.
 15,VTM,Y4
C         ЦИЛИHДPA И ГPYППЫ ГOЛOBOK.
C    03   ПOЛYЧEHИE HOMEPOB
 ,XTS,Y3.AДPEC/10
 ,ASN,64+1.HOMEP ЦИЛИHДPA
 ,ATX,Y3
 ,YTA,.HOMEP ГPYППЫ
 ,ASN,64+36.ГOЛOBOK
 15,AOX,.HOMEP ГOЛOBKИ
C    03   OKOHЧATEЛЬHAЯ ФOPMИPOBKA AДPECA
 ,XTS,Y3
 ,ASN,64-2
 ,ARX,Y3
 ,ASN,64-1.(N ЦИЛИHДPA)*10
 15,ARX,
 ECORAD:,BSS,
C  02     BПИCЫBAHИE AДPECA B CUORD
 ,AEX,Y5
 ,ASN,64-25
 A,AEX,CUORD
 A,ATX,CUORD
C  02
C01       YCT.БИTA B OЧ. ЖДYЩ.ПOДBOДOB
 J,XTA,MWSC-SM.OЧ.ЖДYЩ.ПOДB.
 ,ATX,Y3.CTAPOE ЗHAЧEHИE
 KI,AOX,BIT8
 J,ATX,MWSC-SM.HOBOE ЗHAЧEHИE
C01
C
C  02     OПPEДEЛEHИE ДHЗ ЗAДAЧИ.
 M,XTA,WL1-1
 ,ASN,64+42
 ,ATI,R9
C  02
C         C ЗAKPЫTИEM ЛИ ЗAДAЧИ CEЙ ЗAKAЗ:
 M,XTA,WL1-1
 ,AAX,BCLOSE
 ,U1A,WITHCLOS.C ЗAKPЫTИEM
C01       ЗAKAЗ БEЗ ЗAKPЫTИЯ. ПPOCTAHOBKA
C         БИTA ПEPEXBATA PEAKЦИИ, ECЛИ
 M,XTA,WL1-1
 ,ASN,64-2
 M,AOX,WL1-1
 ,AAX,BIT6.B6.OR.B4. ЗAKAЗA
 SH38:,EQU,BNOTST-BIT6
 ,ASN,64+SH38
 A,AOX,CUORD
 A,ATX,CUORD
 R,UJ,
 WITHCLOS:,BSS,
C01       ДEЙCTBИЯ ПO ЗAKAЗY C ЗAKPЫTИEM
C  02     ФOPMИPOBKA B CUORD БИTOB CTATYCA
C         COГЛACHO HOMEPY CTATYCA ЗAKAЗA.
 M,XTA,WL1-1
 ,ASN,64+2
 ,AAX,C14.
 ,ATI,RL.N CTATYCA*4
 ,XTA,TBCTATYC
 RL,ASN,64.БИTЫ CTATYCA
 ,AAX,C7.CПPABA
 ,ASN,64-3
 A,AOX,CUORD
 A,ATX,CUORD
C  02     ПPOЧИCTKA 38P ДЛЯ MOHOДEЙCTBИЙ
  ,AAX,BMONCH
  ,ASN,64+1.BNOTST-BMONCH=1
  A,AEX,CUORD
  A,ATX,CUORD
C  02     ECЛИ ЗAKAЗ HE OT KAHAЛA OПOЗHA-
C         BAHИЯ, TO B CYMMATOP ЗAKAЗЧИKA
C         CTABИTCЯ HE HOЛЬ.
  R9,VZM,*5.OПOЗHABAHИE
 R9,WTC,*YCЫ.HE 0 B
 *5:,ATX,.CYMMATOP
C  02
C01
 R,UJ,
C......   KOHEЦ 'INTOSYS'.
 PRIRIND:,BSS,
C......   П/П OПPEДEЛEHИЯ ИHДEKCA ПO WL1
C         HAИБOЛEE ПPИOPИTETHOГO ЗAKAЗA
C         HA MOДYЛЬ. PEЗYЛЬTAT HA CYMMATOPE
C         И B ЯЧEЙKE Y3.
C         ПPИ OTCYTCTBИИ ЗAKAЗOB CYMMATOP=0.
C          OБPAЩEHИE ПO RL.
 ,XTA,DREADY
 A,AAX,BIT48
 ,UZA,*6.MOДYЛЬ HE ГOTOB
 A,XTA,SQTM
 *6:,AOX,SCTMOD
 A,AAX,SQTM
 RL,UZA,.HET ЗAKAЗOB
 ,ANX,
 ,ATX,Y3
 RL,UJ,
 TTPRCO:,BSS,.
C......   ПOДПPOГPAMMA ПPOДBИЖKИ
C         CЧETЧИKA OШИБOK (COBT)
C         ЧEPEЗ 8 PAЗ ПEЧATЬ HA TT.
C         ПOPTИT W,RL,YINT,YINT1.
C
C         ПOXOД HA CTATИCTИKY.
 RL,VJM,GOSTA
 A,XTA,CUORD.ЗДECЬ CЧETЧИK
 ,ARX,BITCO.+1
 A,ATX,CUORD
 ,AAX,CDIAG
 ,AEX,BITDIAG
 R,U1A,.HE KPATHO 8
 TTPR:,BSS,
C......   ДИAГHOCTИKA HA TEЛETAЙПE
C         AДPEC ПEPBOГO CЛOBA B W
C
C         HET ЛИ ПEPEXBATA PEAKЦИИ:
C         ECЛИ ECTЬ, TT БЛOKИPYETCЯ.
 A,XTA,CUORD.
 ,AAX,BBLTT
 R,U1A,.TT БЛOKИPOBAH
 TTPRWBL:,BSS,
C......   П/П ПEЧATИ HA TT БEЗ KOHTPOЛЯ
C         HAЛИЧИЯ БИTA БЛOKИPOBKИ ДИAГHOCTИK
C
C         ФOPMИPOBKA AДPECA B KOДE ISO
C                ФOPMAT COOБЩEHИЯ :
C             NN CCCГГ ШTT ZZZZ PPPPPP
C       NN     : HOMEP MOДYЛЯ
C       CCC    : HOMEP ЦИЛИHДPA
C       ГГ     : HOMEP ГOЛOBKИ
C       TT     : ШИФP ЗAДAЧИ
C       ZZZZ   : HOMEP ЗOHЫ ИЗ WL1
C       PPPPPP : PEГИCTP COCTOЯHИЯ(9-24 PAЗP.)
C
 A,XTA,CUORD.TEKYЩИЙ ЗAKAЗ
 ,AAX,BIT37.ДГ*2048
  ,ATX,Y41
  A,AEX,CUORD
  ,ASN,64+25
  ,AAX,C7777
  RL,VJM,DECLET
C      HA CYMMATOPE AДPEC БEЗ ДГ
C      B ДECЯTИЧHOM БAЙTOBOM BИДE
  ,ASN,64+8
  ,ATX,Y43.N ЦИЛИHДPA
  ,YTA,
  ,ATX,Y4.N ГOЛOBKИ БEЗ ДГ
C      ПPOБA TИПA ДИCKOB И KMД
  J,ARX,TYPD-SM
  ,U1A,LITDUB.ДИCKИ-7
  ,XTA,PRDK
  J,AAX,BIT24-1
  ,U1A,LITDUB.ДYБH.KMД-29
C      ЗABOДCKOЙ KMД-29
C      ФOPMИPOBKA HOMEPA ГOЛOBKИ
  ,XTA,Y41
  ,ASN,64-3
  ,AOX,Y4
  ,ASN,64+39
  ,AAX,C37
  RL,VJM,DECLET
  ,ASN,64-32
  ,UJ,TOY41
C      MAЛЫE ИЛИ ДYБHEHCKИE ДИCKИ
C      ФOPMИPOBKA HOMEPA ГOЛOBKИ
  LITDUB:,XTA,Y41
  ,ASN,64-11
  ,AOX,Y4
  ,AUX,MU1/177
  TOY41:,ATX,Y41
C      ФOPMИPOBKA HOMEPOB MOДYЛЯ И ЦИЛИHДPA
  ,ITA,A
  ,ASN,64-42
  ,AUX,MUNLET
  ,ARX,CT30
  ,AOX,Y43
  ,ATX,Y4
C         ПOДГOTOBKA ПEЧATИ N ШИФPA
C         ЗAДAЧИ-XOЗЯЙKИ ЗAKAЗA
 RL,VJM,PRIRIND
 ,ATI,RL.ИHДEKC ЗAKAЗA
 RL,XTA,WL1-1
 ,ASN,64+42.ДHЗ ЗAДAЧИ
 ,X-A,XNW.EE ШИФP
  ,ASN,64-30
  ,AUX,MUNLET
  ,AOX,Y41
 ,AOX,CTSH.6H00 Ш00
 ,ATX,Y4+1
C      ФOPMИPOBKA  N ЗOHЫ
  RL,XTA,WL1-1
  ,ASN,64+25
  ,A-X,BIT4
  ,AAX,C7777
  ,ASN,64-33
  ,AUX,MUNLET
  ,ARX,CT0000
  ,ATX,Y42
C      ФOPMИPOBKA  PC
  ,XTA,Y1
  ,APX,MPPC
  ,AUX,MUNLET
  ,AOX,CT000000
  ,ATX,Y43
 W,XTA,
 P,UTM,-CONTR
 P,V1M,*11
 W,UTM,-KERKMD
 W,V1M,*11
 ,XTA,KERWR
 *11:,BSS,
 ,ATX,TTBUF
 J,MTJ,W
 J,VTM,TTBUF
 ,ITA,J
 J,VJM,TTPRINT
   :   ,24,3
 W,MTJ,J
 R,UJ,
 DIV10:,BSS,
C......   П/П ДEЛEHИЯ HA 10 C OCTATKOM.
C         ДEЛИMOE - CYMMATOP P36-25,
C         HA BЫXOДE: ДEЛИMOИ, ЧACTHOE,
C         OCTATOK - Y5,Y3,CYMMATOP.
C         OБPAЩEHИE ПO RL.
 ,ASN,64+25
 ,AAX,C7777
 ,ATX,Y5.ДEЛИMOE
 ,A*X,C1/10.
 ,ATX,Y3.ЧACTHOE
 ,ASN,64-2
 ,ARX,Y3
 ,ASN,64-1
 ,X-A,Y5
 RL,UJ,
 GOSTA:,BSS,
C......   ПOДПPOГPAMMA ЗAПИCИ B CTATИCTИKY
C         OБPAЩEHИE ПO PEГИCTPY RL.
C         ПOPTИT YINT,YINT1,M.
C         B W AДPEC TT TEKCTA PYГAHИ.
 RL,MTJ,M.BOЗBPAT
 RL,VJM,PRIRIND
 M,MTJ,RL.BOCCT BOЗBPAT
 ,ATI,M.HOMEP B ГЛ.OЧ.
   :   ,24,2003B
 M,XTA,WL1-1.KYC ГЛ.OЧ.
 ,ATX,YINT
 A,XTA,DNAMIN.ИMЯ ПAKETA
 ,ATX,YINT1
C         ФOPMИPOBKA 0+ГO CЛOBA CTATИCTИKИ
 ,ITA,W.AДPEC TT TEK.
 ,ARX,CONSD1.N OШИБKИ =
 ,ASN,64-15.= OTH.AДP.TT TEK
 ,ARX,CONSD2.CTAHД.БИTЫ
 ,UJ,STATIST
 RETSTA:,24,3.BOЗBPAT ИЗ
 RL,UJ,.CTATИCTИKИ
 CONSD1:7,37,77777B
 15,37,-TT/TEK
 CONSD2:3,Z03,YINT
 ,Z04,RETSTA
 YCTAPE:,BSS,.
C......   ПOДПPOГPAMMA YCTAHOBKИ
C         K,KI,A:K - HOMEP MOДYЛЯ,
C         KI=K.EQ.7.A=J*8+K.
C         HA BXOДE ACC-ШKAЛA ПO MOД
C         OБPAЩEHИE ПO RL
 ,ANX,M41
 ,ATI,KI
 ,AEX,C7
 ,ATI,K
 YCTA:K,MTJ,A
 J8,J+M,A
 A,UTM,-BEGMT
 RL,UJ,
 SEXIT:,BSS,
C......   БЛOK BЫXOДA ИЗ
C         YЧACTKA CПEЦИAЛЬHЫX ДEЙCTBИЙ.
 J,XTA,CODM-SM.ECTЬ ЛИ ПOДBOДЫ
  ,U1A,CORCM.ДA,ECTЬ
C       KMД CBOБOДEH, HO MOЖET ECTЬ
C       MOHOПOЛИЗAЦИЯ HAПPABЛEHИЯ
  J,XTA,FLMNCH-SM
  RL,VTM,AFTREFUS.ПOCЛE OTBЯЗKИ
  ,UZA,REFUSE.HET MONO
C       YCTAHOBЛEHA MOHOПOЛИЗAЦИЯ.
C       ECЛИ ECTЬ ЗAK.ПOДBOДOB,TOЛKHEM ИX
  J,XTA,MWSC-SM
  ,U1A,LTWSC
  ,UJ,END DISC
  AFTREFUS:,BSS,
C       BOЗMOЖHO OTBЯЗKA БЫЛA CДEЛAHA
C       ДЛЯ ПPOПYCKA ЗAKAЗOB ДPYГИX ЭBM.
C       ECЛИ ECTЬ EЩE ЗAДAHИЯ HA BЫДAЧY
C       ПOДBOДOB,ПOПPOБYEM CHOBA ЗAXBATИTЬ KMД
  J,XTA,MWSC-SM
  ,UZA,SETMRM.HET ЗAДAHИЙ
C       ЗAПPOC KMД
  J,ATX,WAITKMD-SM.ЖДEM
  RL,VJM,REQUEST
C       MOЖET БЫTЬ OH CPAЗY ЗAXBAЧEH
  ,MOD,237B
  J,AAX,BIT29-SM
  ,UZA,END DISC.HE ЗAXBAЧEH
C        ГAШEHИE MACKИ ГPП И AB
  R,VJM,CLOMIR
  R,VJM,ZEROPC
  ,XTA,
  J,ATX,TIMEX-SM
  :,24,3
  ,UJ,GETKMD
  CORCM:,BSS,
C         KOPPEKЦИЯ ЦИKЛИЧECKOЙ MACKИ
C         ПPИOPИTETOB ДЛЯ BЫБOPA MOДYЛЯ.
 J,XTA,MACI-SM.ЦИKЛ. MACKA
 ,ASN,64+1
 ,U1A,SCI1
 ,XTA,C377
 SCI1:J,ATX,MACI-SM.HOBAЯ MACKA
C         OПPEДEЛEHИE ПPИOPИTETHOГO MOДYЛЯ
 J,AAX,CODM-SM
 ,U1A,SCI2
 J,XTA,CODM-SM
 SCI2:,ANX,M41.HOMEP MOДYЛЯ
 ,ATI,KI.B OБPATHOM
 ,AEX,C7.И
 ,ATI,K.ПPЯMOM KOДAX
 J,ATX,CNMOD-SM
C         YCTAHOBKA MACKИ ГPП.
  R,VJM,PERMIR/T
 R,VTM,END DISC
 RL,VJM,ELECT
C         YCTAHOBKA MACKИ ДИCKOB.
 J,XTA,CODM-SM
 ,UJ,SETDM
 DAFSET:,ENTRY,
C++++++++++++++++++++++++++++++++++
C    BXOД ИЗ ЗAДAЧИ OБMEHA ПOCЛE
C    ПOCTAHOBKИ B ГЛABHYЮ OЧEPEДЬ
C++++++++++++++++++++++++++++++++++
C
C         MECTO B ГЛABHOЙ O4. B N.
 N,XTA,WL1.KYC
 ,ASN,64+7
 ,AAX,C7
 ,ATI,K.N MOДYЛЯ
 ,AEX,C7.OH ЖE B
 ,ATI,KI.OБPATHOM KOДE
 ,ITA,J.ПOЛYЧEHИE
 ,ASN,64-3.N HAПPABЛEHИЯ
 ,ATI,J8.* HA 8.
 RL,VJM,YCTA.
C         YCTAHOBKA БИTA B ШKAЛY-OЧEPEДЬ
C         COOTBETCTBYЮЩEГO MOДYЛЯ
 ,NTR,7
 N,XTA,BIT48
 A,AOX,SQTM
 A,ATX,SQTM
C         HE ECTЬ ЛИ ЭTO ЗAKAЗ,
C         HAПPABЛEHHЫЙ HA ДИCKOBOД.
 N,XTA,WL1
 ,AAX,BTMODE
 ,UZA,IFINIT.OБЫЧHЫЙ ЗAKAЗ
C         YCTAHOBKA БИTA B ШKAЛY ЗAKAЗOB,
C         HAПPABЛEHHЫX HA MOДYЛЬ
  N,XTA,BIT48
 ,AOX,SCTMOD
 ,ATX,SCTMOD
 IFINIT:,BSS,
C   ...   HAДO ЛИ OCYЩECTBЛЯTЬ ИHИЦИAЦИЮ:
 N,MTJ,M
 RL,VJM,PRIRIND
 ,UZA,ENDMAC70.ЗAKAЗ HE ПYCTИTЬ
C         A CAMЫЙ ЛИ ПPИOPИTETHЫЙ
C         TOЛЬKO ЧTO ПPИHЯTЫЙ ЗAKAЗ.
 M,UTM,1
 ,ITA,M
 ,AEX,Y3
 ,U1A,ENDMAC70.HE CAMЫЙ
C         ПPИEM ЗAKAЗA B OБCЛYГY.
 R,VJM,INTOSYS.
C         B YЗ ПOMEЩEHO CTAPOE ЗHAЧEHИE
C         OЧEPEДИ ЖДYЩИX ПOДBOДOB (MWSC)
C
C         ПPOБA HA ПPИBЯЗKY.
 J,XTA,KMDOWN-SM
 ,U1A,OWN.KMД ПPИKPEПЛEH
C         ПPOБA: HYЖEH ЛИ ЗAПPOC.
 ,XTA,Y3.CTAPOE MWSC.
  ,U1A,ENDMAC70.ЗAПPOC BЫДAH
C         CПYCK ФЛAГA ИДYЩEГO OБMEHA:
C         COPC1(J).NE.0
 ,XTA,BIT48
 J,ATX,COPC1-SM
C      BЫДAЧA ЗAПPOCA И BЫXOД.0
  J,ATX,WAITKMD-SM.ЖДEM
  RL,VJM,REQUEST.ЗAПPOC KMД
C         ПPOBEPKA: HE ПPИШЛA ЛИ ПPИBЯЗKA
 R,VTM,PROGIM
 ,MOD,237B
 J,AAX,BIT29-SM
  ,UZA,ENDMAC70.-HET
  ,XTA,.CБPOC
  J,ATX,TIMEX-SM.TAЙMEPA
  ,UJ,ZEROPC
 OWN:,BSS,
C         KMД CBOЙ
 J,XTA,DBUC-SM.ECTЬ ЛИ OБMEH
  ,U1A,ENDMAC70.ECTЬ
 PROGIM:,BSS,
C         ИMИTAЦИЯ ЗAKAЗA OБPAБOTKИ
C         ПPEPЫBAHИЙ ДЛЯ BЫДAЧИ ПOДBOДA
   :   ,24,2003B
  RL,VTM,ENDMAC70
  SENTRY:,BSS,
C......   ПOДПPOГPAMMA BXOДA B YЧACTOK
C         CПEЦ.ДEЙCTBИЙ, T.E. ЗAПYCKA
C         KAHAЛA ЗAДAЧИ OБMEHA.
C         ПOPTИT R.  OБPAЩEHИE ПO RL.
  J,XTA,BIT29-SM
  R,VJM,CLOMIR
  J,XTA,BITDISK-SM
  ,UJ,GIVTRA
C
 DISINT:,ENTRY,
C++++++++++++++++++++++++++++++++++
C    PEAKЦИЯ HA ДИCKOBЫE ПPEPЫBAHИЯ
C++++++++++++++++++++++++++++++++++
C
  ,XTA,.CБPOC
  J,ATX,TIMEX-SM.TAЙMEPA
C      ПPOБA:HE ПPИBЯЗKA ЛИ.
 J,XTA,KMDOWN-SM
 R,VTM,RETDNT.BЫXOД ИЗ DISINT
  ,UZA,LINK.A ЖДEM ЛИ ПPEP
C         YCTAHOBKA ФЛAГA ASD(FLASD.EQ.0)
  ,XTA,
 J,ATX,FLASD-SM
C         OБPAЗOBAHИE DINT -
C         ШKAЛЫ ЗAKAЗAOB HA OБPAБOTKY
C         ДИCKOBЫX ПPEPЫBAHИЙ
 R,VJM,READ PC1
 ,ATX,YINT
 ,ASN,64+8
 ,ATX,YINT1
C         ПPOБA, HE БЫЛ ЛИ OБMEH.
 J,XTA,COPC1-SM
 ,U1A,NOTRI.HE БЫЛO OБMEHA
C         ЗAПИCЬ PC1 И CПYCK ФЛAГA.
 R,VJM,READR2
 ,ASN,64-8
 ,AOX,YINT1
 J,ATX,COPC1-SM
 R,VJM,ZEROPC
 J,XTA,COPC1-SM
C         COBПAЛ ЛИ ИДEHTИФИKATOP:
 ,AAX,MBIDEN.БИT HECOBПAДEHИЯ
 ,AEX,MBIDEN
 ,UZA,BADID.HE COBПAЛ
C        CБPOC ФЛAГA ACД (FLASD(J).NE.0)
 J,ATX,FLASD-SM
 NOTRI:,BSS,.
 ,XTA,YINT
 J,AAX,CODM-SM
 ,ATX,YINT
 J,ATX,DINT-SM
C       YCTAHOBKA ФЛAГA ACД (FLASD(J).EQ.0)
C       ПPИ OTCYTCTBИИ KOHЦA OБMEHA
C       И OTCYTCTBИИ KOHЦA ПOДBOДA
 J,AOX,FLASD-SM
 J,ATX,FLASD-SM
C      ЧИCTKA KOПИИ ДИCKOBOЙ MACKИ.
 ,XTA,Y INT.
 ,AEX,FULWOR
 J,AAX,CODM-SM.KOПИЯ MACKИ
 J,ATX,CODM-SM
 RETDNT:,BSS,
C         ЗAKPЫTИE ДИCKOBЫX ПPEPЫBAHИЙ,
C         MACKИ ДИCKOB И BЫXOД.
  RL,VTM,AFGVTR
  ,UJ,SENTRY
 BADID:,BSS,
C   ---   ИДEHTИФИKATOP HE COBПAЛ -
C         - HAДO ЧИTATЬ ЗAГOЛOBOK.
C
C          YCT.HЛAГA ИДYЩEГO OБMEHA:
C         COPC(J)=0
 J,ATX,COPC1-SM
C         ПOCTAHOBKA B DBUC ПPИЗHAKA
C         ЧTEHИЯ ЗAГOЛOBKA.
 R,VTM,RHEA
 ,ITA,R
 J,AEX,DBUC-SM
 ,AAX,C7
 J,AEX,DBUC-SM
 J,ATX,DBUC-SM
C         BЫДAЧA ЧTEHИЯ ЗAГOЛOBKA.
 R,VJM,READHEAD
C         YCTAHOBKA MACKИ И BЫXOД.
 R,VTM,RETURN
  ,UJ,PERMIR/T
C
 LINK:J,XTA,WAITKMD-SM
 ,U1A,ZEROPC.ЖДEM ПPEP
 ,XTA,KREF
 J,EXT,20B.OTBЯЗKA
 ,ATI,
  ,UTC,RETURN-END DISC
  SETMRM:R,VTM,END DISC
C
C   ***********************************
C   * TOT,KTO HAБЛЮДAЛ Y CEБЯ ЯBЛEHИE *
C   * ЛOЖHOЙ ПPИBЯЗKИ KMД, HAПPИMEP,  *
C   * И.P.PЫБAKOB, ПYCTЬ BMECTO CЛEДY-*
C   * ЮЩEЙ KOMAHДЫ BCTABИT TY, ЧTO B  *
C   * KOMMEHTAPИИ ПOCЛE HEE.          *
C   ***********************************
C
  R,UJ,.OБЫЧHЫЙ CЛYЧAЙ
C      ,UJ,SETMASK.OTKPЫTЬ MACKY (ДЛЯ ЛOBЛИ ЛOЖHЯKA)
C
C
C----------------------------------
C
C......   БЛOKИ ЭЛEMEHTAPHЫX ДEЙCTBИЙ
C         BCE БЛOKИ ЛИБO ПOCЛE ИCПOЛHEHИЯ
C         ПEPEДAЮT YПPABЛEHИE HA END DISC,
C         ЛИБO ЯBЛЯЮTCЯ ПOДПPOГPAMMAMИ C
C         BOЗBPATOM ПO PEГИCTPY R ИЛИ RL.
C         (ПO YMOЛЧAHИЮ - П/П C R).
 REQUEST:,BSS,.
C......   ЗAПPOC KOHTPOЛЛEPA
C       BOЗBPAT ПO RL
  R,VTM,REQ1
C
C......   OTKPЫTИE MACKИ ГPП И
C         YCTAHOBKA TAЙMEPA
  PERMIR/T:,XTA,MAX TIME
  J,ATX,TIMEX-SM
 SETMASK:,BSS,
  J,XTA,BIT29-SM
  ,UJ,PERMIR
C
  REQ1:,BSS,
 ,XTA,BIT9
 J,033,20B
 ,ATI,
 RL,UJ,
 REFUSE:,BSS,.
C......   OTKAЗ OT KOHTPOЛЛEPA.
C         ПPИ BXOДE ДOЛЖHO БЫTЬ  ACC=0.
C   BOЗBPAT ПO  RL
 J,ATX,KMD OWN-SM
 R,VJM,CLODM
 ,XTA,KREF.
 J,033,20B
 ,ATI,.
 RL,UJ,
 KREF:,LOG,50
 ZERO PC:,BSS,.
 KRER:,EQU,BIT4.
 ,XTA,KRER.
 J,033,20B.
 ,ATI,.
 R,UJ,.
  CLODM:,24,2003B
C......   ЧИCTKA MACKИ ДИCKOB.
 ,XTA,
 SETDM:,BSS,.
C......   YCTAHOBKA MACKИ PC 1.
 KSETDM:,EQU,BIT11
 ,AOX,KSETDM.ФOPMИPOBKA KYCA
 J,EXT,20B
 ,ATI,
 R,UJ,
 ELECT:,BSS,
C......   BЫБOP MOДYЛЯ. BOЗBPAT - RL.
 KI,XTA,BIT8
 ,AOX,KELECT
 J,EXT,20B
 ,ATI,
 RL,UJ,
 KELECT:,LOG,2400
 READ PC2:,BSS,.
C......   ЧTEHИE PC 2 И 9-16 PAЗPЯДOB PC 1
C         OБOЩEHHЫЙ PC2 ПOMEЩAETCЯ B Y1.
C         ПOPTИT RL, YINT1.
   :   ,24,2003B
 RL,VJM,ELECT
 R,MTJ,RL
 R,VJM,READPC1
 ,ATX,YINT1
 R,VJM,READR2
 ,ASN,64-16
 ,AOX,YINT1
  :,24,3
 ,ASN,64+8
 ,ATX,Y1.OБOБЩEHHЫЙ PC2
 RL,UJ,
 READR2:,BSS,
C......   ЧTEHИE PC2.
 ,XTA,KRPC2
 J,033,20B
 ,ATI,
 J,033,4000B
 ,AOX,BIT40
 R,UJ,
 KRPC2:,LOG,31
 READ PC1:,BSS,
C......   ЧTEHИE PC 1
 ,XTA,KRPC1
 J,EXT,20B.
 ,ATI,.
 J,EXT ,4000B
 R,UJ,
 KRPC1:,LOG,11
 MOVE0:,BSS,.
C......   YCTAHOBKA HA 0-OЙ ЦИЛИHДP.
C         ПOPTИT RL.
 KMOVEO:,EQU,BIT1
   :   ,24,2003B
 RL,VJM,ELECT
 ,XTA,KMOVEO
 J,EXT,20B.
 ,ATI,
C         OPГAHИЗAЦИЯ BЫДEPЖKИ BPEMEHИ.
C         T = 50 MИKPOCEKYHД.
 RL,VTM,1-15
  :,24,3
 ,UJ,WAITR
 MOVE:,BSS,
C......   BЫДAЧA ДBИЖEHИЯ ПO ИHФOPMAЦИИ ИЗ
C         CUORD(A). ПOPTЯTCЯ RL,YINT.
 KMOVE:,EQU,BIT2
   :   ,24,2003B
 RL,VJM,ELECT
 ,XTA,KMOVE
    J,EXT,20B
 ,ATI,
 A,XTA,CUORD
 RL,VJM,SETPATH
   :   ,24,3
C         OPГAHИЗAЦИЯ BЫДEPЖKИ BPEMEHИ
C         T = 17 MИKPOCEKYHД.
 RL,VTM,1-5.1-T/3.4MKCEK
 WAITR:,BSS,.
C         OЖИДAHИE OCBOБOЖДEHИЯ KMД
C         RL=1-T/3.4MKCEK. T-HYЖHOE BPEMЯ.
 ,ATI,
 ,ATI,
 RL,VLM,WAITR
 R,UJ,
 TRANSF:,BSS,
C......   ЗAПИCЬ, ЧTEHИE И PAЗMETKA.
C
C         BEДEHИE CЧETЧИKA OБMEHOB
 ,XTA,BIT37.EДИHИЦA CЧETЧИKA
 ,ARX,COUNTTR
 ,ATX,COUNTTR
 J,XTA,DBUC-SM.KYC ГЛAB. OЧ.
C         ЗAHECEHИE ИДEHTИФИKATOPA B Y3
 ,ASN,64+7.
 ,AAX,C3.
 ,ASN,64-4.
 ,ATX,Y3.
  ,ASN,64-3
  J,AEX,DBUC-SM
  ,ATX,YINT1
C         OПPEДEЛEHИE TИПA OБMEHA
 ,AAX,C3.
 ,ATI,P.TИП
C         ЗAПYCK YBY
  ,ITA,K.HOMEP
  ,ASN,64-7.MOДYЛЯ B
  ,AOX,YINT1.8-10 P.БYC
 ,AAX,MYBY.
 J,033,.
  ,ATI,
  RL,VTM,PUTCOM
  J,XTA,TYPD-SM
  ,UZA,ELECT.7MБ
  J,XTA,BIT24-1
  ,AAX,PRDK
  ,U1A,ELECT.ДYБHA-29
C         BЫДAЧA KOMAHДЫ B KMД
  PUTCOM:P,XTA,KYCS.KOMAHДA
 ,AOX,Y3.ИДEHTИФИKATOP
 J,033,20B.
 ,ATI,.
C         BЫДAЧA HOMEPA ДOPOЖKИ
 J,XTA,DBUC-SM
 RL,VJM,SETPATH
 ,XTA,.
 J,ATX,COPC1-SM.ФЛAГ OБMEHA.
 ,033,
 RL,VTM,8.
 CBS:,ATX,1.
 RL,UTM,-1.
 RL,VIM,CBS.
 R,UJ,.
 KYCS:,BSS,
C         KOMAHДY OБMEHA.
 ,LOG,4.ЗAПИCЬ
 ,LOG,5.PAЗMETKA
 ,LOG,3.ЧTEHИE
 ,LOG,6.CPABHEHИE
 READHEAD:,BSS,
C......   ЧTEHИE ЗAГOЛOBKA
 KRHEAD:,EQU,C7
 ,XTA,YBYRH
 J,EXT,
 ,XTA,KRHEAD
 J,EXT,20B
 ,ATI,
 R,UJ,
 YBYRH:,LOG,4440 0000
 SETPATH:,BSS,
C......   BЫДAЧA B KMД HOMEPA ДOPOЖKИ.
C         BOЗBPAT - RL. KYC ГЛ.OЧ. - YINT.
 KSET12:,EQU,BIT8
 ,ASN,64+25
 ,AAX,C7777
 ,ATX,YINT.N ДOPOЖKИ
 J,ARX,TYPD-SM
 ,U1A,SET11/1.ДИCK HA 7MГБAЙT
C       HE ЗABOДCKOЙ ЛИ KMД
  ,XTA,PRDK
  J,AAX,BIT24-1
  ,UZA,CAMKMD.-ДA
  ,XTA,YINT
C         BЫДAЧA CTAPШEГO PAЗPЯДA
 ,AAX,BIT12
 ,ASN,64+7
 ,AOX,KSET12
  SET12:,BSS,
 J,033,20B
 ,ATI,
 SET11/1:,XTA,YINT.BЫДAЧA
 ,AOX,BIT12.MЛAДШИX PAЗPЯ-
 J,033,20B.ДOB ДOPOЖKИ
 ,ATI,
 RL,UJ,
  CAMKMD:,BSS,
C       BCTABKA ДГ B KYC BЫБOPKИ HMД
  ,XTA,YINT
  ,AAX,BIT12
  ,ASN,64+2
  KI,AOX,BIT8
  ,AOX,KELECT
  ,UJ,SET12
C......   KOHEЦ ЭЛEMEHTAPHЫX ДEЙCTBИЙ
C......   PAЗHЫE KOHCTAHTЫ
 M41:,000,
 ,Z00,-41.
 C1/10:,LOG,4001 4631 4631 4632
 GKYCREAD:,LOG,400002
  MYBY:,LOG,1 4177 1600
 MASKWL1:,LOG,3 7777 7737 0120
 MADORSPB:,LOG,7777 7776 0000 0170
 MAKOM:,LOG,400 007
 MCAM:,LOG,04102 04102 04102 0
C         TAБЛИЦA ПEPEKOДИPOBKИ KOMAHД.
 TABKYC:,BSS,
 ,OCT,35402072040.7604 3A10
 ,OCT,77402072064.FE04 3A1A
 ,OCT,35402072050.7604 3A14
 ,OCT,77402072064.FE04 3A1A
C         TAБЛИЦA БИTOB CTATYCA
 TBCTATYC:,GOST,6H0000'121''20'.3,1,1,0
 ,BSS,
C......   MACKИ ABAPИЙ
C MABT   :  , EQU ,BIT4
 MCOMP:,LOG,2010
 CREADY:,LOG,60 0001
C BNEWM  :  , EQU ,BIT11   .CПA
  MNEWM  :  , LOG ,2 2000  .CПA + POФ
C B1PCK  :  , EQU ,BIT3
 TT/TEK:,BSS,
C         TEKCTЫ ДИAГHOCTИK TT
 KERNOR:,ISO,6HMДHEГT
 KEERA:,ISO,6HMD AД.
 KEERC:,ISO,6HMD KC.
 KEERYBY:,ISO,6HMD OШM
 KERBM:,ISO,6HKAЛИБP
 KERKMD:,ISO,6HMD OБ.
 KERCMO:,ISO,6HMДCTOП
 KERWR:,ISO,6HMД ЗAП
  KERSS:,ISO,6HMD SSW
 :,BSS,
C         KOHCTAHTЫ TEЛETAЙПA
  MU1/177 :,GOST,6H1'177'0000
  MUNLET  :,GOST,6H777777
  CT30    :,ISO,6H30 000
  CT0000  :,ISO,6H 0000
  CTSH    :,ISO,6H00 Ш00
  CT000000:,ISO,6H000000
  MPPC:,LOG,377 7417
C         PAЗДYБЛИPOBAHHЫE ЯЧEЙKИ.
 CNMOD:,BSS,NCHD
 KMD OWN:,BSS,NCHD
 CODM:,BSS,NCHD
 COPC1:,BSS,NCHD
 MACI:,BSS,NCHD
 FLASD:,BSS,NCHD
 DBUC:,BSS,NCHD.
 DINT:,BSS,NCHD.
 SKAL:,BSS,NCHD
 MWSC:,BSS,NCHD
 WAITKMD:,BSS,NCHD
C         PAБOЧИE ЯЧEЙKИ
 TTBUF:,BSS,
 Y3:,BSS,1
 Y4:,BSS,1
  Y41:,LOG,
  Y42:,LOG,
  Y43:,LOG,
 ,LOG,
 ,END,.

 *DISCS:,NAME,.
  DNAM:,BSS,16.ИMEHA ДП
  :,BSS,16.PEЗEPB
  ШMOД:,OCT,7777 74.ШK.BOЗM.MOДEЛEЙ MЛ
  ШФЛД:,OCT,.    ШK.PEAЛЬHЫX MOДEЛEЙ
  REZ:,BSS, . RESERVED NAMES
  ,TEXT,8HDUBNA/01
  ,TEXT,8HDUBNA/02
  ,TEXT,8HDUBNA/03
  ,TEXT,8HDUBNA/04
  ,TEXT,8HDUBNA/05
  ,TEXT,8HDUBNA/06
  ,TEXT,8HDUBNA/07
  ,TEXT,8HDUBNA/08
  ,TEXT,8HDUBNA/01
  ,TEXT,8HDUBNA/02
  ,TEXT,8HDUBNA/03
  ,TEXT,8HDUBNA/04
  ,TEXT,8HDUBNA/05
  ,TEXT,8HDUBNA/06
  ,TEXT,8HDUBNA/07
  ,TEXT,8HDUBNA/08
 ,END,.
 SPECMON:,NAME,
 DLIBMT:,LC,2.BTOPOЙ PEЗИДEHT
  *113:B,BLOCK,PULT,PASS
  *YCЫ:LC,BLOCK,ГYC
 MN*TL:,EQU,1067B
C......   MOHИTOPHЫE COMMON'Ы.
 INFLOAD*:,LC,2
  MONITOR*:,SUBP,
  /VERE/:LC,BLOCK,(2)
  ,CONT,STALOA(0),M/STL,A/CAT,INF0
 RM*:,LC,5
 A/I0:,LC,1
 A/MINUS:,LC,1
  *REGIM//:,LC,1
 BASE:,BSS,
 1,BASE,BASE
C====================================
 'ПPOBEPKA CПEЦИФИKИ ЗAДA4И
C---------
  ,FUN,70100B+PASS.N TT
  ,ASN,64-40
 ,AAX,=:016
  ,ATX,N*TT
 ,ITA,3
 ,AAX,=77
 ,AEX,=77
  ,UZA,SETDELTA
  ,ITA,3
  ,ASN,64-42
  ,AUX,=6H'''''''''7''7'
  ,AOX,=6HTASK00
  ,ATX,NAME
 14,VTM,NAME
 ,ITA,14
 ,FUN,64B.= ЗAПИCЬ NAME
C
C...   ЗAXBAT ЛEHTЫ C БCЗ :
C
   ,uj, pp
  GT:13,VTM,66B
  ,*63,DLIBMT+1.ИMЯ
  ,U1A,*+1.ЗAДAHO
  ,XTA,LIBMT.CTAHДAPT
  :,*57,2050B
  ,UZA,STOP.HET MЛ
  ,AAX,=:3
  ,U1A,STOP.HEГOT/ЗAHЯT
  ,*65,DLIBMT
  ,AOX,/FIZIM
  ,FUN,112B
  ,*72,=:11 40 35 00 37 77
C
C...   ПPOБA HAЛИЧИЯ CTAT.ЗAГPYЗЧИKA
C      B KATAЛOГE БCЗ
C
  pp:,bss,
  ,*70,=0010 3500 0066 0000
  ,XTA,73400B
  ,AEX,=6HSTALOA
  ,U1A,R/SYS.-HET
  4,VTM,-1023
  :4,XTA,73777B.ПEPEПИCЬ
  4,ATX,-1.KATAЛOГA HA
  4,VLM,*-1.OБM.ЛИCT
  ,UJ,R/SYS+1.OБXOД ЧT.ЗAГP.
  R/SYS:,BSS,
  ,*70,INFLOA.ПOCAДKA
                           'ЗAГPYЗЧИKA'
C---------OФOPMЛEHИE MOHИTOPHЫX CBЯЗEЙ
 ,XTA,ALOAD
 ,ATX,INFLOAD*
 ,ATX,RM*+2
 ,ATI,15
 ,XTA,=1100
 ,ATX,17B.MECTO PAЗДEЛA
  ,XTA,
  ,ATX,*REGIM//
 ,XTA,=76000.
 ,ATX,43B.ЛИCT OБMEHA
 ,XTA,=I0
 ,ATX,A/I0
 ,XTA,=:002
 ,ATX,A/MINUS
 ,XTA,STOP
 ,CTX,20B
 'ПOДПPABKA ЗAГPYЗЧИKA'
 CHLOAD:,BSS,
  ,XTA,/FIZIM
  ,ATX,M/STL
  ,ATX,A/CAT
  ,ATX,INF0
C=========================================
 'OФOPMЛEHИE ИMEHИ PAЗДEЛA=CЛ.ЗAДAЧИ'
C---------
 ,ITA,3.ИMЯ ИЗ 3 И.P.
 ,ASN,64+6
 ,ATX,INFORTA.ИHФ ДЛЯ ЗAДA4И
 ,YTA,
 ,AUX,TMASK.ПPEOБP. B TEXT
 ,ARX,STAND.OФOPMЛEHИE
 ,ATX,NAME
C==================================
 'ПEPECЫЛKA БЛOKA OTKAЗA OT ЛEHTЫ
C---------
 LENTGH:,EQU,HEAD-ENDHEAD
 4,VTM,LENTGH
 LOOP:,BSS,
 4,XTA,ENDHEAD
 ,UTC,1000B
 4,CTX,-LENTGH
 4,VLM,LOOP
C=======================================
 'ПY HA CЛYЖEБHYЮ ЗAДA4Y
C---------
 ,XTA,
 ,ATX,1077B.
 ,XTA,NAME
 ,ATX,1070B
 ,WTC,ALOAD
 13,VJM,
C===================================
 'ECЛИ HET ЗAДAЧИ HA MЛ'
C---------
 13,VTM,MIST
  TO TEL:,ITA,13
  ,AOX,N*TT
  ,FUN,76B
  STOP:,SJ,
 MIST:,ISO,6HTASK'041'
 ,OCT,
 SETDELTA:,BSS,
 ,ITA,3
  ,ASN,64+6
C---------  YCTAHOBKA ДEЛЬTЫ
  ,ATI,14
  14,UTM,-777B
  14,VZM,SETNAM
  ,*52,DLIBMT
  PRIZ:,*63,DLIBMT+1
  :,UZA,FST
  ,AUX,=6H'137''137''137''137''137''137'
  FN:,ATX,ZONE
  ,*63,DLIBMT
  ,ASN,64-39
 ,AUX,=6H'7''7''7''0''0''0'
  ,ARX,=6H000''''''
 ,ATX,ZONE+2
 13,VTM,ZONE
  ,UJ,TOTEL
  FST:,XTA,=6HD73LIB
  ,UJ,FN
C
C...   ЗAПPOC HOBOГO ИMEHИ БCЗ
C      ( ПPИKAЗ 77777/ )
C
  SETNAM :12,VTM,TGN
  ,*63,ГYC
  ,ASN,64+15
  ,ASN,64-40
  ,AOX,=6H'100':ИMЯ
  12,ATX,
  ,ITA,12
  ,AOX,N*TT
  ,FUN,76B
  ,XTA,=1
  ,FUN,66B
  ,ASN,64+12
  ,ATX,ZONE
  NORM:,XTA,ZONE
  ,UZA,WRN
  ,AAX,=:77
  ,U1A,WRN
  ,XTA,ZONE
  ,ASN,64-6
  ,ATX,ZONE
  ,UJ,NORM
  WRN:,XTA,ZONE
  ,*52,DLIBMT+1
  ,UZA,PRIZ
  ,FUN,66B.ЗA NБOБ
  ,APX,=171717170000
  ,ASN,64+32
  ,FUN,203B
  ,AEX,
  ,XTA,=7777
  ,*53,DLIBMT+1
  ,UJ,PRIZ
 ZONE:,ISO,12HD73LIB ЗOHA
 ,OCT,
  TGN:,ISO,12HQ:ИMЯ БCЗ '77'''
C====================
 ' OTKAЗ OT ЛEHTЫ OT ИMEHИ ЗAДA4И
C---------
 HEAD:,BSS,
 1,BAS,*
 1,VTM,1000B
  ,XTA,N*TT
 ,ATX,MN*TL
 15,VTM,73401B.
  ,*70,ПOДBOД
  ,XTA,BIT66
  ,*57,4000B
 ,XTA,INFORTA
 ,ATI,1
 ,WTC,1077B
 ,UJ,
  ПOДBOД:,030,
  ,066,
  BIT66:,OCT,0000 0000 004
  N*TT:,LOG,
 ENDHEAD:,BSS,
 INFORTA:,BSS,1
C---------
C====================================
 'BCЯKAЯ BCЯЧИHA'
C---------
 INFLOA:8,014,3503B.
 ,021,101B.C 2101 - 1 CEKT
  /FIZIM:,LOG,10 0000 0066 0000
C---------
 ALOAD:,LOG,73401.
 LIBMT:,TEXT,8HD73LIB '05'
 TMASK:,TEXT,8H    '07''07'
 TRACENAM:,BSS,
C---------
 STAND:,TEXT,8HTASK00
 NAME:,BSS,1
 ,BSS,2
 ,END,SPECMON
 MDRECOGN:,NAME,.26.06.74
C
C======   STATISTICS FOR INGENEERS
C======   RESIDENT OF MD-RECOGNIZING
C
 NR/MD:,SUBP,.
C******   OПPOC ГOTOBHOCTИ MOДYЛEЙ MД,
C
C......   ИCПOЛЬЗYEMЫE ИHДEKC-PEГИCTPЫ:
 T:,EQU,4.
 RJ:,EQU,6.
 R:,EQU,7.
C
  * И П Д :LC,BLOCK,(1),XNFM(5),XNCM
  NFM:,WEQ,XNFM
  NCM:,WEQ,XNCM
 Ш/FULL:,EQU,*ИПД+11B
 Ш/ПOTOK:,EQU,*ИПД+12B
 SMASAV:LC,BLOCK,SAVAC
 C MDREC:LC,BLOCK,SC MDR,SC REC,NMOD
 ,CONT,WA CNTS,CEKMET
 *YCЫ:LC,BLOCK,(1),RDSCAL
 *SWCMN:LC,BLOCK,SBUSYFLG,SWSCAL
 BPEMEHA:LC,BLOCK,T1(4)
 C37:,LC,1
  B30/16:LC,BLOCK,MSHI
  B16/1:,LC,1
 C7:,LC,1.
 C177:LC,BLOCK,MASTA
 *STATBUF:LC,BLOCK,MBLO,MOUT
 STATIST:,SUBP,.
 NRESCALL:,SUBP,.
  NR/STAT:,SUBP,
  ABTIME:,LC,
  *530:B,BLOCK,STAT IS,NEWTIME
  *TLWORK:LC,BLOCK,OLDTIME
  *CB:LC,BLOCK,(13),INT0
  BLSTOUT:LC,BLOCK,TSEC
  STATUSMT:LC,BLOCK,MBUS,OLD(4),NEW(4)
  ,CONT,W05741(4)
  SCALES:LC,BLOCK,(4),SCALCLTA
  TRA TAPE:,SUBP,
  CLANEWTA:,SUBP,
  *677:B,BLOCK,BITS
  ,CONT,B48,B47,B46,B45,B44,B43,B42,B41
  ,CONT,B40,B39,B38,B37,B36,B35,B34,B33
  ,CONT,B32,B31,B30,B29,B28,B27,B26,B25
  ,CONT,B24,B23,B22,B21,B20,B19,B18,B17
  ,CONT,B16,B15,B14,B13,B12,B11,B10,B9
  ,CONT,B8,B7,B6,B5,B4,B3,B2,B1
C-------------
 ,33,01234
 ,XTA,MBLO.
 ,UZA,NO STA.CTAT.БЛOKИP.
 ,XTA,MOUT.
 ,AOX,TSEC.
 ,UZA,NO STAT.
 T,CALL,STATOUT.
 NO STAT:,XTA,CEKMET
 ,UZA,NO STA
 ,XTA,WCNT
 ,ARX,B1.
 ,AAX,MA STA
 ,ATX,WCNT.
 ,UIA,NO STA.
 R,VTM,-3
 *L1:R,XTA,T1+3
 R,ATX,*T1+3
 R,VLM,*L1
  13,VTM,1-NCM
  14,VTM,NFM
 T,VTM,
 Ф MM:14,XTA,*YCЫ.
 ,ATI,12
 ,AAX,M SHI
 ,UZA,ФMM1
 ,XTA,Ш/ПOTOK
 14,AAX,BITS
 ,UZA,HEП 1
 ,UTC,2
 HEП 1:R,VTM,
 ,NTR,6
 R,VZM,HEП 2
 ,UTC,37B
 HEП 2:T,UTM,1
 12,VZM,ФMM1.ИПЗ OTKAЧAHO.
 12,XTA,-1
 R,A+X,*T1
 R,ATX,*T1
 12,XTA,110B
 R,A+X,*T2
 R,ATX,*T2
 ФMM1:14,UTM,1
 13,VLM,ФMM
 R,VTM,-1
 10,VTM,
 *L2:10,XTA,*T1
 ,NTR,3
 ,A+X,INT0
 ,ASN,64-20
 R,ATX,*T1+1
 10,XTA,*T2
 ,A+X,INT0
 R,AOX,*T1+1
 ,ASN,64-8
 R,ATX,*T1+1
 10,UTM,2
 R,VLM,*L2
 ,XTA,RDSCAL
 ,AOX,SWSCAL
 ,WTC,SBUSYFLG
 ,AOX,BITS
 ,ATX,*T3
 ,ITA,T
 ,AAX,C37
  ,UZA,*1
 ,AOX,*T1
  *1:,BSS,
 ,ATX,*T1
 ,ITA,T
 ,ASN,64+5
  ,UZA,*2
 ,AOX,*T2
  *2:,BSS,
 ,ATX,*T2
 ,XTA,MИHMET
   :   ,24,2003B
 ,UJ,STATIST
 MИHMET:3,Z00,*T1
 1,Z03,NO STA.
 NO STA:,BSS,.
  15,VTM,*T1
  14,VTM,-3
  :,24,2003B
  ,XTA,
  :14,XTS,NEW+3
  14,AEX,OLD+3
  15,AOX,
  14,VLM,*-1
  ,ASN,64+8
  ,AAX,B16/1
  ,AOX,NMOD
  ,U1A,CALL
  ,XTA,2.HA TP-2
  ,AAX,STAT IS.БИT BEДEHИЯ CTAT
  ,AOX,OLDTIME. ECTЬ KAHAЛЫ?
  ,UZA,RET.- HE BEДEM
  ,NTR,2
  ,XTA,ABTIME
  ,A-X,NEWTIME
  ,U1A,RET
  ,XTA,I/STAT
  ,UJ,NRESCALL
  I/STAT:,Z00,NR/STAT
  ,Z00,RJ
C
  CALL:,BSS,
 ,XTA,IW CALL.
 ,UJ,NRESCALL.
 IW CALL:,Z00,NR/MD.
  ,Z00,RJ
 MD/RET:,ENTRY,.
  RET:,BSS,
  :,24,2003B
  ,XTA,
  ,ATX,MBUS
  ,ATX,CEKMET
  ,UJ,CLANEWTA
C
C****  OTKPЫTИE ЗAДAЧ, ЖДYЩИX MЛ/MД
C
  OPENWAIT:,ENTRY,
  ,XTA,B18
  ,CALL,K COCEДY
  ,XTA,SCALCLTA
  ,AOX,RDSCAL
  ,ATX,RDSCAL
  ,XTA,
  ,ATX,SCALCLTA
  :,24,3
  5,UJ,
C
C****  AKTИBИЗAЦИЯ ЗAKAЗOB HA OБMEH C MЛ,
C      ЗAДEPЖAHHЫX ПO HEГOTOBHOCTИ MФ.
C
  TOЛЧOK:,ENTRY,
  :,24,2003B
  2,XTA,B8
  1,AOX,W05741-3
  1,ATX,W05741-3
  1,MTJ,14
  5,MTJ,15
  ,UJ,TRA TAPE
C--------
 WCNT:,LOG,7.
C--------
 *T1:,BSS,1
 *T2:,BSS,1
 *T3:,BSS,2
 ,END,.


*tcatalog
*MAIN DD86
*CALL DATAPR*
*EXECUTE
*READ OLD 
*end file
``````
еконец
$ 